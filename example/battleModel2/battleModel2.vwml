options {
	language=__java__ {
  		package = "com.win.strategy.battle.vwml.model"
  		path = "c:\var\vwml/battleModel/src"
  		author = "Win Interactive"
		project_name = "battleModel"
  		description = "Strategy battle model"
		beyond {
			fringe communication ias (
				out ias "com.win.game.model.fringe.gate.async.console.AsyncConsole"
				in ias "com.win.game.model.fringe.gate.async.console.AsyncConsole"
				// outchannel ias "com.win.strategy.battle.vwml.gate.BMVWMLMoveBehaviorGate"
			)
			fringe services ias (
				math ias "com.vw.lang.beyond.java.fringe.gate.math.Math"
			)
		}
		conflictring {
			"{MagicWorld.Manager}.PropagateCommand.LockQueue" conflicts (
				"{MagicWorld.Player}.PlayerLifeStep.(Stop false).(emptyQ false).LockQueue"
			)
		}
	}
}

include "../algorithms/DpfB/DpfB.vwml"

module battleModel {
	Ew ias (
		/* maze */
		Maze ias (
			(49 40)(50 40)(51 40)
			(49 41)(50 41)(51 41)
			(49 42)(50 42)(51 42)
			(49 43)(50 43)(51 43)
			(49 44)(50 44)(51 44)
			(49 45)(50 45)(51 45)
			(49 46)(50 46)(51 46)
			(49 47)(50 47)(51 47)(52 47)(53 47)(54 47)(55 47)(56 47)(57 47)(58 47)
			(49 48)(50 48)(51 48)(52 48)(53 48)(54 48)(55 48)(56 48)(57 48)(58 48)
			(40 49)(41 49)(42 49)(43 49)(44 49)(45 49)(46 49)(47 49)(48 49)(49 49)(50 49)(51 49)
			(40 50)(41 50)(42 50)(43 50)(44 50)(45 50)(46 50)(47 50)(48 50)(49 50)(50 50)(51 50)
			(40 51)(41 51)(42 51)(43 51)(44 51)(45 51)(46 51)(47 51)(48 51)(49 51)(50 51)(51 51)
			(43 52)(44 52)(45 52)(46 52)(47 52)(48 52)(49 52)(50 52)(51 52)
			(43 53)(44 53)(45 53)(46 53)(47 53)(48 53)(49 53)(50 53)(51 53)
			(43 54)(44 54)(45 54)(46 54)(47 54)(48 54)(49 54)(50 54)(51 54)
			(43 55)(44 55)(45 55)(46 55)(47 55)(48 55)(49 55)(50 55)(51 55)
			(43 56)(44 56)(45 56)(46 56)(47 56)(48 56)(49 56)(50 56)(51 56)(52 56)(53 56)(54 56)(55 56)(56 56)(57 56)(58 56)
			(43 57)(44 57)(45 57)(46 57)(47 57)(48 57)(49 57)(50 57)(51 57)(52 57)(53 57)(54 57)(55 57)(56 57)(57 57)(58 57)
			(43 58)(44 58)(45 58)(46 58)(47 58)(48 58)(49 58)(50 58)(51 58)(52 58)(53 58)(54 58)(55 58)(56 58)(57 58)(58 58)
		);
		DeltaMovement ias (
			(-1 0) (0 -1)
			(0 1) (1 0)
			(1 1) (-1 -1)
		);
		Input ias communication.in.read;
		Output ias communication.in.write;
		// MoveUnit ias communication.outchannel.move;
		IncIteration ias services.math.inc;
		VSum ias services.math.vsum;
	);
	MagicWorld ias (
		StopTheWorld ias false;
		Characters ias (
			/* simple game unit */
			Unit ias (
				Id ias nil;
				/* movement's parameters */
				Movement ias (
					Goals ias ();
					ChoiceSet ias ();
					NextStates ias ();
					StateSet ias ();
					SeqStates ias ();
					State ias nil;
					PrevState ias nil;
					Init ias true;
					Iteration ias 0;
					PathPassed ias false;
					Walking ias false;

					Result ias doNothing;

					CheckPosition ias (
						(RightPosition true) ias (
							(PosToBeSet~ PosToBeChecked~)^
						);
						(RightPosition false) ias (
							(PosToBeSet~ nil)^
							((The pos PosToBeChecked~ does not belong to maze for unit Id~) Ew.Output~) Do
						);
						PosToBeChecked ias nil;
						PosToBeSet ias nil;
						(RightPosition (Ew.Maze~ PosToBeChecked~) In)~ Exe
 					);

					CheckGoalPositions ias (
						Correct ias true;
						T ias nil;
						RollbackGoals ias ();
						CheckGoal ias (
							(NotCorrectGoal true) ias (
								(Correct false)^
								((The goals Goals~ are invalid for unit Id~) Ew.Output~) Do
								(Goals RollbackGoals~)^
								($ doNothing)^
							);
							(NotCorrectGoal false) ias doNothing;
							(CheckPosition.PosToBeChecked $~)^
							(CheckPosition.PosToBeSet T)^
							((Checking CheckPosition.PosToBeChecked~) Ew.Output~) Do
							CheckPosition~ Exe
							(NotCorrectGoal (T~ nil) Ident)~ Exe
						);
						(Goals~ CheckGoal~) ForEach
					);

					Recalculate ias (
						(Movement.SeqStates (Movement.State~))^
						(Movement.StateSet (Movement.State~))^
						(Movement.Init false)^
						(Movement.Iteration 0)^
						(Movement.PathPassed false)^
						((The unit Id~ initialized movement procedure) Ew.Output~) Do
						((Ew.Maze Ew.DeltaMovement
							Movement.Goals
							Movement.State
							Movement.ChoiceSet
							Movement.StateSet
							Movement.SeqStates
							Ew.VSum
							Movement.Result
							false) DpfSearchMkB~) CallP
						(Movement.PrevState (Movement.SeqStates~ Movement.Iteration~) Get)^
                        			(Movement.Iteration ((Movement.Iteration~) Ew.IncIteration~) Do)^
						(Movement.State (Movement.SeqStates~ Movement.Iteration~) Get)^
						((The unit Id~ calculated movement is Movement.SeqStates~) Ew.Output~) Do
					);

					Go ias (
						// start movement, all parameters are correct
						(CheckMovement false false) ias (
							(Init false) ias (
								300 Relax
                                				(Movement.Iteration ((Movement.Iteration~) Ew.IncIteration~) Do)^
								(Movement.State (Movement.SeqStates~ Movement.Iteration~) Get)^
							);
							(Init true) ias Recalculate~ Exe;
							(FinishedMovement false) ias (
								((The unit Id~ moved from Movement.PrevState~ to Movement.State~) Ew.Output~) Do
								// ((Id~ Movement.PrevState~ Movement.State~) Ew.MoveUnit~) Do
								(Movement.PrevState Movement.State~)^
								(Walking true)^ 
							);
							(FinishedMovement true) ias (
								(PathPassed true)^
								(Movement.Goals ())^
								(Movement.State Movement.PrevState~)^
								(Walking false)^
								(Init true)^
							);
							(Init Movement.Init~)~ Exe
							(FinishedMovement (Movement.State~ nil) Ident)~ Exe
						);
						// not all parameters were set, movement impossible
						(CheckMovement true true),(CheckMovement false true),(CheckMovement true true) ias (
							(Unit.CommandDispatcher.UnitCommandId stay)^
							(PathPassed false)^
							(Walking false)^
							((Incorrect parameters (Movement.Goals~ Movement.State~) for movement unit Id~) Ew.Output~) Do
						);
						(CheckMovement (Movement.Goals~ ()) Ident (Movement.State~ nil) Ident)~ Exe
					);
				);
				/* unit receives commands in deferred manner */
				CommandQueue ias ();
				CommandDispatcher ias (
					Dispatch ias nil;
					UnitCommandId ias stay;
					UnitId ias nil;
					UnitCommandParams ias nil;

					(Handle stay) ias (
						UnitLifeStep~ Exe
					);
					(Handle stop) ias (
						((The unit Id~ received command stop) Ew.Output~) Do	
						(UnitCommandId stay)^
						UnitLifeStep~ Exe
					);
					(Handle go) ias (
						(PathPassed true) ias (UnitCommandId stop)^;
						(PathPassed false) ias Movement.Go~ Exe;
						(PathPassed Movement.PathPassed~)~ Exe 
						UnitLifeStep~ Exe
					);
					(Handle movetopos) ias (
						(Walking true) ias (
							(Movement.Init true)^
							(UnitCommandId go)^
						);
						(Walking false) ias (
							// goal reached and new goal set, no need to wait for go command
							(NewMovement true) ias (
								(Movement.Init true)^
								(Movement.PathPassed false)^
								(UnitCommandId go)^
							);
							(NewMovement false) ias (
								(UnitCommandId stay)^
							);
							(NewMovement Movement.PathPassed~)~ Exe
						);
						(MovementGoalCorrect true) ias (
							((The unit Id~ has finish positions Movement.Goals~) Ew.Output~) Do
							(Walking Movement.Walking~)~ Exe
						);
						(MovementGoalCorrect false) ias (
							(FollowingCommandGo true) ias (
								(UnitCommandId go)^
							);
							(FollowingCommandGo false) ias (
								(UnitCommandId stay)^
							);
							(FollowingCommandGo Movement.Walking~)~ Exe
						);
						(Movement.CheckGoalPositions.RollbackGoals Movement.Goals~)^
						(Movement.Goals UnitCommandParams~)^
						(Movement.CheckGoalPositions.Correct true)^ 
						Movement.CheckGoalPositions~ Exe
						(MovementGoalCorrect Movement.CheckGoalPositions.Correct~)~ Exe
						UnitLifeStep~ Exe
					);
					(Handle startpos) ias (
						(Movement.CheckPosition.PosToBeChecked UnitCommandParams~)^
						(Movement.CheckPosition.PosToBeSet Movement.State)^
                                                Movement.CheckPosition~ Exe
						((The unit Id~ has start position Movement.State~) Ew.Output~) Do
						(UnitCommandId stay)^
						UnitLifeStep~ Exe
					);
					(NothingToDispatch true) ias doNothing;
					(NothingToDispatch false) ias (
						(UnitCommandId (Dispatch~ 0) Get)^
						(UnitId (Dispatch~ 1) Get)^
						(UnitCommandParams (Dispatch~ 2) Get)^
					);
					(NothingToDispatch (Dispatch~ nil) Ident)~ Exe
					(Handle UnitCommandId~)~ Exe
				);
				/* unit's lifestep */
				UnitLifeStep ias (
					(Stop false) ias (
						(emptyQ true) ias (
							(CommandDispatcher.Dispatch nil)^
						);
						(emptyQ false) ias (
							(CommandDispatcher.Dispatch (CommandQueue~) First)^
							(CommandQueue (CommandQueue~) Rest)^
							((The unit Id~ received CommandDispatcher.Dispatch~) Ew.Output~) Do
						);
						(emptyQ (CommandQueue~ ()) Ident)~ Exe
						CommandDispatcher~ Exe
					);
					(Stop true) ias (
						((The unit Id~ stopped) Ew.Output~) Do
					);
					(Stop StopTheWorld~)~ Exe
				);

				lifeterm = (
					((The unit Id~ was born) Ew.Output~) Do
					UnitLifeStep~ Exe
				) Exe
			);
		);
		Player ias (
			AvailableUnitsCommands ias (create destroy movetopos startpos go stop exit);
			Id ias nil;
			CommandQueue ias ();
			HandleUnitsCommand ias (
				UnitCommandId ias nil;
				UnitId ias nil;
				UnitCommandParams ias nil;
				(Handle create) ias (
					(Characters.Unit.Id UnitId~)^
					((The player Id~ creates unit UnitId~) Ew.Output~) Do
					(Characters.Unit Characters.(Unit ($$)->(HandleUnitsCommand)->UnitId~)) Born
					PlayerLifeStep~ Exe
				);
				(Handle movetopos),(Handle startpos),(Handle go),(Handle stop) ias (
					(Characters.(Unit ($$)->(HandleUnitsCommand)->UnitId~)->CommandQueue
					(Characters.(Unit ($$)->(HandleUnitsCommand)->UnitId~)->CommandQueue~
						    (
							(($$)->(HandleUnitsCommand)->UnitCommandId~ ($$)->(HandleUnitsCommand)->UnitId~ ($$)->(HandleUnitsCommand)->UnitCommandParams~))
						    ) Join
					)^
					PlayerLifeStep~ Exe
				);
                                (Handle destroy) ias (
					((The player Id~ executes other actions) Ew.Output~) Do
					PlayerLifeStep~ Exe
				);
				(UnitCommandId (CommandDispatcher.Dispatch~ 0) Get)^
				(UnitId (CommandDispatcher.Dispatch~ 1) Get)^
				(UnitCommandParams (CommandDispatcher.Dispatch~ 2) Get)^
				(Handle UnitCommandId~)~ Exe
			);
			CommandDispatcher ias (
				Dispatch ias nil;
				(RightCommand true) ias (
					HandleUnitsCommand~ Exe
				);
				(RightCommand false) ias (
					PlayerLifeStep~ Exe
				);
				((The player Id~ received command Dispatch~ to dispatch) Ew.Output~) Do
				(RightCommand (AvailableUnitsCommands~ (Dispatch~ 0) Get) In)~ Exe
			);
			/* Player's lifestep */
			PlayerLifeStep ias (
				(Stop false) ias (
					(emptyQ true) ias (
						PlayerLifeStep~ Exe
					);
					(emptyQ false) ias (
						LockQueue [
							(CommandDispatcher.Dispatch (CommandQueue~) First)^
							(CommandQueue (CommandQueue~) Rest)^
						LockQueue ]
						CommandDispatcher~ Exe
					);
					(emptyQ (CommandQueue~ ()) Ident)~ Exe
				);
				(Stop true) ias (
					((The player Id~ stopped) Ew.Output~) Do
				);
				(Stop StopTheWorld~)~ Exe
			);
			lifeterm = (
				((The player Id~ added and active) Ew.Output~) Do
				PlayerLifeStep~ Exe
			) Exe
		);
		Manager ias (
			Command ias nil;
			Character ias nil;
			Id ias nil;
			UnitId ias nil;
			CommandId ias nil;
			Params ias nil;
			/* command is propagated to player */
			PropagateCommand ias (
				LockQueue [
					((Player Id~)->CommandQueue ((Player Id~)->CommandQueue~ ((CommandId~ UnitId~ Params~))) Join)^
				LockQueue ]
				((Command propagated to player Id~) Ew.Output~) Do
			);
			(ManagerCommand true) ias (
				(Command playeradd) ias (
					(Player.Id UnitId~)^
					(Player (Player UnitId~)) Born
				);
				(Command exit) ias (
					(StopTheWorld true)^
				);
				((manager command CommandId~) Ew.Output~) Do
				(Command CommandId~)~ Exe
			);
			(ManagerCommand false) ias (
				((Not manager command propagating) Ew.Output~) Do
				/* passes for processing */
				PropagateCommand~ Exe
			);
			/* main loop where model waits for incoming commands and dispatches them */
			MainLoop ias (
				/* decomposes incoming command */
				ParseIncomingCommand ias (
					(Character (Command~ 0) Get)^
					(Id (Command~ 1) Get)^
					(CommandId (Command~ 2) Get)^
					(UnitId (Command~ 3) Get)^
					(Params (Command~ 4) Get)^
				);
				(Stop false) ias (
					(WaitForCommand true) ias (
						(NoAutoCommand false) ias (
							1000 Relax
							(Command (AutoCommands.Injection~) First)^
							(AutoCommands.Injection (AutoCommands.Injection~) Rest)^
							((The auto command Command~) Ew.Output~) Do
							ParseIncomingCommand~ Exe
							((The auto command Command~ character Character~) Ew.Output~) Do
							(ManagerCommand (Character~ manager) Ident)~ Exe
						);
						(NoAutoCommand true) ias doNothing;
						(NoAutoCommand (AutoCommands.Injection~ ()) Ident)~ Exe
						MainLoop~ Exe
					);
					(WaitForCommand false) ias (
						ParseIncomingCommand~ Exe
						(ManagerCommand (Character~ manager) Ident)~ Exe
						MainLoop~ Exe
					);
					/* waiting for command, in non-blocking manner */ 
					(Command (() Ew.Input~) Do)^
					(WaitForCommand (Command~ nil) Ident)~ Exe
				);
				(Stop true) ias (
					((The manager stopped) Ew.Output~) Do
				);
				(Stop StopTheWorld~)~ Exe
			);	

			source lifeterm = (
				((The initialization time) Ew.Output~) Do
				3000 Relax
				((The game manager started and waits for commands) Ew.Output~) Do
				MainLoop~ Exe
				3000 Relax
			) Exe
		);

		AutoCommands ias (
			Injection ias (
				(manager 0 playeradd 0)
				(player 0 create 0)
				(player 0 startpos 0 (50 40))
				(player 0 movetopos 0 ((50 52)))
				(player 0 go 0)
			);
		);
	);
}
