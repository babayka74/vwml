options {
	language=__java__ {
  		package = "com.win.game.tesej.model.vwml.tesej4"
  		path = "c:\var\vwml/tesej4/src"
  		author = "Groozman M.Z."
		project_name = "tesej4Proj"
  		description = "Tesej4 walk"
		visualizer {
			class = "com.vw.lang.sink.java.link.debug.visitor.dot.VWMLLinkDebugPreprocessorDotVisitor"
			data = "c:\var\vwml/tesej4/diagram"
		}
		beyond {
			fringe communication ias (
				out ias "com.vw.lang.beyond.java.fringe.gate.console.Console"
				in ias "com.vw.lang.beyond.java.fringe.gate.console.Console"
			)
			fringe move ias (
				toPosition ias "com.vw.lang.beyond.java.fringe.gate.movement.Go"
			)
			fringe services ias (
				math ias "com.vw.lang.beyond.java.fringe.gate.math.Math"
			)
		}
		conflictring {
			GameTesej.Tesej.Actions.LogicGenerated.CheckPositionBeforeMovement.Study conflicts (GameTesej.Tesej.Actions.LogicGenerated.CheckPositionBeforeMovement.Study)
		}
	}
}

module tesej4 {
	GameTesej ias (
		Ew ias (
			say ias communication.out.write;
			ask ias communication.in.read;
			inc ias services.math.inc;
			movedToPosition ias move.toPosition.go;
		);
		Map ias (
			/* a ias (TesejTemple TesejTemple TesejTemple); */

			a ias (TesejTemple a h TesejTemple b g TesejTemple f h e c d TesejTemple);
			b ias (b a e e g f f f);
			c ias (a c e d f);
			d ias (d c g h g g h h g);
			e ias (e c a b g);
			f ias (f c b a b g);
			g ias (g b e h e c b g b);
			h ias (h a e c e e a);
		);
		EntityRestingTime ias (
			Time ias (
				v ias 1000;
			);
			((relaxing Time.v~ miliseconds) Ew.say~) Do
			Time.v~ Relax
		);
		/* Global counter of Tesejs */
		TesejIds ias 0;
		SpecialPositions ias (Tesej Minotaur TesejTemple MinotaurTemple);
		Tesej ias (
			/* Initial Tesej's id */
			Id ias 0;
			/* Tesej's initial position */
			Position ias Map.a;
			/* Temporary entity which is used in order to store some entity during phase of taking decision about Tesej's movement */
			K ias Map.a;
			/* Tesej selected position but not approved, so need to check whether another entities has occupied it or no */
			TempPosition ias Position;
			/* Tesej's previous position; needed in order to report Tesej's movement */
			PrevPosition ias Position;
			/* Actions which can be performed by Tesej */
			Actions ias (
				LogicGenerated ias (
					/* tesej's action */
					Action ias nil;
					/* tesej selects new position, but not moved */
					SelectNewPosition ias (
						/* Restores position's interpretetion */
						(Position~ K~)^
						(TempPosition Position~~ Random)^
						((Tesej Id~ selects position) Ew.say~) Do
						/* checking position before taking decision */
						(Action CheckPositionBeforeMovement)^
						LifeStep~ Exe
					);
					/* makes decision  */				
					CheckPositionBeforeMovement ias (
						Study [
							(PositionTaken true) ias doNothing;
							(PositionTaken false) ias (
								/* store former position in 'PrevPosition' */
								(PrevPosition Position~)^
								/* Position is interpreted as new position (interpretation of TempPosition) generated during SelectNewPosition */
								(Position TempPosition~)^
								/* on each movenet current position is marked as Tesej's position; so interpretation of current position
						   		should be stored in separate entity */
								(K Position~~)^
								/* Marks current position as taken over by Tesej */
								(Position~ Tesej)^
							);
							((Tesej Id~ analyzes position) Ew.say~) Do
							/* Checks if position was taken before */
							(WasPositionTaken (TempPosition~~ SpecialPositions~)In)^
							/* Actual checking... */
							(PositionTaken WasPositionTaken~)~ Exe
							/* run actual actions */
							(Action LogicGenerated.(PositionUsed WasPositionTaken~))^
							((Tesej Id~ analyzed and took action Action~) Ew.say~) Do
						Study ]
						((Tesej Id~ action Action~ before life step) Ew.say~) Do
						LifeStep~ Exe
					);
					/* Position is free and Tesej moves to it */
					/* some of comments below reflet steps for first movements only (as example) */
					MoveToPosition ias (
						((Tesej Id~ moved from PrevPosition~ to Position~) Ew.say~) Do	
						/* some delay before moveing to new position */
						EntityRestingTime~ Exe
						/* commands to move to position */
						(Action SelectNewPosition)^ 
						LifeStep~ Exe
					);
					/* Tesej is cloned */
					ToClone ias (
						((Tesej Id~ is going to be cloned) Ew.say~) Do
						(TesejIds ((TesejIds~) Ew.inc~) Do)^
						(Tesej (Tesej TesejIds~)) Clone
						(Action SelectNewPosition)^
						((Tesej Id~ was cloned by temple and action is Action~) Ew.say~) Do 
						LifeStep~ Exe
					);
					/* finds who occupied position and runs tesej's action depending on it */
					(PositionUsed true) ias (
						/* some Tesej on position - selects new */
						(PositionUsed Tesej) ias (
							(Action SelectNewPosition)^
							LifeStep~ Exe
						);
						/* Minotaur is here */
                                              	(PositionUsed Minotaur) ias (
						);
						/* Tesej's temple; here tesej can be cloned */
                                              	(PositionUsed TesejTemple) ias (
							(Action ToClone)^
							LifeStep~ Exe
						);
						/* Minotaur's temple; here minotaur can be cloned */
                                              	(PositionUsed MinotaurTemple) ias (
						);

						(PositionUsed TempPosition~~)~ Exe
					);
					/* position is free, here are no special actions needed */
					(PositionUsed false) ias (
						(Action MoveToPosition)^
						LifeStep~ Exe						
					);
				);
			);
			/* Tesej's life step; used when logic generated action should be processed */
			LifeStep ias (
				Actions.LogicGenerated.Action~~ Exe
			);
			/* Its life term; initial action; used when Tesej moves from old position to new one */
			Life ias (
				(Actions.LogicGenerated.Action Actions.LogicGenerated.SelectNewPosition)^ 
				Actions.LogicGenerated.Action~~ Exe
			);
			/* Its source lifeterm */
   			source lifeterm = (
						(K K~~)^
						(Id TesejIds~)^
						Life~ Exe
					) Exe
		);
	);
}
