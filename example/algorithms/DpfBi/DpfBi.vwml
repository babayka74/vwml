module DpfBi {
	/* Depth-first search algorithm. Modification B. inversed */
	DpfSearchMkBInversed ias (
		/* aux. entities; cached by caller */
		Maze ias $0;
		MovementSpeed ias $1;
		Goals ias $2;
		State ias $3;
		ChoiceSet ias $4;
		StateSet ias $5;
		SeqStates ias $6;
		VectorSummationFringe ias $7;
		CompletitionCallback ias $8;
		StepMode ias $9;

		NextStates ias ();
		NewState ias nil;
		/* looks for possible states by observing area; used by GetNextStates */
		LookupNextStates ias (
			Svect ias nil;
			(InMaze true) ias doNothing;
			(InMaze false) ias (NextStates (NextStates~ (Svect~)) Join)^;
			delta ias $;
			(Svect ((delta~~ State~~) VectorSummationFringe~~) Do)^
			(InMaze (Maze~~ Svect~) In)~ Exe
		);
		/* actually looks for next states  */
		GetNextStates ias (
			(NextStates ())^
			(MovementSpeed~~ LookupNextStates~) ForEach
		);
		/* acually sets new state */
		GetNewState ias (NewState ChoiceSet~~ Random)^;
		Search ias (EndSearch (Goals~~ State~~) In)~ Exe;
		/* one of goal's targets has achieved */
		(EndSearch true) ias CompletitionCallback~~ Exe;
		/* continue walking... */
		(EndSearch false) ias (
			(stepMode false) ias Search~ Exe;
			(stepMode true) ias doNothing;
			GetNextStates~ Exe
			(ChoiceSet~ (NextStates~ StateSet~~) Substruct)^
			(Return (ChoiceSet~~ ()) Ident)~ Exe
			(stepMode StepMode~ )~ Exe
		);
		/* pop state from stack and set last state to current state */
		(Return true) ias (
			(stepMode true) ias Search~ Exe;
			(stepMode false) ias doNothing;
			(State~ (SeqStates~~) Last)^
			(SeqStates~ (SeqStates~~) Begin)^
			(stepMode StepMode~ )~ Exe
		);
		(Return false) ias (
			GetNewState~ Exe
			(State~ NewState~)^
			(SeqStates~ (SeqStates~~ (State~~)) Join)^
			(StateSet~ (StateSet~~ (State~~)) Join)^
		);

		Search~ Exe
	);
}
