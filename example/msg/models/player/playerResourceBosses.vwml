module PlayerResourceBosses {
	MagicWorld ias (
		Player ias (
			Resources ias (
				/* Bosses of district */
				Bosses ias (
					BossesList ias ();
					// allows to add bosses in run-time
					AddBoss ias (
						// initialization phase
						InitAs ias oninit;
						// boss's id
						BId ias nil;
						// quartals controlled per boss
						Quartals ias 0;
						// quartal numeric id starts from
						QNIds ias 0;
						// bosses bribes range
						BBR ias ();
						// predefined quartals configuration
						PQConf ias nil;

						(BossOfDistrict (BossOfDistrict BId~) defer) Born
						((BossOfDistrict BId~)->Owner Id~)^
						((BossOfDistrict BId~)->Quartals Quartals~)^
						((BossOfDistrict BId~)->QNIds QNIds~)^
						((BossOfDistrict BId~)->BBR BBR~)^
						((BossOfDistrict BId~)->InitAs InitAs~)^
						((BossOfDistrict BId~)->Id BId~)^
						((BossOfDistrict BId~)->PQConf PQConf~)^
#if (verbose)
						((Creating boss (BossOfDistrict BId~)) Ew.Output~) Do
#endif
						(BossOfDistrict BId~)->Init~ Exe
						(BossesList (BossesList~ (BId~)) Join)^
					);
					BossUnInitialized ias (
						RemoveBoss ias (
							(BossesList (BossesList~ (B~)) Substruct)^
#if (verbose)
							((Uninitialized boss (BossOfDistrict B~) removed and remained bosses are BossesList~) Ew.Output~) Do
#endif
						);
						BLR ias (
							(BossesList ()) FinishInterception
#if (verbose)
							((All bosses uninitialized) Ew.Output~) Do
#endif
							((Player Id~) Tx (ModeInfo.ModeMain~ partclosed Id~ (bosses))) Gate
						);
						B ias nil;
						RemoveBoss~ Exe
					);
					PostBossesCommand ias (
						Cmd ias exit;
						BS ias (
#if (verbose)
							((Cmd~ to boss (BossOfDistrict $~)) Ew.Output~) Do
#endif
							((Player Id~) Tx (ModeInfo.ModeBossOfDistrict~ Cmd~ $~ ())) Gate
						);
						(BossesList~ BS~) ForEach
					);
					// initiate finish command
					IFCmd ias (
						Cmd ias nil;
						(NI true) ias BossUnInitialized.BLR~ Exe;
						(NI false) ias (
							(BossesList () BossUnInitialized.BLR~) StartInterception
							(PostBossesCommand.Cmd Cmd~)^
							PostBossesCommand~ Exe
						);
						(NI (BossesList~ ()) Ident)~ Exe
					);
					Done ias (
						(IFCmd.Cmd exit)^
						IFCmd~ Exe
					);
					Reset ias (
						(IFCmd.Cmd reset)^
						IFCmd~ Exe
					);
					// adds initial bosses' set
					AddInitialSet ias (
						InitBossesNum ias 0;
						BossesInitFinished ias (
							(InitBossesNum 0) FinishInterception
#if (verbose)
							((Player Id~ initialized all bosses) Ew.Output~) Do							
#endif
							(InitSteps (InitSteps~ (bosses)) Substruct)^
						);
						BossInitialized ias (
							(InitBossesNum ((InitBossesNum~) Ew.DecValue~) Do)^
#if (verbose)
							((Player Id~ InitBossesNum~ left bosses) Ew.Output~) Do							
#endif
						);
						// creating and initializing bosses
						CIBL ias (
							(BId (Id~ $~))^
							(AddBoss.BId BId~)^
						        (AddBoss.QNIds (($~ Ew.BossOfDistrictConf.ControlledQuartals~) Ew.Mul~) Do)^
							(AddBoss.QNIds ((AddBoss.QNIds~ Ew.BossOfDistrictConf.GlobalStartQNum~) Ew.Sum~) Do)^
							(AddBoss.Quartals Ew.BossOfDistrictConf.ControlledQuartals~)^
							(AddBoss.InitAs oninit)^
							(AddBoss.BBR Ew.BossOfDistrictConf.BossOfDBribeRanges~)^
							AddBoss~ Exe
						);
						(NIBS true) ias BossesInitFinished~ Exe;
						(NIBS false) ias (
#if (verbose)
							((Creating InitBossesNum~ bosses) Ew.Output~) Do
#endif
							(InitBossesNum 0 BossesInitFinished~) StartInterception
							(InitBossesNum~ CIBL~) Repeat
						);
						(InitBossesNum Ew.PlayerAccountConf.BossesOfDistrictPerPlayer~)^
						(NIBS (InitBossesNum~ 0) Ident)~ Exe
					);
				);
			);
		);
	);
}
