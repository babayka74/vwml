module PlayerResources {
	MagicWorld ias (
		Player ias (
			Resources ias (
				ActiveQuartals ias 0;
				InitSteps ias ();
				InitDone ias (
					(InitSteps () InitDone~) FinishInterception
#if (verbose)
					((Player Id~ initialized) Ew.Output~) Do
#endif
#if (release)                   
                              		(((playermanagment playeradd ok new) (Player Id~)) Ew.OutChannel~) Do
#endif

				);
				/* available units */
				Units ias (
					Hired ias ();
					/* shared entity which stores units' current state (position) */
					UnitsStateSet ias ();
					UnitsStateSetLocker ias nil;
					Init ias (
						(Hired ())^
#if (verbose)
						((Player Id~ initialized hired units storage) Ew.Output~) Do
#endif
					);
				);
				/* cars' container */
				Garage ias (
					// max number of cars which can be placed here
					Capacity ias 0;
					Free ias 0;
					Cars ias ();
					Init ias (
						(Cars ())^
						(Update.Conf (Garage Ew.GarageConf.Garage~))^
						Update~ Exe
						((Player Id~ initialized garage) Ew.Output~) Do
					);
					Reserve ias (
						Place ias 0;
						Result ias false;
						RP ias 0;
						// reserving
						(Allow 1) ias (
							(Result true)^
							(Free ((Free~ Place~) Ew.Sub~) Do)^
#if (verbose)
							((Garage of Player Id~ reserved Place~ and free places are Free~) Ew.Output~) Do
#endif
						);
						// no places
						(Allow 0),(Allow -1) ias (
#if (verbose)
							((Garage of Player Id~ does not have free places) Ew.Output~) Do
#endif
						);
						(Result false)^
						(RP ((Free~ Place~) Ew.Sub~) Do)^
						(Allow ((RP~ 0) Ew.Compare~) Do)~ Exe
					);
					CancelReserve ias (
						Place ias 0;
						RP ias 0;
						(Allow -1),(Allow 0) ias (
							(Free ((Free~ Place~) Ew.Sum~) Do)^
#if (verbose)
							((Garage of Player Id~ canceled Place~ and free places are Free~) Ew.Output~) Do
#endif
						);
						(Allow 1) ias (
#if (verbose)
							((Garage of Player Id~ exceeds number of free places Free~ and capacity Capacity~) Ew.Output~) Do
#endif
						);
						(RP ((Free~ Place~) Ew.Sum~) Do)^
						(Allow ((RP~ Capacity~) Ew.Compare~) Do)~ Exe
					);
					Update ias (
						Conf ias nil;
						(NotReady true) ias doNothing;
						(NotReady false) ias (
							F ias nil;
							SetProps ias (
								P ias nil;
								(P capacity) ias (
									// general garage's capacity
									(Capacity ($~ 1) Get)^
									(Free Capacity~)^
#if (verbose)                           	
									((Garage capacity of player Id~ initialized to Capacity~) Ew.Output~) Do
#endif                                  	
								);
								(P cars) ias (
									// cars types and classes
									CTC ias nil;
									SetCTC ias (
										CarType ias nil;
										CarClasses ias nil;
										SetClasses ias (
											// car's class
											Clazz ias nil;
											// occupied places in garage
											(Clazz ($~ 0) Get)^
											// quantity of cars of given class
											((Q CarType~ Clazz~) 0)^
											((Q CarType~ Clazz~) Garage true) Context
											// number of places occupied by car in garage
											((P CarType~ Clazz~) ($~ 1) Get)^
											((P CarType~ Clazz~) Garage true) Context
#if (verbose)                           	
											((Inside garage quantity of (Q CarType~ Clazz~) (Q CarType~ Clazz~)~ and places (P CarType~ Clazz~) (P CarType~ Clazz~)~) Ew.Output~) Do
#endif                                  	
										);
										// getting car type
										(CarType ($~ 0) Get)^
										(CarClasses ($~ 1) Get)^
										// iterate over classes
										(CarClasses~ SetClasses~) ForEach
									);
									(CTC ($~ 1) Get)^
									// iterate over all types and create necessary data
									(CTC~ SetCTC~) ForEach
								);
								(P ($~ 0) Get)~ Exe
							);
							(F (Conf~ 1) Get)^
							(F~ SetProps~) ForEach
						);
						(NotReady (Conf~ nil) Ident)~ Exe
					);
				);
				/* controlled/own/visible quartals */
				Quartals ias (
					/* player is owner, at least, of one quartal */
					Own ias ();
					/* set of quartals controlled by player */
					Controlled ias ();
					/* player knows about these quartals but doesn't have control upon them */
					Visible ias ();
					Init ias (
						// This entity is monitored by interceptor
						ExpectedQuartals ias 0;
						NotifyAboutAllInitializedQuartals ias (
							(ExpectedQuartals 0 NotifyAboutActivitiesStopped~) FinishInterception
							(InitSteps (InitSteps~ (quartals)) Substruct)^
#if (verbose)
							((Player Id~ left init steps InitSteps~) Ew.Output~) Do							
#endif
						);
						ActivateQuartal ias (
							(QReady false) ias (
								(Quartal (Quartal (Id~ $~)) defer) Born
#if (verbose)
								((Player Id~ created quartal (Quartal (Id~ $~)) by request) Ew.Output~) Do
#endif
								(QReady true)~ Exe
							);
							// quartal's activation
							(QReady true) ias (
								((Quartal (Id~ $~))->Id (Id~ $~))^
								((Quartal (Id~ $~))->Owner Id~)^
								((Quartal (Id~ $~))) Activate
							);
							// checks if quartal has already been created
							(QReady (Quartal (Id~ $~)) EI)~ Exe
						);
						(ExpectedQuartals Ew.PlayerAccountConf.QuartalsPerPlayer~)^
						// waits untill all quartals have been initialized
						(ExpectedQuartals 0 NotifyAboutAllInitializedQuartals~) StartInterception
						(Ew.PlayerAccountConf.QuartalsPerPlayer~ ActivateQuartal~) Repeat
						// create battlefield and associate it with quartal
						((Player Id~) Tx (ModeInfo.ModeBattle~ createbf Id~ ((Id~ 0)))) Gate
						(Controlled ())^
						(Visible ())^
						(Own ())^
						LInit~ Exe
					);
					LInit ias (
						(ActiveQuartals Ew.PlayerAccountConf.QuartalsPerPlayer~)^
#if (verbose)
						((Player Id~ created and initialized own quartals) Ew.Output~) Do
#endif
					);
					Deactivate ias (
						A ias nil;
						AllOwnQurtalsDeactivated ias (
							(ActiveQuartals 0 AllOwnQurtalsDeactivated~) FinishInterception
							// send message to player about closing all own quartals
							((Player Id~) Tx (ModeInfo.ModeMain~ partclosed Id~ (quartals))) Gate
						);
						(NoQuartals true) ias ((Player Id~) Tx (ModeInfo.ModeMain~ partclosed Id~ (quartals))) Gate;
						(NoQuartals false) ias (
							ResetQuartal ias ((Player Id~) Tx (ModeInfo.ModeDeal~ A~ Id~ ($~))) Gate;
							(ActiveQuartals 0 AllOwnQurtalsDeactivated~) StartInterception
							(Own~ ResetQuartal~) ForEach
						);
						(NoQuartals (Own~ ()) Ident)~ Exe
					);
					RestoreActiveQ ias (
						(RQ false) ias doNothing;
						(RQ true) ias (ActiveQuartals Ew.PlayerAccountConf.QuartalsPerPlayer~)^;
						(RQ (ActiveQuartals~ 0) Ident)~ Exe
					);
					Done ias (
						RestoreActiveQ~ Exe
						(Deactivate.A releasequartal)^
						Deactivate~ Exe
					);
					Reset ias (
						RestoreActiveQ~ Exe
						(Deactivate.A resetquartal)^
						Deactivate~ Exe
					);
				);
				BattleFieldDone ias (
					(NotSelectedBF true) ias ((Player Id~) Tx (ModeInfo.ModeMain~ partclosed Id~ (battlefield))) Gate;
					(NotSelectedBF false) ias (
						((Player Id~) Tx (ModeInfo.ModeBattle~ destroybf Id~ ((Id~ 0)))) Gate
					);
					(NotSelectedBF (Resources.SelectedBattle~ nil) Ident)~ Exe 
				);
				BattleFieldReset ias (
					(NotSelectedBF true) ias ((Player Id~) Tx (ModeInfo.ModeMain~ partclosed Id~ (battlefield))) Gate;
					(NotSelectedBF false) ias (
						((Player Id~) Tx (ModeInfo.ModeBattle~ resetbf Id~ ((Id~ 0)))) Gate
					);
					(NotSelectedBF (Resources.SelectedBattle~ nil) Ident)~ Exe 
				);
				ResourceManagerDone ias ((Player Id~) Tx (ModeInfo.ModeResourceMgr~ exit Id~ ())) Gate;
				/* set of player's battles */
				Battles ias ();
				/* Selected battle */
				SelectedBattle ias nil;
				/* Business development factor */
				Business ias (
					RQ ias 0; // quantum
#if (business)
					InitialGDevelop ias 0;
					InitialGContain ias 0;
					GDevelop ias 0;		// see StrategyGame_Hiring&Dismiss
					GContain ias 0; 	// see StrategyGame_Hiring&Dismiss (mentioned as hospitality)
					LH ias 0; 		// see Battle result document
#endif
					Value ias nil;
					Result ias false;
					Init ias (
						/* by resource type */
#if (business)
						(GDevelop 0)^
						(GContain 0)^
						(Update.From ((Factors Ew.BusinessConf.Factors~) (GDevelop Ew.BusinessConf.GDevelop~) (GContain Ew.BusinessConf.GContain~) (UnitsInfo Ew.BusinessConf.UnitsInfo~) (LH Ew.BusinessConf.LH~)))^
#else
						(Update.From (Factors Ew.BusinessConf.Factors~))^
#endif
						Update~ Exe
#if (verbose)
						((Player Id~ initialized business factor resources) Ew.Output~) Do
#endif
					);
					
#if (business)
					AssembleGDevelop ias (
						AssembleByRt ias (GDevelop ((GDevelop~ (Gdf $~)~) Ew.Sum~) Do)^;
						(GDevelop 0)^
						(Ew.BusinessConf.AvailableResources~ AssembleByRt~) ForEach
						(GDevelop ((GDevelop~ InitialGDevelop~) Ew.Sum~) Do)^
#if (verbose)                           	
						((Player Id~ has assembled gdevelop factor GDevelop~) Ew.Output~) Do 
#endif
					);

					AssembleGContain ias (
						AssembleByRt ias (GContain ((GContain~ (Ctf $~)~) Ew.Sum~) Do)^;
						(GContain 0)^
						(Ew.BusinessConf.AvailableResources~ AssembleByRt~) ForEach
						(GContain ((GContain~ InitialGContain~) Ew.Sum~) Do)^
#if (verbose)                           	
						((Player Id~ has assembled gcontain factor GContain~) Ew.Output~) Do 
#endif
					);
#endif // business
					Update ias (
						// ((<factors>)(<gdevelop>))
						From ias nil;
						UpdateFactors ias (
							// factors' indexes inside Factors entity
							FType ias 0;
							// Business development
							BDFactor ias 1;
							KFactor ias 2;
							// Containment (aka Hospitality)
							CFactor ias 3;
							// GDevelop 
							GFactor ias 4;
							// cashier
							CEFactor ias 5;
							CCFactor ias 6;
							// example: ((vodka 0 1) (food 0 1) (gold 0 1) (viski 0 1))
							F ias nil;
							(Ignore true) ias doNothing;
							(Ignore false) ias (
								AssociateByRt ias (
									(Type ($~ FType~) Get)^
									// business dev factor
									((Bdf Type~) ($~ BDFactor~) Get)^
									((Bdf Type~) Business true) Context
									// k-factor
									((Kdf Type~) ($~ KFactor~) Get)^
									((Kdf Type~) Business true) Context
#if (business)
									// initial value of containment (aka hospitality per resource)
									((Ctf Type~) ($~ CFactor~) Get)^
									((Ctf Type~) Business true) Context
									// initial value of gangster development (per resource)
									((Gdf Type~) ($~ GFactor~) Get)^
									((Gdf Type~) Business true) Context
									// cashier (C & E) factors
									((CEf Type~) ($~ CEFactor~) Get)^
									((CEf Type~) Business true) Context
									((CCf Type~) ($~ CCFactor~) Get)^
									((CCf Type~) Business true) Context
#endif
#if (verbose)                                           	
									((Player Id~ has factors for (Bdf Type~) (Bdf Type~)~ and (Kdf Type~) (Kdf Type~)~ and (Ctf Type~) (Ctf Type~)~ and (Gdf Type~) (Gdf Type~)~ and (CEf Type~) (CEf Type~)~ and (CCf Type~) (CCf Type~)~) Ew.Output~) Do 
#endif
								);
								(F (F~ 1) Get)^
								(F~ AssociateByRt~) ForEach
							);
							(Ignore (F~ nil) Ident)~ Exe
						);
#if (business)
						UpdateGDevelop ias (
							// (<number>)
							F ias nil;
							(Ignore true) ias doNothing;
							(Ignore false) ias (InitialGDevelop (F~ 1) Get)^;
							(Ignore (F~ nil) Ident)~ Exe
#if (verbose)                           	
							((Player Id~ has initial gdevelop factor InitialGDevelop~) Ew.Output~) Do 
#endif
						);

						UpdateGContain ias (
							// (<number>)
							F ias nil;
							(Ignore true) ias doNothing;
							(Ignore false) ias (InitialGContain (F~ 1) Get)^;
							(Ignore (F~ nil) Ident)~ Exe
#if (verbose)                           	
							((Player Id~ has gcontain factor InitialGContain~) Ew.Output~) Do 
#endif
						);

						UpdateLH ias (
							F ias nil;
							// (<number>)
							F ias nil;
							(Ignore true) ias doNothing;
							(Ignore false) ias (LH (F~ 1) Get)^;
							(Ignore (F~ nil) Ident)~ Exe
#if (verbose)                           	
							((Player Id~ has LH factor LH~) Ew.Output~) Do 
#endif
						);

						UpdateUnitsInfo ias (
							F ias nil;
							K ias nil;
							P ias nil;
							PP ias nil;
							(Ignore false) ias (
								ParseInfo ias (
									// parses property
									(PP clazz) ias (
										// setups Q counter (Q kind class); see TakeOnBoardUnit
										SQC ias (
											// quantity
											((Q K~ ($~ 0) Get) ($~ 1) Get)^
											// make it addressable -> Resources.Business
											((Q K~ ($~ 0) Get) Business true) Context
#if (verbose)                                           	
											((Player Id~ initialized (Q K~ ($~ 0) Get) to (Q K~ ($~ 0) Get)~) Ew.Output~) Do
#endif                                                  	
										);
										// available classes
										((P~ 1) Get SQC~) ForEach
									);
									// kind of unit
									(K ($~ 0) Get)^
									// properties
									(P ($~ 1) Get)^
									(PP (P~ 0) Get)~ Exe
								);
								(F (F~ 1) Get)^
								(F~ ParseInfo~) ForEach
							);
							(Ignore true) ias doNothing;
							(Ignore (F~ nil) Ident)~ Exe
						);
						(UpdateGDevelop.F (From~ 1) Get)^
						UpdateGDevelop~ Exe
						(UpdateGContain.F (From~ 2) Get)^
						UpdateGContain~ Exe
						(UpdateUnitsInfo.F (From~ 3) Get)^
						UpdateUnitsInfo~ Exe
						(UpdateLH.F (From~ 4) Get)^
						UpdateLH~ Exe
#endif // business
						(UpdateFactors.F  (From~ 0) Get)^
						UpdateFactors~ Exe
					);
					Report ias (
#if (release)
						Fs ias ();
#endif
						Report ias (
							(T ($~ 0) Get)^
#if (verbose)
							((Player Id~ has business conf for resource (Bdf T~) (Bdf T~)~ and (Kdf T~) (Kdf T~)~) Ew.Output~) Do
#endif
#if (release)
							(Fs (Fs~ ((T~ (Bdf T~)~ (Kdf T~)~))) Join)^
#endif
						);
						(Ew.BusinessConf.Factors~ Report~) ForEach
#if (verbose)
						((Player Id~ compiled business factors Fs~) Ew.Output~) Do
#endif
#if (release)
						(((business factor done) (player Id~) Fs~) Ew.OutChannel~) Do
#endif
					);
					IncreaseBF ias (
						(ByBdfType (Bdf (Value~ 1) Get))^
						(ByBdfType~ ((ByBdfType~~ (Value~ 2) Get) Ew.SumF~) Do)^
						(Result true)^
#if (verbose)
						((Player Id~ increased business dev factor ByBdfType~ on (Value~ 2) Get balance is ByBdfType~~) Ew.Output~) Do
#endif
#if (release)
						(((business factor done)(player Id~)((ByBdfType~ 1) Get (Value~ 2) Get ByBdfType~~)) Ew.OutChannel~) Do
#endif
					);
					DecreaseBF ias (
						(Allow -1) ias (
							(Result false)^
#if (verbose)
							((Player Id~ does not have enougth resources ByBdfType~~ on ByBdfType~ required (Value~ 2) Get) Ew.Output~) Do
#endif
						);
						(Allow 0),(Allow 1) ias (
							(Result true)^
							(ByBdfType~ ((ByBdfType~~ (Value~ 2) Get) Ew.SubF~) Do)^
#if (verbose)
							((Player Id~ decreased business dev factor (Value~ 2) Get balance is ByBdfType~~ for ByBdfType~) Ew.Output~) Do
#endif
#if (release)
							(((business factor done)(player Id~)((ByBdfType~ 1) Get (Value~ 2) Get ByBdfType~~)) Ew.OutChannel~) Do
#endif
						);
						(ByBdfType (Bdf (Value~ 1) Get))^
						(Allow ((ByBdfType~~ (Value~ 2) Get) Ew.CompareF~) Do)~ Exe
					);
					RecalcRQuantum ias (
						// (Id~ Resource.Type~ Resource.Quantum~ LInvestQuantum~)
						(RQ (( (Value~ 2) Get (Value~ 3) Get (Kdf (Value~ 1) Get)~) Ew.RecalcQuantum~) Do)^
#if (verbose)
						((Player Id~ new contribution quantum for (Value~ 1) Get is RQ~) Ew.Output~) Do
#endif
					);
				);

				/* Account's information */
				Account ias (
					ByRtType ias nil;
					Value ias nil;
					Result ias false;
					Init ias (
						/* by resource type */
						(Update.From Ew.PlayerAccountConf.ResourceTypes~)^
						Update~ Exe
#if (verbose)
						((Player Id~ initialized account resources) Ew.Output~) Do
#endif
					);
					Update ias (
						From ias nil;
						AssociateByRt ias (
							(Type ($~ 0) Get)^
							((Rt Type~) ($~ 1) Get)^
							((Rt Type~) Account true) Context
#if (verbose)
							((Player Id~ has resource (Rt Type~) (Rt Type~)~) Ew.Output~) Do 
#endif
						);
						(From~ AssociateByRt~) ForEach
					);
					Report ias (
						AccountReport ias (
#if (verbose)
							((Player Id~ has resource (($~ 0) Get (Rt ($~ 0) Get)~)) Ew.Output~) Do
#endif
#if (release)
							(((banking balance done)(player Id~)(($~ 0) Get (Rt ($~ 0) Get)~)) Ew.OutChannel~) Do
#endif
						);
						(Ew.PlayerAccountConf.ResourceTypes~ AccountReport~) ForEach
					);
					Recharge ias (
						(ByRtType (Rt (Value~ 1) Get))^
						(ByRtType~ ((ByRtType~~ (Value~ 2) Get) Ew.SumF~) Do)^
						(Result true)^
#if (verbose)
						((Player Id~ recharged account ByRtType~ on (Value~ 2) Get balance is ByRtType~~) Ew.Output~) Do
#endif
#if (release)
						(((banking recharge done)(player Id~)((ByRtType~ 1) Get (Value~ 2) Get ByRtType~~)) Ew.OutChannel~) Do
#endif
					);
					Withdraw ias (
						(Allow -1) ias (
							(Result false)^
#if (verbose)
							((Player Id~ does not have enougth resources ByRtType~~ on account ByRtType~ required (Value~ 2) Get) Ew.Output~) Do
#endif
						);
						(Allow 0),(Allow 1) ias (
							(Result true)^
							(ByRtType~ ((ByRtType~~ (Value~ 2) Get) Ew.SubF~) Do)^
#if (verbose)
							((Player Id~ withdrawed (Value~ 2) Get balance is ByRtType~~ for account ByRtType~) Ew.Output~) Do
#endif
#if (release)
							(((banking withdraw done)(player Id~)((ByRtType~ 1) Get (Value~ 2) Get ByRtType~~)) Ew.OutChannel~) Do
#endif
						);
						(ByRtType (Rt (Value~ 1) Get))^
						(Allow ((ByRtType~~ (Value~ 2) Get) Ew.CompareF~) Do)~ Exe
					);
				);
				Hiring ias (
					Init ias (
						((HiringAgency Id~)->Id Id~)^
						((HiringAgency Id~)->Owner Id~)^
						(HiringAgency Id~)->Init~ Exe
						((HiringAgency Id~)) Activate
					);
					Done ias (
						(HiringBroker.CommandId exit)^
						(HiringBroker.RequestId nil)^
						(HiringBroker.CommandParams ())^
						HiringBroker~ Exe
					);
					Reset ias (
						(HiringBroker.CommandId reset)^
						(HiringBroker.RequestId nil)^
						(HiringBroker.CommandParams ())^
						HiringBroker~ Exe
					);
				);
				Attacking ias (
					Init ias (
						((AttackManager Id~)->Id Id~)^
						((AttackManager Id~)->PlayerId Id~)^
						(AttackManager Id~)->Init~ Exe
					);
				);
				PreInit ias (
					ResourceManager.Init~ Exe
					Units.Init~ Exe
					Garage.Init~ Exe
					Account.Init~ Exe
					Business.Init~ Exe
					Attacking.Init~ Exe
#if (business)
					(Cashier.Setup.Conf Ew.BusinessConf.Cashier~)^
					Cashier.Start~ Exe
#endif
				);
				Init ias (
					(InitSteps (hiringagency quartals))^
					(InitSteps () InitDone~) StartInterception
					PreInit~ Exe
					Quartals.Init~ Exe
					Hiring.Init~ Exe
				);
				// light init, after reset
				LInit ias (
					PreInit~ Exe
					Quartals.LInit~ Exe
				);
				Done ias (
					BattleFieldDone~ Exe
#if (business)
					Cashier.Stop~ Exe
#endif
				);
				Reset ias (
					BattleFieldReset~ Exe
#if (business)
					Cashier.Stop~ Exe
#endif
				);
			);
		);
	);
}
