module HiringAgency {
	MagicWorld ias (
		HiringAgency ias (
			Id ias nil;
			Owner ias nil;
			HiringProcessTime ias nil;
			StopAgency ias false;
			GlobalHiringRequestId ias 0;

			// command format
			CmdFormat ias (
				// information about unit's hiring properties
				RqHiringInfoIndex ias 0;
				// hiring property - cost
				RqCostPropIndex ias 0;
				// hiring property - hiring time
				RqHTimePropIndex ias 1;
				// hiring property - character id
				RqIdPropIndex ias 2;
				// information about hiring unit
				RqCharacterInfoIndex ias 1;
			);

			Init ias (
				(StopAgency false)^
				((The hiring agency for player Owner~ initialized) Ew.Output~) Do
			);
			Done ias (
				((Player Id~) Tx (ModeInfo.ModeMain~ partclosed Id~ (hiringagency))) Gate
				((The hiring agency for player Owner~ uninitialized) Ew.Output~) Do
			);
			Agent ias (
				Commands ias (starthiring cancelhiring paymentresult check exit);
				Command ias nil;
				Request ias nil;
				RequestId ias 0;
				RequestsInProgress ias ();
				NewRequestId ias nil;

				StopAgencyProcess ias (
					UnblockAndStop ias (
						(StopAgency true)^
						((HiringAgency Id~) Tx (check 0 ())) Gate
					);
					(NoRequestsInProgress true) ias UnblockAndStop~ Exe;
					(NoRequestsInProgress false) ias (
						StopHiringRequest ias (
							(CancelHiringRequest.RqId $~)^
							CancelHiringRequest~ Exe
						);
						(RequestsInProgress~ StopHiringRequest~) ForEach
						(RequestsInProgress ())^
						UnblockAndStop~ Exe
					);
					(NoRequestsInProgress (RequestsInProgress~ ()) Ident)~ Exe
				);
				// starts hiring process when user's payment operation approved
				ActuallyStartHiringRequest ias (
					// confirmed primary request
					(ConfRq (Request~ 1) Get)^
					// getting hiring info (time to hire unit)
					(TimeToHireInfo ((ConfRq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqHTimePropIndex~) Get)^
					(GlobalHiringRequestId ((GlobalHiringRequestId~) Ew.IncValue~) Do)^
					// generate new request id
					(NewRequestId (Id~ GlobalHiringRequestId~))^
					// save request for processing time (need in case if cancel operation is received)
					(NewRequestId~ ConfRq~)^
					(RequestsInProgress (RequestsInProgress~ (NewRequestId~)) Join)^
					// (HiringInfo~ 1) Get => time needed to hire specified unit
					((TimeToHireInfo~ 1) Get (Hiring Id~ NewRequestId~) ProcessRequest~ nil (NewRequestId~ ConfRq~)) Recall
					// player may use GlobalHiringRequestId~ in order to cancel hiring operation
					((The hiring agent Id~ started hiring request NewRequestId~ for player Owner~ ready in (TimeToHireInfo~ 1) Get ms) Ew.Output~) Do
				);
				CancelHiringRequest ias (
					RqId ias nil;
					(-1 (Hiring Id~ RqId~) nil) Recall
					((The hiring agent cancelled request RqId~ for player Owner~) Ew.Output~) Do
					// player's account must be recharged
					(CostInfo ((RqId~~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqCostPropIndex~) Get)^
					((Player Owner~) Tx (ModeInfo.ModeDeal~ returnpayment Owner~ (Id~ (CostInfo~ 1) Get (CostInfo~ 2) Get))) Gate
				);
 				// cancels existing hiring request which was activated by 'ActuallyStartHiringRequest'
				ActuallyCancelHiringRequest ias (
					// here RequestId must coincide with one of processed GlobalHiringRequestId~
					(RequestId (RequestsInProgress~ RequestId~) Find)^
					(CancelHiringRequest.RqId RequestId~)^
					CancelHiringRequest~ Exe
					// remove request from waiting list
					(RequestsInProgress (RequestsInProgress~ (RequestId~)) Substruct)^
				);
				// called when unit's request is ready (aka agent found and hired unit)
				ProcessRequest ias (
					(RqId ($~ 0) Get)^
					(Rq ($~ 1) Get)^
					(RequestsInProgress (RequestsInProgress~ (RqId~)) Substruct)^
					(CharacterIdInfo ((Rq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqIdPropIndex~) Get)^
					(CharacterProps (Rq~ CmdFormat.RqCharacterInfoIndex~) Get)^
					// creating unit (aka found)
					(UnitId (CharacterIdInfo~ 1) Get)^
					((The hiring agent reports about processing request RqId~ for player Owner~ and sets props CharacterProps~ for unit UnitId~) Ew.Output~) Do
					(Characters.Unit (Unit UnitId~) defer) Born
					((Characters)->(Unit UnitId~)->PlayerId Owner~)^
					((Characters)->(Unit UnitId~)->Id UnitId~)^
					((Characters)->(Unit UnitId~)) Activate
					// concrete mechanism of properties setting
					(CharacterProps~ (Characters)->(Unit UnitId~)->PropertySetOperation~) ForEach
					// notify player about success hiring operation
					((Player Owner~) Tx (ModeInfo.ModeResourceMgr~ hireunit Owner~ (UnitId~))) Gate
				);
				Behavior ias (
					(RightCommand false) ias doNothing;
					(RightCommand true) ias (
						(Command starthiring) ias (
							// get cost and check if player has enough resources
							(CostInfo ((Request~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqCostPropIndex~) Get)^
							// (cost <resource> <value>)
							// Request => original request
							((Player Owner~) Tx (ModeInfo.ModeDeal~ payforunit Owner~ (Request~ (CostInfo~ 1) Get (CostInfo~ 2) Get))) Gate
						);
						(Command paymentresult) ias (
							(payment confirmed) ias ActuallyStartHiringRequest~ Exe;
							(payment declined) ias (
								((Not enough resources for request (Request~ 1) Get) Ew.Output~) Do
							);
							(payment (Request~ 0) Get)~ Exe
						);
						(Command cancelhiring) ias (
							(InProgress false) ias ((Invalid request id RequestId~) Ew.Output~) Do;
							(InProgress true) ias ActuallyCancelHiringRequest~ Exe;
							(InProgress (RequestsInProgress~ RequestId~) In)~ Exe
						);
						(Command check) ias ((Hiring agency Id~ received self test ok) Ew.Output~) Do;
						(Command exit) ias StopAgencyProcess~ Exe;
						(Command Command~)~ Exe
					);
					(RightCommand (Commands~ Command~) In)~ Exe					
				);
			);

			CommandDispatcher ias (
				Dispatch ias nil;
				// (starthiring | cancelhiring RequestId Request)
				// Request=> (((cost vodka 2000)(hiringtime 20000)(id 2))((kind skeleton | zombie | monster) (attack (yes yes yes)) (defence (no no yes)))
				(Agent.Command (Dispatch~ 0) Get)^
				(Agent.RequestId (Dispatch~ 1) Get)^
				(Agent.Request (Dispatch~ 2) Get)^
				Agent.Behavior~ Exe
			);
			HiringAgencyLifeStep ias (
				(ready false false) ias (
					(CommandDispatcher.Dispatch nil)^
					HiringAgencyLifeStep~ Exe
				);
				(ready true false) ias (
					(CommandDispatcher.Dispatch ((HiringAgency Id~) Rx) Gate)^
					((The hiring agency Id~ received CommandDispatcher.Dispatch~) Ew.Output~) Do
					CommandDispatcher~ Exe
					HiringAgencyLifeStep~ Exe
				);
				(ready false true),(ready true true) ias (
					((The hiring agency Id~ of player Owner~ reports about exiting) Ew.Output~) Do
				);
				(ready (((HiringAgency Id~) Ready) Gate true) Ident StopAgency~)~ Exe
			);
			lifeterm = (
				((The hiring agency Id~ activated for owner Owner~) Ew.Output~) Do
				((HiringAgency Id~) Register blocked) Gate
				((HiringAgency Id~) Tx (check 0 ())) Gate
				HiringAgencyLifeStep~ Exe
				((HiringAgency Id~) Unregister) Gate
				Done~ Exe
				((The hiring agency Id~ deactivated for owner Owner~) Ew.Output~) Do
			) Exe
		);
	);
}
