module HiringAgency {
	MagicWorld ias (
		HiringAgency ias (
			Id ias nil;
			Owner ias nil;
			HiringProcessTime ias nil;
			StopAgency ias false;
			GlobalHiringRequestId ias 0;

			// command format
			CmdFormat ias (
				// information about unit's hiring properties
				RqHiringInfoIndex ias 0;
				// hiring property - cost
				RqCostPropIndex ias 0;
				// hiring property - hiring time
				RqHTimePropIndex ias 1;
				// hiring property - character id
				RqIdPropIndex ias 2;
				// information about hiring unit
				RqCharacterInfoIndex ias 1;
			);

			Init ias (
				(StopAgency false)^
#if (verbose)
				((The hiring agency for player Owner~ initialized) Ew.Output~) Do
#endif
			);
			Done ias (
				((Player Id~) Tx (ModeInfo.ModeMain~ partclosed Id~ (hiringagency))) Gate
#if (verbose)
				((The hiring agency for player Owner~ uninitialized) Ew.Output~) Do
#endif
			);
			Agent ias (
				AgentCommands ias (cancel check reset exit report collectstate);
				StaffHiringCommands ias (starthiring cancelhiring staffpaymentresult);
				CarsLookingCommands ias (startcarlooking cancelcarlooking carpaymentresult reservegarageresult);
				Command ias nil;
				Request ias nil;
				RequestId ias 0;
				RequestsInProgress ias ();
				NewRequestId ias nil;

				CharacterIdInfo ias nil;
				
				GetKindOfUnit ias (
					UnitProps ias nil;
					Kind ias nil;
					FetchKindPropForReport ias (
						(KindProp false) ias doNothing;
						(KindProp true) ias (
							(Kind ($~ 1) Get)^
							($~ doNothing)^
						);
						(KindProp (kind ($~ 0) Get) Ident)~ Exe
					);
					(Kind nil)^
					(UnitProps~ FetchKindPropForReport~) ForEach
				);

				GetClazzOfUnit ias (
					UnitProps ias nil;
					Clazz ias nil;
					FetchClazzPropForReport ias (
						(ClazzProp false) ias doNothing;
						(ClazzProp true) ias (
							(Clazz ($~ 1) Get)^
							($~ doNothing)^
						);
						(ClazzProp (clazz ($~ 0) Get) Ident)~ Exe
					);
					(Clazz nil)^
					(UnitProps~ FetchClazzPropForReport~) ForEach
				);

				CancelHiringRequests ias (
					F ias nil;
					(NoRequestsInProgress true) ias F~ Exe;
					(NoRequestsInProgress false) ias (
						StopHiringRequest ias (
							(CancelHiringRequest.RqId $~)^
							CancelHiringRequest~ Exe
						);
						(RequestsInProgress~ StopHiringRequest~) ForEach
						(RequestsInProgress ())^
						F~ Exe
					);
					(NoRequestsInProgress (RequestsInProgress~ ()) Ident)~ Exe
				);
				ResetAgencyProcess ias (
					(CancelHiringRequests.F doNothing)^
					CancelHiringRequests~ Exe
					((Player Id~) Tx (ModeInfo.ModeMain~ partclosed Id~ (hiringagency))) Gate
#if (verbose)
					((The hiring agency for player Owner~ resetted) Ew.Output~) Do
#endif
				);
				StopAgencyProcess ias (
					UnblockAndStop ias (
						(StopAgency true)^
						((HiringAgency Id~) Tx (check 0 ())) Gate
					);
					(CancelHiringRequests.F UnblockAndStop~)^
					CancelHiringRequests~ Exe
				);
				// starts hiring process when user's payment operation approved
				ActuallyStartHiringRequest ias (
					(T false) ias doNothing;
					(T true) ias (TimeToHireInfo ((ConfRq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqHTimePropIndex~) Get)^;
					// associated container
					AC ias staff;
					NCmd ias starthiring;
					// confirmed primary request
					ConfRq ias nil;
					// hiring timer
					TimeToHireInfo ias nil;
					// getting hiring info (time to hire unit)
					(T (TimeToHireInfo~ nil) Ident)~ Exe
					(GlobalHiringRequestId ((GlobalHiringRequestId~) Ew.IncValue~) Do)^
					// generate new request id
					(NewRequestId (Id~ GlobalHiringRequestId~))^
					// save request for processing time (need in case if cancel operation is received)
					(NewRequestId~ (ConfRq~ (AC~ NCmd~)))^
					(RequestsInProgress (RequestsInProgress~ (NewRequestId~)) Join)^
					// (HiringInfo~ 1) Get => time needed to hire specified unit
					((TimeToHireInfo~ 1) Get (Hiring Id~ NewRequestId~) ProcessRequest~ nil (NewRequestId~ ConfRq~)) Recall
					// player may use GlobalHiringRequestId~ in order to cancel hiring operation
#if (verbose)
					((The hiring agent Id~ started hiring request NewRequestId~ for player Owner~ ready in (TimeToHireInfo~ 1) Get ms) Ew.Output~) Do
#endif
#if (release)
					(CharacterIdInfo ((ConfRq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqIdPropIndex~) Get)^
					(GetKindOfUnit.UnitProps (ConfRq~ CmdFormat.RqCharacterInfoIndex~) Get)^
					GetKindOfUnit~ Exe
                                        (UnitId (CharacterIdInfo~ 1) Get)^
					(((hiring NCmd~ inProgress) (Player Owner~) (unit UnitId~) GetKindOfUnit.Kind~ NewRequestId~) Ew.OutChannel~) Do								
#endif
				);
				CancelHiringRequest ias (
					RqId ias nil;
					NCmd ias nil;
					Rq ias nil;
					(-1 (Hiring Id~ RqId~) nil) Recall
#if (verbose)
					((The hiring agent cancelled request RqId~ for player Owner~) Ew.Output~) Do
#endif
					(Rq (RqId~~ 0) Get)^
					// player's account must be recharged
					(CostInfo ((Rq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqCostPropIndex~) Get)^
					((Player Owner~) Tx (ModeInfo.ModeDeal~ returnpayment Owner~ (Id~ (CostInfo~ 1) Get (CostInfo~ 2) Get))) Gate
#if (release)
					(CharacterIdInfo ((Rq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqIdPropIndex~) Get)^
					(GetKindOfUnit.UnitProps (RqId~~ CmdFormat.RqCharacterInfoIndex~) Get)^
					GetKindOfUnit~ Exe
                                        (UnitId (CharacterIdInfo~ 1) Get)^
					(((hiring NCmd~ done) (Player Owner~) (unit UnitId~) GetKindOfUnit.Kind~ RqId~) Ew.OutChannel~) Do								
#endif
				);
 				// cancels existing hiring request which was activated by 'ActuallyStartHiringRequest'
				ActuallyCancelHiringRequest ias (
					(RR true) ias (
#if (verbose)
						((The hiring agent can not cancel request RqId~ for player Owner~ as invalid) Ew.Output~) Do
#endif
#if (release)
						(((hiring cancelhiring failed invalidRequestId) (Player Owner~) (unit UnitId~) RqId~) Ew.OutChannel~) Do								
#endif
					);
					(RR false) ias (
						(CancelHiringRequest.RqId RequestId~)^
						CancelHiringRequest~ Exe
						// remove request from waiting list
						(RequestsInProgress (RequestsInProgress~ (RequestId~)) Substruct)^
					);
					// here RequestId must coincide with one of processed GlobalHiringRequestId~
					(RequestId (RequestsInProgress~ RequestId~) Find)^
					(RR (RequestId~ nil) Ident)~ Exe
				);
				// called when unit's request is ready (aka agent found and hired unit)
				ProcessRequest ias (
					// additional info
					AI ias nil;
					// see StartActualHiring (Recall)
					(RqId ($~ 0) Get)^
					(Rq ($~ 1) Get)^
					(RequestsInProgress (RequestsInProgress~ (RqId~)) Substruct)^
					(CharacterIdInfo ((Rq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqIdPropIndex~) Get)^
					(CharacterProps (Rq~ CmdFormat.RqCharacterInfoIndex~) Get)^
#if (business)
					(CostInfo ((Rq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqCostPropIndex~) Get)^
#endif
					// creating unit (aka found)
					(UnitId (CharacterIdInfo~ 1) Get)^
#if (verbose)
					((The hiring agent reports about processing request RqId~ for player Owner~ and sets props CharacterProps~ for unit UnitId~) Ew.Output~) Do
#endif
					(Characters.Unit (Unit UnitId~) defer) Born
					((Characters)->(Unit UnitId~)->PlayerId Owner~)^
					((Characters)->(Unit UnitId~)->Id UnitId~)^
					((Characters)->(Unit UnitId~)->CId RqId~)^
#if (business)
					((Characters)->(Unit UnitId~)->Cost CostInfo~)^
					((Characters)->(Unit UnitId~)->Contain 0)^
					((Characters)->(Unit UnitId~)->Class 1)^
#endif
					((Characters)->(Unit UnitId~)->Capacity 0)^
					(AI (RqId~~ 1) Get)^
					// notification that is sent to external world
					((Characters)->(Unit UnitId~)->UnitReadyNotify (AI~ 1) Get)^
					// unit's associated container
					((Characters)->(Unit UnitId~)->Container (AI~ 0) Get)^
					// concrete mechanism of properties setting
					(CharacterProps~ (Characters)->(Unit UnitId~)->PropertySetOperation~) ForEach
#if (unitalive)
					((Characters)->(Unit UnitId~)) Activate
#else
					(Characters)->(Unit UnitId~)->NotifyOwner~ Exe
#endif
				);
				PrepareReport ias (
					Report ias (
						TimerState ias nil;
						(ConfRq $~~)^
#if (verbose)
						((The hiring agent Id~ has hiring request ConfRq~ for player Owner~) Ew.Output~) Do
#endif
#if (release)
						(GetKindOfUnit.UnitProps (ConfRq~ CmdFormat.RqCharacterInfoIndex~) Get)^
						GetKindOfUnit~ Exe
						(CharacterIdInfo ((ConfRq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqIdPropIndex~) Get)^
                                        	(UnitId (CharacterIdInfo~ 1) Get)^
						// get timer state
						(TimerState ((Hiring Id~ $~)) TState)^
						(((hiring starthiring inProgress) (Player Owner~) (unit UnitId~) GetKindOfUnit.Kind~ $~ TimerState~) Ew.OutChannel~) Do								
#endif
					);
					(RequestsInProgress~ Report~) ForEach
				);
				CollectState ias (
					State ias ();
					CState ias (
						TimerState ias nil;
						(ConfRq $~~)^
#if (verbose)
						((The hiring agent Id~ has hiring request ConfRq~ for player Owner~) Ew.Output~) Do
#endif
						// get timer state
						(TimerState ((Hiring Id~ $~)) TState)^
						(State (State~ ((ConfRq~ TimerState~))) Join)^
					);
					(RequestsInProgress~ CState~) ForEach
					((Player Owner~) Tx (ModeInfo.ModeStateManagerBroker~ rspcsha Id~ State~)) Gate
				);
				RestoreState ias (
					State ias nil;
					R ias (
						(Rq ($~ 0) Get)^
						(Ts ($~ 1) Get)^
						// hiring request additional info
						(HrAI ($~ 1) Get)^
						(ActuallyStartHiringRequest.NCmd (HrAI~ 1) Get)^
						(ActuallyStartHiringRequest.AC (HrAI~ 0) Get)^
						(ActuallyStartHiringRequest.ConfRq (Rq~ 0) Get)^
						(ActuallyStartHiringRequest.TimeToHireInfo (nil (Ts~ 0) Get))^
						ActuallyStartHiringRequest~ Exe
					);
					(State~ R~) ForEach
					((Player Id~) Tx (ModeInfo.ModeStateManagerBroker~ rsprsha Id~ ())) Gate
				);
				Behavior ias (
					(AgentCommands true) ias (
						(Command cancel) ias (
							(InProgress false) ias (
#if (verbose)
								((Invalid request id RequestId~) Ew.Output~) Do
#endif
							);
							(InProgress true) ias ActuallyCancelHiringRequest~ Exe;
							(InProgress (RequestsInProgress~ RequestId~) In)~ Exe
						);
						(Command report) ias PrepareReport~ Exe;
						(Command collectstate) ias CollectState~ Exe;
						(Command restorestate) ias RestoreState~ Exe;
						(Command check) ias (
#if (verbose)
							((Hiring agency Id~ received self test ok) Ew.Output~) Do
#endif
						);
						(Command exit) ias StopAgencyProcess~ Exe;
						(Command reset) ias ResetAgencyProcess~ Exe;
						(Command Command~)~ Exe
					);
					(AgentCommands false) ias (
						(Staff false) ias (
							(Cars false) ias (
#if (verbose)                           	
								((The hiring agent Id~ reports about unsupported command Command~ for player Owner~) Ew.Output~) Do
#endif
							);
							(Cars true) ias CarsHiringBehaviour~ Exe;
							(Cars (CarsLookingCommands~ Command~) In)~ Exe
						);
						(Staff true) ias StaffHiringBehaviour~ Exe;
						(Staff (StaffHiringCommands~ Command~) In)~ Exe
					);
					(AgentCommands (AgentCommands~ Command~) In)~ Exe
				);
			);

			CommandDispatcher ias (
				Dispatch ias nil;
				// (starthiring | cancelhiring RequestId Request)
				// Request=> (((cost vodka 2000)(hiringtime 20000)(id 2))((kind skeleton | zombie | monster) (attack (yes yes yes)) (defence (no no yes)))
				(Agent.Command (Dispatch~ 0) Get)^
				(Agent.RequestId (Dispatch~ 1) Get)^
				(Agent.Request (Dispatch~ 2) Get)^
				Agent.Behavior~ Exe
			);
			HiringAgencyLifeStep ias (
				(ready false false) ias (
					(CommandDispatcher.Dispatch nil)^
					HiringAgencyLifeStep~ Exe
				);
				(ready true false) ias (
					(CommandDispatcher.Dispatch ((HiringAgency Id~) Rx) Gate)^
#if (verbose)
					((The hiring agency Id~ received CommandDispatcher.Dispatch~) Ew.Output~) Do
#endif
					CommandDispatcher~ Exe
					HiringAgencyLifeStep~ Exe
				);
				(ready false true),(ready true true) ias (
#if (verbose)
					((The hiring agency Id~ of player Owner~ reports about exiting) Ew.Output~) Do
#endif
				);
				(ready (((HiringAgency Id~) Ready) Gate true) Ident StopAgency~)~ Exe
			);
			lifeterm = (
#if (verbose)
				((The hiring agency Id~ activated for owner Owner~) Ew.Output~) Do
#endif
				((HiringAgency Id~) Register blocked) Gate
				((HiringAgency Id~) Tx (check 0 ())) Gate
				((Player Owner~) Tx (ModeInfo.ModeMain~ hainitialized Id~ ())) Gate
				HiringAgencyLifeStep~ Exe
				((HiringAgency Id~) Unregister) Gate
				Done~ Exe
#if (verbose)
				((The hiring agency Id~ deactivated for owner Owner~) Ew.Output~) Do
#endif
			) Exe
		);
	);
}
