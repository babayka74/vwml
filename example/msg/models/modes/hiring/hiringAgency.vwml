module HiringAgency {
	MagicWorld ias (
		HiringAgency ias (
			Id ias nil;
			Owner ias nil;
			HiringProcessTime ias nil;
			StopAgency ias false;
			GlobalHiringRequestId ias 0;

			// command format
			CmdFormat ias (
				// information about unit's hiring properties
				RqHiringInfoIndex ias 0;
				// hiring property - cost
				RqCostPropIndex ias 0;
				// hiring property - hiring time
				RqHTimePropIndex ias 1;
				// hiring property - character id
				RqIdPropIndex ias 2;
				// information about hiring unit
				RqCharacterInfoIndex ias 1;
			);

			Init ias (
				(StopAgency false)^
#if (verbose)
				((The hiring agency for player Owner~ initialized) Ew.Output~) Do
#endif
			);
			Done ias (
				((Player Id~) Tx (ModeInfo.ModeMain~ partclosed Id~ (hiringagency))) Gate
#if (verbose)
				((The hiring agency for player Owner~ uninitialized) Ew.Output~) Do
#endif
			);
			Agent ias (
				AgentCommands ias (cancel check reset exit report collectstate schedulehiringrq);
				StaffHiringCommands ias (instanthiring instanthiringall starthiring cancelhiring staffpaymentresult);
				CarsLookingCommands ias (instantcarlooking startcarlooking cancelcarlooking carpaymentresult reservegarageresult);
				Command ias nil;
				Request ias nil;
				RequestId ias 0;
				NewRequestId ias nil;

				CharacterIdInfo ias nil;
				
				GetKindOfUnit ias (
					UnitProps ias nil;
					Kind ias nil;
					FetchKindPropForReport ias (
						(KindProp false) ias doNothing;
						(KindProp true) ias (
							(Kind ($~ 1) Get)^
							($~ doNothing)^
						);
						(KindProp (kind ($~ 0) Get) Ident)~ Exe
					);
					(Kind nil)^
					(UnitProps~ FetchKindPropForReport~) ForEach
				);

				GetClazzOfUnit ias (
					UnitProps ias nil;
					Clazz ias nil;
					FetchClazzPropForReport ias (
						(ClazzProp false) ias doNothing;
						(ClazzProp true) ias (
							(Clazz ($~ 1) Get)^
							($~ doNothing)^
						);
						(ClazzProp (clazz ($~ 0) Get) Ident)~ Exe
					);
					(Clazz nil)^
					(UnitProps~ FetchClazzPropForReport~) ForEach
				);

				CancelHiringRequests ias (
					F ias nil;
					Q ias nil;
					(NRq true) ias F~ Exe;
					(NRq false) ias (
						StopHiringRequest ias (
							(CancelHiringRequest.RqId $~)^
							(CancelHiringRequest.Q Q~)^
							CancelHiringRequest~ Exe
						);
						(Q~~ StopHiringRequest~) ForEach
						(Q~ ())^
						F~ Exe
					);
					(Q Agent.CarRequestsQ)^
					(NRq (Q~~ ()) Ident)~ Exe
					(Q Agent.StaffRequestsQ)^
					(NRq (Q~~ ()) Ident)~ Exe
				);
				ResetAgencyProcess ias (
					(CancelHiringRequests.F doNothing)^
					CancelHiringRequests~ Exe
					((Player Id~) Tx (ModeInfo.ModeMain~ partclosed Id~ (hiringagency))) Gate
#if (verbose)
					((The hiring agency for player Owner~ resetted) Ew.Output~) Do
#endif
				);
				StopAgencyProcess ias (
					UnblockAndStop ias (
						(StopAgency true)^
						((HiringAgency Id~) Tx (check 0 ())) Gate
					);
					(CancelHiringRequests.F UnblockAndStop~)^
					CancelHiringRequests~ Exe
				);
				RequestConfirmation ias (
					RqId ias nil;
					Rq ias nil;
					NCmd ias nil;
					Status ias nil;
#if (release)
					(CharacterIdInfo ((Rq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqIdPropIndex~) Get)^
					(GetKindOfUnit.UnitProps (Rq~ CmdFormat.RqCharacterInfoIndex~) Get)^
					GetKindOfUnit~ Exe
                                        (UnitId (CharacterIdInfo~ 1) Get)^
					(((hiring NCmd~ Status~) (Player Owner~) (unit UnitId~) GetKindOfUnit.Kind~ RqId~) Ew.OutChannel~) Do								
#endif

				);
				ScheduleRequest ias (
					// confirmed primary request
					NCmd ias starthiring;
					TimeToHireInfo ias nil;
					NewRequestId ias nil;
					Q ias nil;
					(P true) ias ((ProcessingRq Q~) false)^;
					(P false) ias (
						T ias nil;
						(TimeToHireInfo ((NewRequestId~~ 1) Get 2) Get)^
						// NewRequestId~ => (ConfRq~ (AC~ NCmd~ TimeToHireInfo~))
						// (HiringInfo~ 1) Get => time needed to hire specified unit
						((TimeToHireInfo~ 1) Get (Hiring Id~ NewRequestId~) ProcessRequest~ nil (NewRequestId~ (NewRequestId~~ 0) Get Q~ true)) Recall
						// player may use GlobalHiringRequestId~ in order to cancel hiring operation
#if (verbose)
						(T (TimeToHireInfo~ 1) Get)^
						((The hiring agent Id~ started hiring request NewRequestId~ for player Owner~ ready in T~ ms) Ew.Output~) Do
#endif
#if (release)
						(RequestConfirmation.NCmd ((NewRequestId~~ 1) Get 1) Get)^
						(RequestConfirmation.Rq (NewRequestId~~ 0) Get)^
						(RequestConfirmation.RqId NewRequestId~)^
						(RequestConfirmation.Status inProgress)^
						RequestConfirmation~ Exe
#endif
					);
					(Q (Agent.Request~ 0) Get)^
					(NewRequestId (Q~~) First)^
#if (verbose)
					((The hiring agent Id~ schedules NewRequestId~) Ew.Output~) Do
#endif
					(P (NewRequestId~ nil) Ident)~ Exe
				);
				// starts hiring process when user's payment operation approved
				ActuallyStartHiringRequest ias (
					(T false) ias doNothing;
					(T true) ias (TimeToHireInfo ((ConfRq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqHTimePropIndex~) Get)^;
					(ProcessingRq true) ias (
						T ias nil;
#if (verbose)
						(T (TimeToHireInfo~ 1) Get)^
						((The hiring agent Id~ postponed request NewRequestId~ for player Owner~ ready in T~ ms) Ew.Output~) Do
#endif
					);
					(ProcessingRq false) ias (
						((ProcessingRq Q~) true)^
						((HiringAgency Id~) Tx (schedulehiringrq nil (Q~))) Gate
					);
					// associated container
					AC ias staff;
					NCmd ias starthiring;
					// confirmed primary request
					ConfRq ias nil;
					// hiring timer
					TimeToHireInfo ias nil;
					// operational queue (requests)
					Q ias nil;
					// getting hiring info (time to hire unit)
					(T (TimeToHireInfo~ nil) Ident)~ Exe
					(GlobalHiringRequestId~ 0)^
					(GlobalHiringRequestId ((GlobalHiringRequestId~) Ew.IncValue~) Do)^
					// generate new request id
					(NewRequestId (Id~ GlobalHiringRequestId~))^
					// save request for processing time (need in case if cancel operation is received)
					(NewRequestId~ (ConfRq~ (AC~ NCmd~ TimeToHireInfo~)))^
					(NewRequestId~ Agent true) Context
					(Q~ (Q~~ (NewRequestId~)) Join)^
					// checks wether any request is processed now
					(ProcessingRq ((ProcessingRq Q~)~ true) Ident)~ Exe
					// notification about scheduling task
					(RequestConfirmation.NCmd NCmd~)^
					(RequestConfirmation.Rq ConfRq~)^
					(RequestConfirmation.RqId NewRequestId~)^
					(RequestConfirmation.Status scheduled)^
					RequestConfirmation~ Exe
				);
				CancelHiringRequest ias (
					RqId ias nil;
					NCmd ias nil;
					Rq ias nil;
					Q ias nil;
					RqS ias nil;
					(S true) ias (Q~ (Q~~ (RqId~)) Substruct)^;
					(S false) ias doNothing;
					(RqS false) ias ((HiringAgency Id~) Tx (schedulehiringrq nil (Q~))) Gate;
					(RqS true) ias doNothing;

					(RqS ((Hiring Id~ RqId~)) TState)^
					(-1 (Hiring Id~ RqId~) nil) Recall
					(S (Q~~ RqId~) In)~ Exe
#if (verbose)
					((The hiring agent cancelled request RqId~ for player Owner~ on queue Q~ left Q~~) Ew.Output~) Do
#endif
					(RqId~ Agent true) Context
					(Rq (RqId~~ 0) Get)^
					// player's account must be recharged
					(CostInfo ((Rq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqCostPropIndex~) Get)^
					((Player Owner~) Tx (ModeInfo.ModeDeal~ returnpayment Owner~ (Id~ (CostInfo~ 1) Get (CostInfo~ 2) Get))) Gate
#if (release)
					(RequestConfirmation.NCmd NCmd~)^
					(RequestConfirmation.Rq Rq~)^
					(RequestConfirmation.RqId RqId~)^
					(RequestConfirmation.Status done)^
					RequestConfirmation~ Exe
#endif
					(RqS (RqS~ nil) Ident)~ Exe
				);
				InstantAllRequests ias (
					NCmd ias nil;
					Q ias nil;
					Instant ias (
						(-1 (Hiring Id~ $~) nil) Recall
#if (verbose)
						((The hiring agent cancelled request $~ for player Owner~ on queue Q~) Ew.Output~) Do
#endif
						(100 (Hiring Id~ $~) ProcessRequest~ nil ($~ ($~~ 0) Get Q~ false)) Recall
					);
					(Q~~ Instant~) ForEach
					(Q~ ())^
				);
				InstantHiringRequest ias (
					RqId ias nil;
					Rq ias nil;
					NCmd ias nil;
					(T false) ias (
						(-2 (Hiring Id~ RqId~) nil) Recall
#if (verbose)
						((The hiring agent immidiately handles request RqId~ for player Owner~) Ew.Output~) Do
#endif
#if (release)
						(RequestConfirmation.Status done)^
#endif
					);
					(T true) ias (
#if (release)
						(RequestConfirmation.Status failed)^
#endif
					);
					(RqId~ Agent true) Context
					(Rq (RqId~~ 0) Get)^
					(RequestConfirmation.Rq Rq~)^
					(RequestConfirmation.RqId RqId~)^
					(RequestConfirmation.NCmd NCmd~)^
					(T (((Hiring Id~ RqId~)) TState nil) Ident)~ Exe
					RequestConfirmation~ Exe
				);
				// called when unit's request is ready (aka agent found and hired unit)
				ProcessRequest ias (
					// additional info
					AI ias nil;
					Q ias nil;
					RqId ias nil;
					Rq ias nil;

					(QR true) ias (Q~ (Q~~ (RqId~)) Substruct)^;
					(QR false) ias doNothing;
					// see StartActualHiring (Recall)
					(RqId ($~ 0) Get)^
					(Rq ($~ 1) Get)^
					(Q ($~ 2) Get)^
					(QR ($~ 3) Get)~ Exe

#if (verbose)
					((The hiring agent player Owner~ remained units in Q~ are Q~~) Ew.Output~) Do
#endif

					(CharacterIdInfo ((Rq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqIdPropIndex~) Get)^
					(CharacterProps (Rq~ CmdFormat.RqCharacterInfoIndex~) Get)^
#if (business)
					(CostInfo ((Rq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqCostPropIndex~) Get)^
#endif
					// creating unit (aka found)
					(UnitId (CharacterIdInfo~ 1) Get)^
#if (verbose)
					((The hiring agent reports about processing request RqId~ for player Owner~ and sets props CharacterProps~ for unit UnitId~) Ew.Output~) Do
#endif
					(Characters.Unit (Unit UnitId~) defer) Born
					((MagicWorld)->(Characters)->(Unit UnitId~)->PlayerId Owner~)^
					((MagicWorld)->(Characters)->(Unit UnitId~)->Id UnitId~)^
					((MagicWorld)->(Characters)->(Unit UnitId~)->CId RqId~)^
#if (business)
					((MagicWorld)->(Characters)->(Unit UnitId~)->Cost CostInfo~)^
					((MagicWorld)->(Characters)->(Unit UnitId~)->Contain 0)^
					((MagicWorld)->(Characters)->(Unit UnitId~)->Class 1)^
#endif
					((MagicWorld)->(Characters)->(Unit UnitId~)->Capacity 0)^
					(AI (RqId~~ 1) Get)^
					// notification that is sent to external world
					((MagicWorld)->(Characters)->(Unit UnitId~)->UnitReadyNotify (AI~ 1) Get)^
					// unit's associated container
					((MagicWorld)->(Characters)->(Unit UnitId~)->Container (AI~ 0) Get)^
					// concrete mechanism of properties setting
					(CharacterProps~ (MagicWorld)->(Characters)->(Unit UnitId~)->PropertySetOperation~) ForEach
#if (unitalive)
					((MagicWorld)->(Characters)->(Unit UnitId~)) Activate
#else
					(MagicWorld)->(Characters)->(Unit UnitId~)->NotifyOwner~ Exe
#endif                                  // increment reference counter
					((schedulehiringrq nil (Q~)) 0)^
					((HiringAgency Id~) Tx (schedulehiringrq nil (Q~))) Gate
				);
				PrepareReport ias (
					Q ias nil;
					Report ias (
						TimerState ias nil;
						(ConfRq $~~)^
#if (verbose)
						((The hiring agent Id~ has hiring request ConfRq~ for player Owner~) Ew.Output~) Do
#endif
#if (release)
						(GetKindOfUnit.UnitProps (ConfRq~ CmdFormat.RqCharacterInfoIndex~) Get)^
						GetKindOfUnit~ Exe
						(CharacterIdInfo ((ConfRq~ CmdFormat.RqHiringInfoIndex~) Get CmdFormat.RqIdPropIndex~) Get)^
                                        	(UnitId (CharacterIdInfo~ 1) Get)^
						// get timer state
						(TimerState ((Hiring Id~ $~)) TState)^
						(((hiring starthiring inProgress) (Player Owner~) (unit UnitId~) GetKindOfUnit.Kind~ $~ TimerState~) Ew.OutChannel~) Do								
#endif
					);
					(Q Agent.CarRequestsQ)^
					(Q~~ Report~) ForEach
					(Q Agent.StaffRequestsQ)^
					(Q~~ Report~) ForEach
				);
				CollectState ias (
					State ias ();
					Q ias nil;
					CState ias (
						TimerState ias nil;
						(ConfRq $~~)^
#if (verbose)
						((The hiring agent Id~ has hiring request ConfRq~ for player Owner~) Ew.Output~) Do
#endif
						// get timer state
						(TimerState ((Hiring Id~ $~)) TState)^
						(State (State~ ((ConfRq~ TimerState~))) Join)^
					);
					(Q Agent.CarRequestsQ)^
					(Q~~ CState~) ForEach
					(Q Agent.StaffRequestsQ)^
					(Q~~ CState~) ForEach
					((Player Owner~) Tx (ModeInfo.ModeStateManagerBroker~ rspcsha Id~ State~)) Gate
				);
				RestoreState ias (
					State ias nil;
					R ias (
						(Rq ($~ 0) Get)^
						(Ts ($~ 1) Get)^
						// hiring request additional info
						(HrAI ($~ 1) Get)^
						(ActuallyStartHiringRequest.NCmd (HrAI~ 1) Get)^
						(ActuallyStartHiringRequest.AC (HrAI~ 0) Get)^
						(ActuallyStartHiringRequest.ConfRq (Rq~ 0) Get)^
						(ActuallyStartHiringRequest.TimeToHireInfo (nil (Ts~ 0) Get))^
						ActuallyStartHiringRequest~ Exe
					);
					(State~ R~) ForEach
					((Player Id~) Tx (ModeInfo.ModeStateManagerBroker~ rsprsha Id~ ())) Gate
				);
				Behavior ias (
					(AgentCommands true) ias (
						(Command cancel) ias doNothing;
						(Command report) ias PrepareReport~ Exe;
						(Command collectstate) ias CollectState~ Exe;
						(Command restorestate) ias RestoreState~ Exe;
						(Command check) ias (
#if (verbose)
							((Hiring agency Id~ received self test ok) Ew.Output~) Do
#endif
						);
						(Command exit) ias StopAgencyProcess~ Exe;
						(Command reset) ias ResetAgencyProcess~ Exe;
						(Command schedulehiringrq) ias ScheduleRequest~ Exe;
						(Command Command~)~ Exe
					);
					(AgentCommands false) ias (
						(Staff false) ias (
							(Cars false) ias (
#if (verbose)                           	
								((The hiring agent Id~ reports about unsupported command Command~ for player Owner~) Ew.Output~) Do
#endif
							);
							(Cars true) ias CarsHiringBehaviour~ Exe;
							(Cars (CarsLookingCommands~ Command~) In)~ Exe
						);
						(Staff true) ias StaffHiringBehaviour~ Exe;
						(Staff (StaffHiringCommands~ Command~) In)~ Exe
					);
					(AgentCommands (AgentCommands~ Command~) In)~ Exe
				);
			);

			CommandDispatcher ias (
				Dispatch ias nil;
				// (starthiring | cancelhiring RequestId Request)
				// Request=> (((cost vodka 2000)(hiringtime 20000)(id 2))((kind skeleton | zombie | monster) (attack (yes yes yes)) (defence (no no yes)))
				(Agent.Command (Dispatch~ 0) Get)^
				(Agent.RequestId (Dispatch~ 1) Get)^
				(Agent.Request (Dispatch~ 2) Get)^
				Agent.Behavior~ Exe
			);
			HiringAgencyLifeStep ias (
				(ready false false) ias (
					(CommandDispatcher.Dispatch nil)^
					HiringAgencyLifeStep~ Exe
				);
				(ready true false) ias (
					(CommandDispatcher.Dispatch ((HiringAgency Id~) Rx) Gate)^
#if (verbose)
					((The hiring agency Id~ received CommandDispatcher.Dispatch~) Ew.Output~) Do
#endif
					CommandDispatcher~ Exe
					HiringAgencyLifeStep~ Exe
				);
				(ready false true),(ready true true) ias (
#if (verbose)
					((The hiring agency Id~ of player Owner~ reports about exiting) Ew.Output~) Do
#endif
				);
				(ready (((HiringAgency Id~) Ready) Gate true) Ident StopAgency~)~ Exe
			);
			lifeterm = (
#if (verbose)
				((The hiring agency Id~ activated for owner Owner~) Ew.Output~) Do
#endif
				(Agent.(ProcessingRq CarRequestsQ) false)^
				(Agent.(ProcessingRq StaffRequestsQ) false)^
				((HiringAgency Id~) Register blocked) Gate
				((HiringAgency Id~) Tx (check 0 ())) Gate
				((Player Owner~) Tx (ModeInfo.ModeMain~ hainitialized Id~ ())) Gate
				HiringAgencyLifeStep~ Exe
				((HiringAgency Id~) Unregister) Gate
				Done~ Exe
#if (verbose)
				((The hiring agency Id~ deactivated for owner Owner~) Ew.Output~) Do
#endif
			) Exe
		);
	);
}
