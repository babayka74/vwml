module BossOfDistrict {
	MagicWorld ias (
		BossOfDistrict ias (
			// player id
			Owner ias nil;
			// boss's quartals started from this id
			QNIds ias 0;
			// boss controls following number of quartals
			Quartals ias 0;
			// bosse's id (BossD <player id> <boss id>)
			Id ias nil;
			// list of quartals (sherifs)
			CQs ias ();
			// boss bribes ranges
			BBR ias ();
			// initialized during phase (oninit | runtime)
			InitAs ias oninit;
			// incoming command id and its params
			CommandId ias nil;
			CommandParams ias nil;
			// desired bribe
			DesiredBribe ias nil;
			Negotiated ias false;
			Initialized ias false;
			// initialization steps
			Init ias (
				// called when all boss's quartals are initialized
				Finished ias (
#if (verbose)
					((Boss Id~ of player Owner~ initialized with (DesiredBribe~ BBR~ QNIds~ Quartals~) and CQs~) Ew.Output~) Do
#endif
					(Initialized true)^
#if (release)
					(((bossofdistrict init done) (player Owner~) (boss Id~)) Ew.OutChannel~) Do
#endif
					((Player Owner~) Tx (ModeInfo.ModeBossOfDistrict~ bossinitialized Id~ ())) Gate
				);
				(Negotiated false)^
				(Initialized false)^
				(DesiredBribe BBR~ Random)^
				(Player.Resources.Quartals.Construct (Construct Id~) defer) Born
				((Player)->(Resources)->(Quartals)->(Construct Id~)->R (Boss Id~))^
				((Player)->(Resources)->(Quartals)->(Construct Id~)->PId Owner~)^
				((Player)->(Resources)->(Quartals)->(Construct Id~)->M boss)^
				((Player)->(Resources)->(Quartals)->(Construct Id~)->F QNIds~)^
				((Player)->(Resources)->(Quartals)->(Construct Id~)->N Quartals~)^
				((Player)->(Resources)->(Quartals)->(Construct Id~)->CB Finished~)^
				(Player)->(Resources)->(Quartals)->(Construct Id~)~ Exe
			);
			Done ias (
				Finished ias (
					(CQs () Finished~) FinishInterception
					(Initialized false)^
#if (release)
					(((bossofdistrict exit done) (player Owner~) (boss Id~)) Ew.OutChannel~) Do
#endif
					((Player Owner~) Tx (ModeInfo.ModeBossOfDistrict~ bossuninitialized Id~ ())) Gate
				);
				(CQs () Finished~) StartInterception
				Deactivate~ Exe
				((Player)->(Resources)->(Quartals)->(Construct Id~)) Release
			);
			Deactivate ias (
				(N false) ias (
					D ias ((Player Owner~) Tx (ModeInfo.ModeDeal~ releasequartal Owner~ ($~))) Gate;
					(CQs~ D~) ForEach
				);
				(N true) ias Finished~ Exe;
				(N (CQs~ ()) Ident)~ Exe
			);
			// add quartal under boss's supervision
			AddQuartal ias (
				Q ias nil;
				(CQs (CQs~ (Q~)) Join)^
#if (verbose)
				((Player Owner~ added quartal Q~ under boss Id~ supervision and quartals are CQs~) Ew.Output~) Do
#endif
			);
			PublishQuartals ias (
				PQ ias (
					((Player Owner~) Tx (ModeInfo.ModeDeal~ linkquartal $~ ($~))) Gate
				);
				(CQs~ PQ~) ForEach
			);
			// pay bribe and send back list of quartals to player 
			PayBribe ias (
				B ias nil;
				BribeRT ias nil;
				BribeV ias nil;
				// performs actula payment
				Pay ias (
					(Result true) ias (
						(Negotiated true)^
#if (release)
						(((bossofdistrict paybribe done) (player Owner~) (boss Id~) (bribe done B~)) Ew.OutChannel~) Do
#endif
						// all quartals are controlled by player from now
						PublishQuartals~ Exe
						(CQs ())^
						((Player Owner~) Tx (ModeInfo.ModeBossOfDistrict~ exit Id~ ())) Gate
					);
					(Result false) ias (
#if (release)
						(((bossofdistrict paybribe failed) (player Owner~) (boss Id~) (noResources B~)) Ew.OutChannel~) Do
#endif
					);
					((Player Owner~)->(Resources)->(Account)->Value B~)^
					(Player Owner~)->(Resources)->Withdraw~ Exe
					(Result (Player Owner~)->(Resources)->Result~)~ Exe
				);
				// different resources
				(CRT false) ias (
#if (verbose)
					((Invalid resource type BribeRT~ requested (DesiredBribe~ 0) Get) Ew.Output~) Do				
#endif
#if (release)
					(((bossofdistrict paybribe failed) (player Owner~) (boss Id~) (invaildResource BribeRT~ (DesiredBribe~ 0) Get)) Ew.OutChannel~) Do
#endif
				);
				// same resources
				(CRT true) ias (
					// boss doesn't take bribe - too small
					(BB -1) ias (
#if (verbose)
						((The boss Id~ of player Owner~ did not take bribe B~ desired bribe is DesiredBribe~) Ew.Output~) Do
#endif
#if (release)
						(((bossofdistrict paybribe failed) (player Owner~) (boss Id~) (bribe tooSmall B~ DesiredBribe~)) Ew.OutChannel~) Do
#endif
					);
					(BB 0),(BB 1) ias Pay~ Exe;
					// boss's bribe
					(BB ((BribeV~ (DesiredBribe~ 1) Get) Ew.Compare~) Do)~ Exe
				);
				(BribeRT (B 0) Get)^
				(BribeV  (B 1) Get)^
				// check resource type
				(CRT (BribeRT~ (DesiredBribe~ 0) Get) Ident)~ Exe
			);
			PublishQInfo ias (
				I ias ();
				P ias (I (I~ (($~ (Quartal $~)->QInfo~))) Join)^;
				(I ())^
				(CQs~ P~) ForEach
#if (release)
				(((bossofdistrict openinfo done) (player Owner~) (boss Id~) I~) Ew.OutChannel~) Do
#endif
			);
			// commands
			(Handle addquartal),(Handle linkquartal) ias (
				(AddQuartal.Q (CommandParams~ 0) Get)^
				AddQuartal~ Exe
			);
			(Handle paybribe) ias (
				(PayBribe.B (CommandParams~ 0) Get)^
				PayBribe~ Exe
			);
			(Handle quartalsinfo) ias PublishQInfo~ Exe;
			(Handle qinitialized) ias (
				((Player)->(Resources)->(Quartals)->(Construct Id~)->BQN (((Player)->(Resources)->(Quartals)->(Construct Id~)->BQN~) Ew.DecValue~) Do)^
#if (verbose)
				(((Player)->(Resources)->(Quartals)->(Construct Id~)->BQN~ left quartals to initialize boss Id~) Ew.Output~) Do
#endif
			);
			(Handle releasedquartal) ias (
				(Q (CommandParams~ 0) Get)^
#if (verbose)
				((Quartal Q~ released of boss Id~ left quartals CQs~) Ew.Output~) Do
#endif
				(CQs (CQs~ (Q~)) Substruct)^
			);
			(Handle exit),(Handle reset) ias Done~ Exe;
			(Handle CommandId~)~ Exe
		);
	);
}
