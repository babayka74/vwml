module Quartal {
	MagicWorld ias (
		Quartal ias (
			/* each quartal has own uniq id */
			Id ias nil;
			/* player who owns quartal */
			Owner ias nil;
			/* false means that quartal owned but not linked to specific player */
			Linked ias false;
			/* 'true' in case if quartal is not active */
			StopQuartal ias false;
			/* last sender */
			Source ias nil;
			/* last opened quartal info */
			QInfo ias nil;
			/* quartal's resource type (configured) */
			Resource ias (
				Type ias nil;
				/* contribution to player's account */
				Quantum ias 0;
				/* period  */
				Period ias 100; // ms
				/* number */
				Quantums ias 0;
				/* transition cnp -> op; permitted time */
				OpenTime ias 1000; // ms
				/* desired price set by sherif to open information about quartal */
				DesiredOpenInfoPrice ias 100;
				Update ias (
					QConf ias nil;
					BribeR ias nil;
					/* check input parameter */
					CIP ias (
						// name
						P ias nil;
						// value
						V ias nil;
						(NotSetIt true) ias doNothing;
						(NotSetIt false) ias (P~ V~)^;
						(NotSetIt (V~ nil) Ident)~ Exe
					);
					SetV ias (	((CIP.P Type)^ (CIP.V (QConf~ 0) Get)^)
							((CIP.P Quantum)^ (CIP.V (QConf~ 1) Get)^)
							((CIP.P Period)^ (CIP.V (QConf~ 2) Get)^)
							((CIP.P Quantums)^ (CIP.V (QConf~ 3) Get)^)
							((CIP.P OpenTime)^ (CIP.V (QConf~ 4) Get)^)
							((CIP.P DesiredOpenInfoPrice)^ (CIP.V (QConf~ 5) Get)^)
							((CIP.P Sherif.DesiredBribe)^ (CIP.V BribeR~)^)
					);
					SetIt ias (
						$~ Exe
						CIP~ Exe
					);
					(SetV~ SetIt~) ForEach	
				);
				/* resource initialization point */
				Init ias (
					(Update.QConf Ew.QuartalsConf.Resources~ Random)^
					(Update.BribeR Ew.QuartalsConf.SherifBribeRanges~ Random)^
					Update~ Exe
					((Quartal Id~) Tx (Id~ check ())) Gate
#if (verbose)
					((The quartal Id~ initialized with configuration (Type~ Quantum~ Period~ Quantums~ OpenTime~ DesiredOpenInfoPrice~ Sherif.DesiredBribe~)) Ew.Output~) Do					
#endif
				);
			);
			/* one sherif is associated with one quartal; the banker is aux. entity */
			Sherif ias (
				Intention ias nil;
				IntentionParams ias nil;
				DesiredBribe ias nil;
				ProposedBribe ias nil;
				Banker ias (
					OnSuccessPayment ias nil;
					OnFailedPayment ias nil;
					Value ias 0;
					Account ias 0;
					ApplyPayment ias (
						(Account ((Account~ Value~) Ew.Sum~) Do)^
#if (verbose)
						((The sherif of quartal Id~ has following sum Banker.Account~ on account) Ew.Output~) Do
#endif
					);
					AskPaymentForQuartalInfo ias (
						((Player Owner~) Tx (ModeInfo.ModeDeal~ payquartalinfo Owner~ (Id~ Resource.Type~ Value~))) Gate
					);
					AskPaymentForBribe ias (
						((Player Owner~) Tx (ModeInfo.ModeDeal~ paybribe Owner~ (Id~ Resource.Type~ Value~))) Gate
					);
					Pay ias (
						(NoQuantums true) ias (
							((Quartal Id~) Tx (Owner~ requestdone ())) Gate
#if (verbose)
							((The banker of sherif of quartal Id~ payed to player Owner~ all requested resources) Ew.Output~) Do
#endif
#if (release)
							(((deal setintention done) (player Owner~) (quartal Id~) Behavior.State~ (pay finished)) Ew.OutChannel~) Do
#endif
						);
						(NoQuantums false) ias (
							(Resource.Quantums ((Resource.Quantums~) Ew.DecValue~) Do)^
#if (verbose)
							((The banker of sherif of quartal Id~ pays to player Owner~ resource (Id~ Resource.Type~ Resource.Quantum~) and left Resource.Quantums~ payments) Ew.Output~) Do
#endif
							/* sending contribution message to player */
							((Player Owner~) Tx (ModeInfo.ModeDeal~ contribution Owner~ (Id~ Resource.Type~ Resource.Quantum~))) Gate
							/* re-activates contribution */
							(Resource.Period~ (Pay Id~) Pay~) Recall
						);
						(NoQuantums (Resource.Quantums~ 0) Ident)~ Exe
					);
				);
				OpenInfo ias (
					(QInfo (Resource.Type~ Resource.Quantum~ Resource.Period~ Resource.Quantums~ Resource.OpenTime~ Resource.DesiredOpenInfoPrice~ Sherif.DesiredBribe~))^
#if (verbose)
					((The sherif of quartal Id~ opens information about quartal QInfo~) Ew.Output~) Do
#endif
#if (release)                                   
					(((deal setintention done) (player Owner~) (quartal Id~) Behavior.State~ (open QInfo~)) Ew.OutChannel~) Do
#endif
				);
				Behavior ias (
					State ias cnp;
					// called when (Cnp Id~) timer expired
					OnCnpTimer ias ((Quartal Id~) Tx (Owner~ close ())) Gate;

					SuccessPaymentForInvestment ias (
					);

					FailedPaymentForInvestment ias (
					);

					SuccessPaymentForOpenInfo ias (
						(State onp)^
						Banker.ApplyPayment~ Exe
						OpenInfo~ Exe
						// starts timer; when timer is finished quartal is moved to state cnp
						(Resource.OpenTime~ (Cnp Id~) OnCnpTimer~) Recall
#if (verbose)
						((The sherif of quartal Id~ provided information to player Owner~ and cnp timer activated for Resource.OpenTime~ ms) Ew.Output~) Do
#endif
					);
					FailedPaymentForOpenInfo ias (
						(State cnp)^
#if (verbose)
						((The sherif of quartal Id~ did not provide information to player Owner~ since payment was failed) Ew.Output~) Do
#endif
#if (release)
						(((deal setintention failed) (player Owner~) (quartal Id~) State~ (open notEnoughResources)) Ew.OutChannel~) Do
#endif

					);
					SuccessPaymentForBribeRequest ias (
						// stop timer which could be activated on cnp state
#if (verbose)
						((The sherif of quartal Id~ took bribe ProposedBribe~) Ew.Output~) Do
#endif
#if (release)
						(((deal setintention inProgress) (player Owner~) (quartal Id~) State~ (pay QInfo~)) Ew.OutChannel~) Do
#endif
						Banker.ApplyPayment~ Exe
						(State op)^
						// starts timer of contribution period (player's bank account will being updated during this period)
						(Resource.Period~ (Pay Id~) Banker.Pay~) Recall
					);
					FailedPaymentForBribeRequest ias (
						(State onp)^
#if (verbose)
						((The sherif of quartal Id~ did not get bribe payment from player Owner~ since payment was failed) Ew.Output~) Do
#endif
#if (release)
						(((deal setintention failed) (player Owner~) (quartal Id~) State~ (pay notEnoughResources)) Ew.OutChannel~) Do
#endif
					);
					/* payment */
					(paymentresult wtp) ias (
						(paymentresult confirmed) ias Banker.OnSuccessPayment~ Exe;
						(paymentresult declined) ias Banker.OnFailedPayment~ Exe;
						(paymentresult (Sherif.IntentionParams~ 0) Get)~ Exe
					);
					(open wtp),(close wtp),(pay wtp),(requestdone wtp),(notrequestdone wtp) ias (
						// remember messages which were received during payment operation
				        	((Quartal Id~) Tx (Source~ Sherif.Intention~ Sherif.IntentionParams~)) Gate
					);
					// provides information about how much should be payed in order to get information about quartal
					(open cnp) ias (
						(OpenQInfo 0),(OpenQInfo 1) ias (
							(Banker.Value ProposedPrice~)^
							(Banker.OnSuccessPayment SuccessPaymentForOpenInfo~)^
							(Banker.OnFailedPayment FailedPaymentForOpenInfo~)^
							Banker.AskPaymentForQuartalInfo~ Exe
							(State wtp)^
						);
						(OpenQInfo -1) ias (
#if (verbose)
							((The sherif of quartal Id~ did not allow to open info ProposedPrice~ desired price is Resource.DesiredOpenInfoPrice~) Ew.Output~) Do
#endif
#if (release)
							(((deal setintention failed) (player Owner~) (quartal Id~) State~ (open toSmall ProposedPrice~ Resource.DesiredOpenInfoPrice~)) Ew.OutChannel~) Do
#endif
						);
						(ProposedPrice (Sherif.IntentionParams~ 0) Get)^
						// sherif's bribe
						(OpenQInfo ((ProposedPrice~ Resource.DesiredOpenInfoPrice~) Ew.Compare~) Do)~ Exe
					);
					(open cp) ias (
						(State op)^
#if (verbose)
						((The sherif of quartal Id~ provides information to player Owner~ and no sherif account updated) Ew.Output~) Do
#endif
					);
					(open onp),(open op) ias (
#if (verbose)
						((The sherif of quartal Id~ has already opened information about quartal) Ew.Output~) Do
#endif
						OpenInfo~ Exe
					);
					(close onp) ias (
						Resource.Init~ Exe
						(State cnp)^
#if (verbose)
						((The sherif of quartal Id~ closed information no bribe got) Ew.Output~) Do
#endif
#if (release)
						(((deal setintention done) (player Owner~) (quartal Id~) State~ (open closed)) Ew.OutChannel~) Do
#endif
					);
					// 
					(close op) ias (
						(State cp)^
#if (verbose)
						((The sherif of quartal Id~ closed information bribe was got) Ew.Output~) Do
#endif
#if (release)
						(((deal setintention done) (player Owner~) (quartal Id~) State~ (open closed)) Ew.OutChannel~) Do
#endif
					);
					(close cnp),(close cp) ias (
#if (verbose)
						((The sherif of quartal Id~ has already closed information about quartal) Ew.Output~) Do
#endif
					);
					// player payes bribe and started getting resources and back payements from quartal
					(pay onp) ias (
						(TakeBribe 0),(TakeBribe 1) ias (
							(Banker.Value ProposedBribe~)^
							(Banker.OnSuccessPayment SuccessPaymentForBribeRequest~)^
							(Banker.OnFailedPayment FailedPaymentForBribeRequest~)^
							Banker.AskPaymentForBribe~ Exe
							(State wtp)^
						);
						(TakeBribe -1) ias (
#if (verbose)
							((The sherif of quartal Id~ did not take bribe ProposedBribe~ desired bribe is DesiredBribe~) Ew.Output~) Do
#endif
#if (release)
							(((deal setintention failed) (player Owner~) (quartal Id~) State~ (pay toSmall ProposedBribe~ DesiredBribe~)) Ew.OutChannel~) Do
#endif
						);
						(ProposedBribe (Sherif.IntentionParams~ 0) Get)^
						// sherif's bribe
						(TakeBribe ((ProposedBribe~ DesiredBribe~) Ew.Compare~) Do)~ Exe
					);
					(pay cnp),(pay op),(pay cp) ias (
#if (verbose)
						((The sherif of quartal Id~ has already got bribe or qaurtal is closed and payment is impossible) Ew.Output~) Do
#endif
					);
					(requestdone op) ias (
						Resource.Init~ Exe
						(State onp)^
					);
					(requestdone cp) ias (
						Resource.Init~ Exe
						(State cnp)^
					);
					(requestdone cnp),(requestdone onp) ias (
#if (verbose)
						((The sherif of quartal Id~ got invalid message from banker) Ew.Output~) Do
#endif
					);
					(notrequestdone op) ias (
						(State op)^
					);
					(notrequestdone cp) ias (
						(State cp)^
					);
					(notrequestdone cnp),(notrequestdone onp) ias (
#if (verbose)
						((The sherif of quartal Id~ got invalid message from banker) Ew.Output~) Do
#endif
					);
					(exit op),(exit cnp),(exit onp),(exit wtp) ias (
						(StopQuartal true)^
						(-1 (Pay Id~) Banker.Pay~) Recall
						// unblocks quartal's gate
						((Quartal Id~) Tx (Id~ check ())) Gate
#if (verbose)
						((The sherif of quartal Id~ got message to exit) Ew.Output~) Do
#endif
					);
					(check op),(check cnp),(check onp),(check wtp) ias (
#if (verbose)
						((The sherif of quartal Id~ which belongs to player Owner~ got self test message) Ew.Output~) Do
#endif
					);
					(updateconf op),(updateconf cnp),(updateconf onp),(updateconf wtp),(updateconf cp) ias (
						(Resource.Update.QConf (IntentionParams~ 0) Get)^
						(Resource.Update.BribeR (IntentionParams~ 1) Get)^
						Resource.Update~ Exe
						(QInfo (Resource.Type~ Resource.Quantum~ Resource.Period~ Resource.Quantums~ Resource.OpenTime~ Resource.DesiredOpenInfoPrice~ Sherif.DesiredBribe~))^
#if (verbose)
						((The quartal Id~ updated with configuration QInfo~) Ew.Output~) Do					
#endif						
#if (release)
						(((deal setintention done) (player Owner~) (quartal Id~) State~ (updateconf QInfo~)) Ew.OutChannel~) Do
#endif
					);
					(getstate op),(getstate cnp),(getstate onp),(getstate wtp),(getstate cp) ias (
#if (verbose)
						((The quartal Id~ is in state State~ and belongs to Owner~) Ew.Output~) Do					
#endif
#if (release)
						(((deal setintention done) (player Owner~) (quartal Id~) State~ (getstate QInfo~)) Ew.OutChannel~) Do
#endif
					);
					// investement allowed during contribution active phase only
					(invest op) ias (
#if (verbose)
						((The quartal Id~ belongs to Owner~ is in state State~ and investment allowed) Ew.Output~) Do					
#endif
						(Banker.Value (Sherif.IntentionParams~ 0) Get)^
						(Banker.OnSuccessPayment SuccessPaymentForInvestment~)^
						(Banker.OnFailedPayment FailedPaymentForInvestment~)^
						Banker.AskPaymentForQuartalInfo~ Exe
						(State wtp)^
					);
					// invest isn't allowed
				        (invest cnp),(invest onp),(invest wtp),(invest cp) ias (
#if (verbose)
						((The quartal Id~ belongs to Owner~ is in state State~ and investment not allowed) Ew.Output~) Do					
#endif
						(((deal setintention failed) (player Owner~) (quartal Id~) State~ (invest QInfo~)) Ew.OutChannel~) Do
					);
					(Intention~ State~)~ Exe
				);
			);
			CommandDispatcher ias (
				Dispatch ias nil;
				// (Id~ Intention~ IntentionParams~)
				(Source (Dispatch~ 0) Get)^
				(Sherif.Intention (Dispatch~ 1) Get)^
				(Sherif.IntentionParams (Dispatch~ 2) Get)^
				Sherif.Behavior~ Exe
			);
			QuartalLifeStep ias (
				(ready false false) ias (
					(CommandDispatcher.Dispatch nil)^
					QuartalLifeStep~ Exe
				);
				(ready true false) ias (
					(CommandDispatcher.Dispatch ((Quartal Id~) Rx) Gate)^
#if (verbose)
					((The quartal Id~ received CommandDispatcher.Dispatch~) Ew.Output~) Do
#endif
					CommandDispatcher~ Exe
					QuartalLifeStep~ Exe
				);
				(ready false true),(ready true true) ias (
#if (verbose)
					((The sherif of quartal Id~ reports about exiting) Ew.Output~) Do
#endif
				);
				(ready (((Quartal Id~) Ready) Gate true) Ident StopQuartal~)~ Exe
			);
			lifeterm = (
#if (verbose)
				((The quartal Id~ added and active for owner Owner~) Ew.Output~) Do
#endif
				((Quartal Id~) Register blocked) Gate
				Resource.Init~ Exe
				// links quartal to given player
				((Player Owner~) Tx (ModeInfo.ModeDeal~ linkquartal Id~ (Id~))) Gate
				((Player Owner~) Tx (ModeInfo.ModeMain~ qinitialized Id~ (Id~))) Gate
				QuartalLifeStep~ Exe
				((Quartal Id~) Unregister) Gate
			) Exe
		);
	);
}
