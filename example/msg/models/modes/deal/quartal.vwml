module Quartal {
	MagicWorld ias (
		Quartal ias (
			/* each quartal has own uniq id */
			Id ias nil;
			/* player who owns quartal */
			Owner ias nil;
			/* false means that quartal owned but not linked to specific player */
			Linked ias false;
			/* 'true' in case if quartal is not active */
			StopQuartal ias false;
			/* last sender */
			Source ias nil;
			/* last opened quartal info */
			QInfo ias nil;
			/* last invested resource quantum */
			LInvestQuantum ias 0;
			/* last stored quartal state (not stored) */
			LSQState ias nil;
			/* last restored state (not stored) */
			RSState ias nil;
			/* quartal's resource type (configured) */
			Resource ias (
				Type ias nil;
				/* contribution to player's account */
				Quantum ias 0;
				/* period  */
				Period ias 100; // ms
				/* number */
				Quantums ias 0;
				/* transition cnp -> op; permitted time */
				OpenTime ias 1000; // ms
				/* requester - who initiated creation of quartal */
				Requester ias nil;
				/* desired price set by sherif to open information about quartal */
				DesiredOpenInfoPrice ias 100;
				Update ias (
					QConf ias nil;
					BribeR ias nil;

					(Type (QConf~ 0) Get)^
					(Quantum (QConf~ 1) Get)^
                                        (Period (QConf~ 2) Get)^
					(Quantums (QConf~ 3) Get)^
					(OpenTime (QConf~ 4) Get)^
					(DesiredOpenInfoPrice (QConf~ 5) Get)^
					(Sherif.DesiredBribe BribeR~)^
				);
				/* resource initialization point */
				Init ias (
					(Update.QConf Ew.QuartalsConf.Resources~ Random)^
					(Update.BribeR Ew.QuartalsConf.SherifBribeRanges~ Random)^
					Update~ Exe
					((Quartal Id~) Tx (Id~ check ())) Gate
#if (verbose)
					((The quartal Id~ initialized with configuration (Type~ Quantum~ Period~ Quantums~ OpenTime~ DesiredOpenInfoPrice~ Sherif.DesiredBribe~)) Ew.Output~) Do					
#endif
				);
				Link ias (
					(R Player),(R nil) ias ((Player Owner~) Tx (ModeInfo.ModeDeal~ linkquartal Id~ (Id~))) Gate;
					(R Boss) ias ((Player Owner~) Tx (ModeInfo.ModeBossOfDistrict~ linkquartal (Requester~ 1) Get (Id~))) Gate;
					(R (Requester~ 0) Get)~ Exe
					((Player Owner~) Tx (ModeInfo.ModeMain~ qinitialized Id~ (Id~))) Gate
				);
			);
			/* one sherif is associated with one quartal; the banker is aux. entity */
			Sherif ias (
				FormerIntention ias nil;
				Intention ias nil;
				IntentionParams ias nil;
				DesiredBribe ias nil;
				ProposedBribe ias nil;
				Banker ias (
					OnSuccessPayment ias nil;
					OnFailedPayment ias nil;
					Value ias 0;
					Account ias 0;
					ApplyPayment ias (
						(Account ((Account~ Value~) Ew.Sum~) Do)^
#if (verbose)
						((The sherif of quartal Id~ has following sum Banker.Account~ on account) Ew.Output~) Do
#endif
					);
					AskPayment ias (
						((Player Owner~) Tx (ModeInfo.ModeDeal~ dealpayment Owner~ (Id~ Resource.Type~ Value~))) Gate
					);
					Pay ias (
						(NoQuantums true) ias (
							((Quartal Id~) Tx (Owner~ requestdone ())) Gate
#if (verbose)
							((The banker of sherif of quartal Id~ payed to player Owner~ all requested resources) Ew.Output~) Do
#endif
#if (release)
							(((deal setintention done) (player Owner~) (quartal Id~) Behavior.State~ (pay finished)) Ew.OutChannel~) Do
#endif
						);
						(NoQuantums false) ias (
							(Resource.Quantums ((Resource.Quantums~) Ew.DecValue~) Do)^
#if (verbose)
							((The banker of sherif of quartal Id~ pays to player Owner~ resource (Id~ Resource.Type~ Resource.Quantum~) and left Resource.Quantums~ payments) Ew.Output~) Do
#endif
							/* sending contribution message to player */
							((Player Owner~) Tx (ModeInfo.ModeDeal~ contribution Owner~ (Id~ Resource.Type~ Resource.Quantum~))) Gate
							/* re-activates contribution */
							(Resource.Period~ (Pay Id~) Pay~) Recall
						);
						(NoQuantums (Resource.Quantums~ 0) Ident)~ Exe
					);
					CancelContribution ias (
						(-1 (Pay Id~) Pay~) Recall
#if (verbose)
						((The banker of sherif of quartal Id~ cancels contibution procedure) Ew.Output~) Do
#endif
					);
				);
				OpenInfo ias (
					(QInfo (Resource.Type~ Resource.Quantum~ Resource.Period~ Resource.Quantums~ Resource.OpenTime~ Resource.DesiredOpenInfoPrice~ Sherif.DesiredBribe~))^
#if (verbose)
					((The sherif of quartal Id~ opens information about quartal QInfo~) Ew.Output~) Do
#endif
#if (release)                                   
					(((deal setintention done) (player Owner~) (quartal Id~) Behavior.State~ (open QInfo~)) Ew.OutChannel~) Do
#endif
				);
				UpdateBDevelopFactor ias (
					Op ias plus; // plus or minus
					/* sending business development factor message to player */
					((Player Owner~) Tx (ModeInfo.ModeDeal~ bdfchange Owner~ (Id~ Resource.Type~ Resource.Quantum~ Op~))) Gate
				);
				Behavior ias (
					// initial state
					State ias onp;
					// template of sherif's and quartal's states
					(TQRState (Resource.Type Resource.Requester Resource.Quantum Resource.Period Resource.Quantums Resource.OpenTime Resource.DesiredOpenInfoPrice))^
					// build and restore state
					BuildQRState ias (
						S ias ();
						B ias (S (S~ ($~~)) Join)^;
						(S ())^
						(TQRState~ B~) ForEach
					);
					RestoreQRState ias (
						I ias 0;
						S ias nil;
						R ias (
							(V (TQRState~ I~) Get)^
							(V~ $~)^
							(V~ Resource true) Context
							(I ((I~) Ew.IncValue~) Do)^
						);
						(I 0)^
						(S~ K~) ForEach
					);
					// activate contribution timer
					ACTimer ias (
						// timer state
						S ias nil;
						(A true) ias doNothing;
						(A false) ias ((S~ 0) Get (Pay Id~) Banker.Pay~) Recall;
						(A (S~ nil) Ident)~ Exe
					);
					// activates open info timer
					OITimer ias (
						// timer state
						S ias nil;
						(A true) ias doNothing;
						(A false) ias ((S~ 0) Get (Cnp Id~) OnCnpTimer~) Recall;
						(A (S~ nil) Ident)~ Exe
					);
					// called when (Cnp Id~) timer expired
					OnCnpTimer ias ((Quartal Id~) Tx (Owner~ close ())) Gate;
					// restores state after investment's payment
					RStateAfterInvest ias (
						(WState true) ias (State op)^;
						(WState false) ias doNothing;
						// restore state if needed
						(WState (State~ wtp) Ident)~ Exe
					);

					SuccessPaymentForInvestment ias (
						RStateAfterInvest~ Exe
#if (verbose)
						((The investment for quartal Id~ succeeded for player Owner~) Ew.Output~) Do
#endif
						/* sending business development factor message to player */
						(UpdateBDevelopFactor.Op minus)^
						UpdateBDevelopFactor~ Exe
						// calculates new Quantum based on last investment
						((Player Owner~) Tx (ModeInfo.ModeDeal~ rqcalcquantum Owner~ (Id~ Resource.Type~ Resource.Quantum~ LInvestQuantum~))) Gate
					);

					FailedPaymentForInvestment ias (
						RStateAfterInvest~ Exe
						(LInvestQuantum 0)^
#if (verbose)
						((The investment for quartal Id~ failed for player Owner~ with result not enough resources) Ew.Output~) Do
#endif
#if (release)
						(((deal setintention failed) (player Owner~) (quartal Id~) State~ (invest notEnoughResources)) Ew.OutChannel~) Do
#endif
					);

					SuccessPaymentForOpenInfo ias (
						(State onp)^
						Banker.ApplyPayment~ Exe
						OpenInfo~ Exe
						// starts timer; when timer is finished quartal is moved to state cnp
						(Resource.OpenTime~ (Cnp Id~) OnCnpTimer~) Recall
#if (verbose)
						((The sherif of quartal Id~ provided information to player Owner~ and cnp timer activated for Resource.OpenTime~ ms) Ew.Output~) Do
#endif
					);
					FailedPaymentForOpenInfo ias (
						(State cnp)^
#if (verbose)
						((The sherif of quartal Id~ did not provide information to player Owner~ since payment was failed) Ew.Output~) Do
#endif
#if (release)
						(((deal setintention failed) (player Owner~) (quartal Id~) State~ (open notEnoughResources)) Ew.OutChannel~) Do
#endif

					);
					SuccessPaymentForBribeRequest ias (
						// stop timer which could be activated on cnp state
#if (verbose)
						((The sherif of quartal Id~ took bribe ProposedBribe~) Ew.Output~) Do
#endif
#if (release)
						(((deal setintention inProgress) (player Owner~) (quartal Id~) State~ (pay QInfo~)) Ew.OutChannel~) Do
#endif
						Banker.ApplyPayment~ Exe
						(State op)^
						// starts timer of contribution period (player's bank account will being updated during this period)
						(Resource.Period~ (Pay Id~) Banker.Pay~) Recall
						/* sending business development factor message to player */
						(UpdateBDevelopFactor.Op plus)^
						UpdateBDevelopFactor~ Exe
					);
					FailedPaymentForBribeRequest ias (
						(State onp)^
#if (verbose)
						((The sherif of quartal Id~ did not get bribe payment from player Owner~ since payment was failed) Ew.Output~) Do
#endif
#if (release)
						(((deal setintention failed) (player Owner~) (quartal Id~) State~ (pay notEnoughResources)) Ew.OutChannel~) Do
#endif
					);
					/* payment */
					(paymentresult wtp) ias (
						(paymentresult confirmed) ias Banker.OnSuccessPayment~ Exe;
						(paymentresult declined) ias Banker.OnFailedPayment~ Exe;
						(paymentresult (Sherif.IntentionParams~ 0) Get)~ Exe
					);
					(open wtp),(close wtp),(pay wtp),(requestdone wtp),(notrequestdone wtp),(invest wtp) ias (
						// remember messages which were received during payment operation
				        	((Quartal Id~) Tx (Source~ Sherif.Intention~ Sherif.IntentionParams~)) Gate
					);
					// provides information about how much should be payed in order to get information about quartal
					(open cnp) ias (
						(OpenQInfo 0),(OpenQInfo 1) ias (
							(Banker.Value ProposedPrice~)^
							(Banker.OnSuccessPayment SuccessPaymentForOpenInfo~)^
							(Banker.OnFailedPayment FailedPaymentForOpenInfo~)^
							Banker.AskPayment~ Exe
							(State wtp)^
						);
						(OpenQInfo -1) ias (
#if (verbose)
							((The sherif of quartal Id~ did not allow to open info ProposedPrice~ desired price is Resource.DesiredOpenInfoPrice~) Ew.Output~) Do
#endif
#if (release)
							(((deal setintention failed) (player Owner~) (quartal Id~) State~ (open toSmall ProposedPrice~ Resource.DesiredOpenInfoPrice~)) Ew.OutChannel~) Do
#endif
						);
						(ProposedPrice (Sherif.IntentionParams~ 0) Get)^
						// sherif's bribe
						(OpenQInfo ((ProposedPrice~ Resource.DesiredOpenInfoPrice~) Ew.Compare~) Do)~ Exe
					);
					(open onp),(open op),(open cp) ias (
#if (verbose)
						((The sherif of quartal Id~ has already opened information about quartal) Ew.Output~) Do
#endif
						OpenInfo~ Exe
					);
					(close onp) ias (
						(State onp)^
#if (verbose)
						((The sherif of quartal Id~ closed information no bribe got) Ew.Output~) Do
#endif
#if (release)
						(((deal setintention done) (player Owner~) (quartal Id~) State~ (open closed)) Ew.OutChannel~) Do
#endif
					);
					// 
					(close op) ias (
						(State cp)^
#if (verbose)
						((The sherif of quartal Id~ closed information bribe was got) Ew.Output~) Do
#endif
#if (release)
						(((deal setintention done) (player Owner~) (quartal Id~) State~ (open closed)) Ew.OutChannel~) Do
#endif
					);
					(close cnp),(close cp) ias (
#if (verbose)
						((The sherif of quartal Id~ has already closed information about quartal) Ew.Output~) Do
#endif
					);
					// player payes bribe and started getting resources and back payements from quartal
					(pay onp) ias (
						(TakeBribe 0),(TakeBribe 1) ias (
							(Banker.Value ProposedBribe~)^
							(Banker.OnSuccessPayment SuccessPaymentForBribeRequest~)^
							(Banker.OnFailedPayment FailedPaymentForBribeRequest~)^
							Banker.AskPayment~ Exe
							(State wtp)^
						);
						(TakeBribe -1) ias (
#if (verbose)
							((The sherif of quartal Id~ did not take bribe ProposedBribe~ desired bribe is DesiredBribe~) Ew.Output~) Do
#endif
#if (release)
							(((deal setintention failed) (player Owner~) (quartal Id~) State~ (pay toSmall ProposedBribe~ DesiredBribe~)) Ew.OutChannel~) Do
#endif
						);
						(ProposedBribe (Sherif.IntentionParams~ 0) Get)^
						// sherif's bribe
						(TakeBribe ((ProposedBribe~ DesiredBribe~) Ew.Compare~) Do)~ Exe
					);
					(pay cnp),(pay op),(pay cp) ias (
#if (verbose)
						((The sherif of quartal Id~ has already got bribe or qaurtal is closed and payment is impossible) Ew.Output~) Do
#endif
					);
					(requestdone cp),(requestdone op) ias (
						(State onp)^
					);
					(requestdone cnp),(requestdone onp) ias (
#if (verbose)
						((The sherif of quartal Id~ got invalid message from banker) Ew.Output~) Do
#endif
					);
					(notrequestdone op) ias (
						(State op)^
					);
					(notrequestdone cp) ias (
						(State cp)^
					);
					(notrequestdone cnp),(notrequestdone onp) ias (
#if (verbose)
						((The sherif of quartal Id~ got invalid message from banker) Ew.Output~) Do
#endif
					);
					(exit op),(exit cnp),(exit onp),(exit wtp) ias (
						(StopQuartal true)^
						(-1 (Pay Id~) Banker.Pay~) Recall
						// unblocks quartal's gate
						((Quartal Id~) Tx (Id~ check ())) Gate
#if (verbose)
						((The sherif of quartal Id~ got message to exit) Ew.Output~) Do
#endif
					);
					(check op),(check cnp),(check onp),(check wtp) ias (
#if (verbose)
						((The sherif of quartal Id~ which belongs to player Owner~ got self test message) Ew.Output~) Do
#endif
					);
					(collectstate op),(collectstate cnp),(collectstate onp),(collectstate wtp),(collectstate cp) ias (
						// quartal resource info
						BuildQRState~ Exe
						// sherif's state
						(QSherifState (FormerIntention~ State~ IntentionParams~ DesiredBribe~))^
						// state of contribution timer
						(CTimerState ((Pay Id~)) TState)^
						// open info timer state
						(OITimerState ((Cnp Id~)) TState)^
						// last stored quartal state
						(LSQState (Id~ CTimerState~ OITimerState~ BuildQRState.S~ QSherifState~))^
						// delegate to player
						((Player Owner~) Tx (ModeInfo.ModeStateManagerBroker~ rspquartalstate Id~ LSQState~)) Gate
					);
					(restorestate op),(restorestate cnp),(restorestate onp),(restorestate wtp),(restorestate cp) ias (
						(RSState IntentionParams~)^
						// restore quartal's state
						(RestoreQRState.S (RSState~ 3) Get)^
						RestoreQRState~ Exe
						// restore sherif's state
						(QSherifState (RSState~ 4) Get)^
						(FormerIntention (QSherifState~ 0) Get)^
						(Intention FormerIntention~)^
						(State (QSherifState~ 1) Get)^
						(IntentionParams (QSherifState~ 2) Get)^
						(DesiredBribe (QSherifState~ 3) Get)^
						// re-activate contribution timer, if needed
						(ACTimer.S (RSState~ 1) Get)^
						ACTimer~ Exe
						// re-activates open info timer, if needed
						(OITimer.S (RSState~ 2) Get)^
						OITimer~ Exe
						// delegate to player
						((Player Owner~) Tx (ModeInfo.ModeStateManagerBroker~ rsprquartalstate Id~ ())) Gate
					);
					(updateconf op),(updateconf cnp),(updateconf onp),(updateconf wtp),(updateconf cp) ias (
						(Resource.Update.QConf (IntentionParams~ 0) Get)^
						(Resource.Update.BribeR (IntentionParams~ 1) Get)^
						Resource.Update~ Exe
						(QInfo (Resource.Type~ Resource.Quantum~ Resource.Period~ Resource.Quantums~ Resource.OpenTime~ Resource.DesiredOpenInfoPrice~ Sherif.DesiredBribe~))^
#if (verbose)
						((The quartal Id~ updated with configuration QInfo~) Ew.Output~) Do					
#endif						
#if (release)
						(((deal setintention done) (player Owner~) (quartal Id~) State~ (updateconf QInfo~)) Ew.OutChannel~) Do
#endif
					);
					(getstate op),(getstate cnp),(getstate onp),(getstate wtp),(getstate cp) ias (
#if (verbose)
						((The quartal Id~ is in state State~ and belongs to Owner~) Ew.Output~) Do					
#endif
#if (release)
						(((deal setintention done) (player Owner~) (quartal Id~) State~ (getstate QInfo~)) Ew.OutChannel~) Do
#endif
					);
					// resource quantum has already been changed by last investment
					(rspcalcquantum op),(rspcalcquantum cnp),(rspcalcquantum onp),(rspcalcquantum wtp),(rspcalcquantum cp) ias (
						(Resource.Quantum (Sherif.IntentionParams~ 0) Get)^
						(QInfo (Resource.Type~ Resource.Quantum~ Resource.Period~ Resource.Quantums~ Resource.OpenTime~ Resource.DesiredOpenInfoPrice~ Sherif.DesiredBribe~))^
#if (verbose)
						((The quartal Id~ updated with recalculated quantum QInfo~) Ew.Output~) Do					
#endif
#if (release)
						(((deal setintention done) (player Owner~) (quartal Id~) State~ (invest QInfo~)) Ew.OutChannel~) Do
#endif
					);
					// investement allowed during contribution active phase only
					(invest op) ias (
#if (verbose)
						((The quartal Id~ belongs to Owner~ is in state State~ and investment allowed) Ew.Output~) Do					
#endif
						(LInvestQuantum (Sherif.IntentionParams~ 0) Get)^
						(Banker.Value LInvestQuantum~)^
						(Banker.OnSuccessPayment SuccessPaymentForInvestment~)^
						(Banker.OnFailedPayment FailedPaymentForInvestment~)^
						Banker.AskPayment~ Exe
						(State wtp)^
					);
					// invest isn't allowed
				        (invest cnp),(invest onp),(invest wtp),(invest cp) ias (
#if (verbose)
						((The quartal Id~ belongs to Owner~ is in state State~ and investment not allowed) Ew.Output~) Do					
#endif
						(((deal setintention failed) (player Owner~) (quartal Id~) State~ (invest QInfo~)) Ew.OutChannel~) Do
					);
					// reset
					// resource quantum has already been changed by last investment
					(reset op),(reset cnp),(reset onp),(reset cp) ias (
						(State onp)^
						Banker.CancelContribution~ Exe
						((Player Owner~) Tx (ModeInfo.ModeDeal~ resettedquartal Id~ (Id~))) Gate
					);
					(reset wtp) ias (
						// remember messages which were received during payment operation
				        	((Quartal Id~) Tx (Source~ Sherif.Intention~ Sherif.IntentionParams~)) Gate
					);
					(Intention~ State~)~ Exe
				);
			);
			CommandDispatcher ias (
				Dispatch ias nil;
				// (Id~ Intention~ IntentionParams~)
				(Source (Dispatch~ 0) Get)^
				(FormerIntention Sherif.Intention~)^
				(Sherif.Intention (Dispatch~ 1) Get)^
				(Sherif.IntentionParams (Dispatch~ 2) Get)^
				Sherif.Behavior~ Exe
			);
			QuartalLifeStep ias (
				(ready false false) ias (
					(CommandDispatcher.Dispatch nil)^
					QuartalLifeStep~ Exe
				);
				(ready true false) ias (
					(CommandDispatcher.Dispatch ((Quartal Id~) Rx) Gate)^
#if (verbose)
					((The quartal Id~ received CommandDispatcher.Dispatch~) Ew.Output~) Do
#endif
					CommandDispatcher~ Exe
					QuartalLifeStep~ Exe
				);
				(ready false true),(ready true true) ias (
#if (verbose)
					((The sherif of quartal Id~ reports about exiting) Ew.Output~) Do
#endif
				);
				(ready (((Quartal Id~) Ready) Gate true) Ident StopQuartal~)~ Exe
			);
			lifeterm = (
#if (verbose)
				((The quartal Id~ added and active for owner Owner~) Ew.Output~) Do
#endif
				((Quartal Id~) Register blocked) Gate
				Resource.Init~ Exe
				// links quartal to given player
				Resource.Link~ Exe
				QuartalLifeStep~ Exe
				((Quartal Id~) Unregister) Gate
				((Player Owner~) Tx (ModeInfo.ModeDeal~ releasedquartal Id~ (Id~))) Gate
			) Exe
		);
	);
}
