module PlayerDeal {
	MagicWorld ias (
		Player ias (
			Deal ias (
				CommandId ias nil;
				CommandParams ias nil;
				QuartalId ias nil;

				InjectQ ias (
					CB ias (
#if (release)
						(((deal injectquartal done) (player Id~)) Ew.OutChannel~) Do
#endif
					);
					PQConf ias nil;
					(Resources.Quartals.Construct.R (Player Id~))^
					(Resources.Quartals.Construct.PId Id~)^
					(Resources.Quartals.Construct.M regular)^
					(Resources.Quartals.Construct.F nil)^
					(Resources.Quartals.Construct.N 0)^
					(Resources.Quartals.Construct.CB CB~)^
					(Resources.Quartals.Construct.PQConf PQConf~)^
					Resources.Quartals.Construct~ Exe
				);
				UnlinkQ ias (
					(MyEconomicQ true) ias (
						(Resources.Quartals.Own (Resources.Quartals.Own~ (QuartalId~)) Substruct)^
						(Resources.Quartals.All (Resources.Quartals.All~ (QuartalId~)) Substruct)^
						(Resources.BattleZones.DetachZones.Z (MagicWorld)->(Quartal QuartalId~)->(Sherif)->BattleZones~)^
						Resources.BattleZones.DetachZones~ Exe
					);
					(MyEconomicQ false) ias (Resources.Quartals.Visible (Resources.Quartals.Visible~ (QuartalId~)) Substruct)^;
					((MagicWorld)->(Quartal QuartalId~)->Linked false)^
					// if quartal belongs to the player then quartal is considered as player's economic and should be considered separately
					(MyEconomicQ (Id~ (QuartalId~ 0) Get) Ident)~ Exe
#if (verbose)
					((The player Id~ in mode deal and linked quartals are Resources.Quartals.Visible~ and Resources.Quartals.Own~) Ew.Output~) Do
#endif
#if (release)
					(((deal unlinkquartal done) (player Id~) (quartal QuartalId~)) Ew.OutChannel~) Do
#endif
				);
				LinkQ ias (
					(MyEconomicQ true) ias (
						(Resources.Quartals.Own (Resources.Quartals.Own~ (QuartalId~)) Join)^
						(Resources.Quartals.NumOfActiveQuartals (Resources.Quartals.Own~) Size)^
						(Resources.BattleZones.AttachZones.Z (MagicWorld)->(Quartal QuartalId~)->(Sherif)->BattleZones~)^
						Resources.BattleZones.AttachZones~ Exe
					);
					(MyEconomicQ false) ias (Resources.Quartals.Visible (Resources.Quartals.Visible~ (QuartalId~)) Join)^;
					((MagicWorld)->(Quartal QuartalId~)->Linked true)^
					((MagicWorld)->(Quartal QuartalId~)->Owner Id~)^
					((MagicWorld)->(Quartal QuartalId~)->(Resource)->Requester (Player Id~))^
					// if quartal belongs to the player then quartal is considered as player's economic and should be considered separately
					(MyEconomicQ (Id~ (QuartalId~ 0) Get) Ident)~ Exe
#if (verbose)
					((The player Id~ in mode deal and linked quartals are Resources.Quartals.Visible~ and Resources.Quartals.Own~) Ew.Output~) Do
#endif
#if (release)
					(((deal linkquartal done) (player Id~) (quartal QuartalId~)) Ew.OutChannel~) Do
#endif
				);
				// (player 0 setintention (quartalId intention (...)))
 				(Handle setintention) ias (
					(QuartalId (CommandParams~ 0) Get)^
					(Intention (CommandParams~ 1) Get)^
					(IntentionParams (CommandParams~ 2) Get)^
#if (verbose)
					((The player Id~ in mode deal processes CommandId~ for quartal QuartalId~ intention Intention~ and params IntentionParams~) Ew.Output~) Do
#endif
					// propagate to quartal
					((Quartal QuartalId~) Tx (Id~ Intention~ IntentionParams~)) Gate
				);
				(Handle linkquartal) ias (
					(QuartalId (CommandParams~ 0) Get)^
					LinkQ~ Exe
				);
				(Handle unlinkquartal) ias (
					(QuartalId (CommandParams~ 0) Get)^
					UnlinkQ~ Exe
				);
				(Handle releasequartal) ias (
					(QuartalId (CommandParams~ 0) Get)^
					UnlinkQ~ Exe
					((Quartal QuartalId~) Tx (Id~ exit ())) Gate
				);
				(Handle resetquartal) ias (
					(QuartalId (CommandParams~ 0) Get)^
					((Quartal QuartalId~) Tx (Id~ reset ())) Gate
				);
				(Handle releasedquartal),(Handle resettedquartal) ias (
					(Resources.ActiveQuartals ((Resources.ActiveQuartals~) Ew.DecValue~) Do)^
#if (verbose)
					((The player Id~ left quartals Resources.ActiveQuartals~) Ew.Output~) Do
#endif
				);
				(Handle quartalreport) ias (
					Report ias (
#if (verbose)
						((The player Id~ in mode deal and linked quartal $~) Ew.Output~) Do
#endif
#if (release)
						(((deal quartalreport notSupported) (player Id~) (quartal $~)) Ew.OutChannel~) Do
#endif
					);
					(Resources.Quartals.Own~ Report~) ForEach
				);
				(Handle dealpayment) ias (
					(Result true) ias ((Quartal QuartalId~) Tx (Id~ paymentresult (confirmed))) Gate;
					(Result false) ias ((Quartal QuartalId~) Tx (Id~ paymentresult (declined (prc Resources.Account.Withdraw.PRC~)))) Gate;
					(QuartalId (CommandParams~ 0) Get)^
					(Resources.Account.Value CommandParams~)^
					Resources.Account.Withdraw~ Exe
					(Result Resources.Account.Result~)~ Exe
				);
				(Handle payforunit) ias (
					RespCmd ias nil;
					OrigRequest ias nil;
					(Result true) ias ((Player Id~) Tx (ModeInfo.ModeHiring~ RespCmd~ Id~ (confirmed OrigRequest~))) Gate;
					(Result false) ias ((Player Id~) Tx (ModeInfo.ModeHiring~ RespCmd~ Id~ (declined OrigRequest~ (prc Resources.Account.Withdraw.PRC~)))) Gate;
					(OrigRequest (CommandParams~ 0) Get)^
					(RespCmd (CommandParams~ 3) Get)^
					(Resources.Account.Value CommandParams~)^
					Resources.Account.Withdraw~ Exe
					(Result Resources.Account.Result~)~ Exe
				);
				(Handle contribution),(Handle returnpayment) ias (
					(Resources.Account.Value CommandParams~)^
					Resources.Account.Recharge~ Exe
				);
				(Handle bdfchange) ias (
					(Op plus) ias Resources.Business.IncreaseBF~ Exe;
					(Op minus) ias Resources.Business.DecreaseBF~ Exe;
					(Resources.Business.Value CommandParams~)^
					// update value of business development factor (Bdf)
					(QuartalId (CommandParams~ 0) Get)^
					(Op (CommandParams~ 3) Get)~ Exe
				);
				(Handle rqcalcquantum) ias (
					(QuartalId (CommandParams~ 0) Get)^
					// recalculate quantum basing on last investment
					(Resources.Business.RQ 0)^
					(Resources.Business.Value CommandParams~)^
					Resources.Business.RecalcRQuantum~ Exe
					// updates business factor by recalculated quantum
					(Resources.Business.Value (QuartalId~ (CommandParams~ 1) Get Resources.Business.RQ~))^
					Resources.Business.IncreaseBF~ Exe
					// recalc player's instant rating
					((Player Id~) Tx (ModeInfo.ModeMain~ rqrating 0 ())) Gate
					// sends quantum to quartal for updaiting contribution value
					((Quartal QuartalId~) Tx (QuartalId~ rspcalcquantum (Resources.Business.RQ~))) Gate
				);
				(Handle updatebzones) ias (
					(Resources.BattleZones.DetachZones.Z (CommandParams~ 0) Get)^
					Resources.BattleZones.DetachZones~ Exe
					(Resources.BattleZones.AttachZones.Z (CommandParams~ 1) Get)^
					Resources.BattleZones.AttachZones~ Exe
				);
				(Handle back) ias (
					(Player.Mode main)^
#if (verbose)
					((The player Id~ returned to main mode) Ew.Output~) Do
#endif
				);
				(Handle injectquartal) ias (
					(InjectQ.PQConf (CommandParams~ 0) Get)^
					InjectQ~ Exe
				);
				(Handle CommandId~)~ Exe
			);
		);
	);
}