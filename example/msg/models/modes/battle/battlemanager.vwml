module BattleManager {
	MagicWorld ias (
		Player ias (
			BattleManager ias (
				CommandId ias nil;
				To ias nil;
				CommandParams ias nil;
				BFId ias nil;
#if (realbattleimpl)
				// battlefield is created upon player's request
				CreateBattleField ias (
					(Q (CommandParams~ 0) Get)^
					(BattleField (BattleField (Id~ Q~))) Born
					((MagicWorld)->(BattleField (Id~ Q~))->Id (Id~ Q~))^
					((MagicWorld)->(BattleField (Id~ Q~))->PlayerId Id~)^
					((MagicWorld)->(BattleField (Id~ Q~))->Quartal Q~)^
					(MagicWorld)->(BattleField (Id~ Q~))->Init~ Exe
					(Resources.Battles (Resources.Battles~ ((MagicWorld)->(BattleField (Id~ Q~))->Id~)) Join)^
					// default: here defence will be built
					(Resources.SelectedBattle (MagicWorld)->(BattleField (Id~ Q~))->Id~)^
#if (verbose)
					((Player Id~ created battlefield (BattleField (MagicWorld)->(BattleField (Id~ Q~))->Id~)) Ew.Output~) Do 
#endif
				);
				// battlefield is destroyed upon player's request
				DestroyBattleField ias (
					(BFId (Id~ (CommandParams~ 0) Get))^
					(Resources.Battles (Resources.Battles~ (BattleField BFId~)) Substruct)^
#if (verbose)
					((Player Id~ is going to destroy battlefield (BattleField BFId~)) Ew.Output~) Do 
#endif
					((Player Id~) Tx (ModeInfo.ModeBattle~ exit Id~ ())) Gate
				);
				ResetBattleField ias (
					(BFId (Id~ (CommandParams~ 0) Get))^
#if (verbose)
					((Player Id~ is going to reset battlefield (BattleField BFId~)) Ew.Output~) Do 
#endif
					((Player Id~) Tx (ModeInfo.ModeBattle~ reset Id~ ())) Gate
				);
				// Selects battle (becames active from player's point of view)
				SelectBattleField ias (
					(Allow false) ias (
#if (verbose)
						((Player Id~ selected invalid battlefield id BFId~) Ew.Output~) Do
#endif
					);
					(Allow true) ias (Resources.SelectedBattle BFId)^;
					(BFId (Id~ (CommandParams~ 0) Get))^
					(Allow (Resources.Battles~ BFId~) In)~ Exe
				);
				// requested battle status is returned to requester
				GetBattleStatus ias (
					// player
					(RPId (CommandParams~ 0) Get)^
					// its mode where response is sent
					(RPIdMode (CommandParams~ 1) Get)^
					// sent to requested with player's identifier and current battle status
					((Player RPId~) Tx (RPIdMode~ responsebattlestatusbf RPId~ (Id~ (MagicWorld)->(BattleField Resources.SelectedBattle~)->BattleStatus~))) Gate
				);
#else
				CreateBattleField ias (
#if (verbose)
					((Player Id~ created fake battlefield) Ew.Output~) Do 
#endif
				);
				DestroyBattleField ias (
					((Player PlayerId~) Tx (ModeInfo.ModeMain~ partclosed Id~ (battlefield))) Gate
				);
				ResetBattleField ias (
					((Player PlayerId~) Tx (ModeInfo.ModeMain~ partclosed Id~ (battlefield))) Gate
				);
#endif // realbattleimpl
				PrepareAttackZones ias (
					AttackZonesReady ias false;
					// in case if battle wasn't started in 'StartIn' time we have to return all players to deflist
					ClearBZones ias (
						IgnorePlayer ias nil;
#if (release)
	#if (zonebinderfringe)
						// old code((Id~ IgnorePlayer~) Ew.ResetPrepareToAttack~) Do
						(((battle resetpreparetoattack ok)(player Id~)(IgnorePlayer~)) Ew.ResetPrepareToAttack~) Do						
	#endif
#endif
#if (verbose)
						((Player Id~ resets attacked zones and ignored player is IgnorePlayer~) Ew.Output~) Do
#endif
					);
					// associates attack zones
					AssociateBZones ias (
						// start timer which waits for 'StartIn~' time for attack message
#if (verbose)                                   	
						((Player Id~ zones will be cleared in Ew.MatchMakerConf.Attack.StartIn~ ms) Ew.Output~) Do
#endif
						(Ew.MatchMakerConf.Attack.StartIn~ (ClearBZones Id~) ClearBZones~) Recall
					);
					(AttackZonesReady false) ias doNothing;
					(AttackZonesReady true) ias AssociateBZones~ Exe;

#if (verbose)
					(AttackCost (CommandParams~ 0) Get)^
					((Player Id~ is being prepared for attack with attack cost AttackCost~) Ew.Output~) Do						
#endif
#if (release)
	#if (zonebinderfringe)
					// (CommandParams~ 1) Get => AttackCost
				// old code	(AttackZonesReady (((Id~ (CommandParams~ 0) Get Resources.BattleZones.Zones~ (Resources.BattleZones.Zones~) Size Ew.QuartalsConf.BNZ~)) Ew.PrepareToAttack~) Do)^
					(T (CommandParams~ 0) Get)^
					(((battle zoneslist ok)(player Id~)(T~ Resources.BattleZones.Zones~ (Resources.BattleZones.Zones~) Size Ew.QuartalsConf.BNZ~)) Ew.PrepareToAttack~) Do
					(AttackZonesReady true)^
	#else
					(AttackZonesReady true)^
	#endif
#endif
#if (verbose)
					((Player Id~ prepared attacking zones AttackZones~) Ew.Output~) Do						
#endif
					(AttackZonesReady AttackZonesReady~)~ Exe
#if (release)
					(((battle prepareattackzonesbf ok) (player Id~)) Ew.OutChannel~) Do
#endif
				);

				AttackZonesStarted ias (
					(-1 (ClearBZones Id~) PrepareAttackZones.ClearBZones~) Recall
					(PrepareAttackZones.ClearBZones.IgnorePlayer (CommandParams~ 0) Get)^
					PrepareAttackZones.ClearBZones~ Exe
#if (verbose)
					((Player Id~ cancelled battle timer) Ew.Output~) Do						
#endif
#if (release)
					(((battle attackstarted ok) (player Id~)) Ew.OutChannel~) Do
#endif
				);

				(DelegateToBF false) ias (
					(Handle createbf) ias CreateBattleField~ Exe;
					(Handle destroybf) ias DestroyBattleField~ Exe;
					(Handle resetbf) ias ResetBattleField~ Exe;
					// selects battlefield for defence building
					(Handle selectbf) ias SelectBattleField~ Exe;
					(Handle requestbattlestatusbf) ias GetBattleStatus~ Exe;
					(Handle prepareattackzonesbf) ias PrepareAttackZones~ Exe;
					(Handle attackzonesbf) ias AttackZonesStarted~ Exe;
					(Handle CommandId~)~ Exe
				);
				(DelegateToBF true) ias (
					// delegating command to BF
					(NotSelectedBF false) ias (
						((MagicWorld)->(BattleField Resources.SelectedBattle~)->CommandId CommandId~)^
						((MagicWorld)->(BattleField Resources.SelectedBattle~)->Destination To~)^
						((MagicWorld)->(BattleField Resources.SelectedBattle~)->CommandParams CommandParams~)^
#if (verbose)
						((Player Id~ battlemanager delegates command CommandId~ to battlefield (BattleField Resources.SelectedBattle~)) Ew.Output~) Do
#endif
						(BattleField Resources.SelectedBattle~)~ Exe
					);
					(NotSelectedBF true) ias (
#if (verbose)
						((Player Id~ must select battlefield before) Ew.Output~) Do
#endif
					);
					(NotSelectedBF (Resources.SelectedBattle~ nil) Ident)~ Exe
				);				
				(DelegateToBF (BattleField.ExportedCommands~ CommandId~) In)~ Exe
			);
		);
	);
}