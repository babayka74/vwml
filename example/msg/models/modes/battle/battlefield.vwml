module BattleField {
	MagicWorld ias (
		BattleField ias (
			Modes ias (none builddefence attack);
			// BattleField's id (may coincide with Quartal)
			Id ias nil;
			// belongs to player identified by id
			PlayerId ias nil;
			// who attacks player identified by PlayerId
			PlayerAttackerId ias nil;
			// to which quartal battlefield belongs
			Quartal ias nil;
			BattleStatus ias none;
			// command to be parsed
			CommandId ias nil;
			Destination ias nil;
			CommandParams ias nil;
			// default mode on start
			Mode ias none;
			// battle cost
			BattleCost ias 0;
			// global banker's id (for now we have only one banker with id 0)
			GBankerId ias 0;
			// exported commands, filled by ExportCommands term
			ExportedCommands ias ();
			ExportCommands ias (
				(ExportedCommands ())^
				Fill ias (
					F ias (ExportedCommands (ExportedCommands~ ($~)) Join)^;
					($~~ F~) ForEach
				);
				((BuildDefenceModeAvailableCommands AttackModeAvailableCommands NoneModeAvailableCommands) Fill~) ForEach
				(ExportedCommands (ExportedCommands~) Squeeze)^
#if (verbose)
				((The battlefields exported commands are ExportedCommands~) Ew.Output~) Do
#endif
			);
			// battlefield's zones
			// 1. Attack - player which attacks sets his units to 'Nodes'. 'Nodes' must be subgraph of 'Main' graph
			// 2. Defence - player which defends quartal sets his units to 'Nodes'. 'Nodes' must be subgraph of 'Main' graph
			Fields ias (
				Attack ias (
					/* nodes marked as 'attacking' */
					Nodes ias ();
				);
				Defence ias (
					/* nodes marked as 'defence' */
					Nodes ias ();
				);
				Main ias (
					/* all battlefields'/mainfield's nodes */
					Nodes ias ();
				);
				Build ias (
					LockOnBattleFieldBuilding [
						(Ew.BattleField.AttackZone Fields.Attack.Nodes)^
						(Ew.BattleField.AttackZoneCtx Fields.Attack.Nodes)^
						(Ew.BattleField.DefenceZone Fields.Defence.Nodes)^
						(Ew.BattleField.DefenceZoneCtx Fields.Defence.Nodes)^
						(Ew.BattleField.MainZone Fields.Main.Nodes)^
						(Ew.BattleField.MainZoneCtx Fields.Main.Nodes)^
						Ew.BattleField.Build~ Exe
					LockOnBattleFieldBuilding ]
				);
			);
			// commands available on 'builddefence', 'attack' and 'none' modes
			BuildDefenceModeAvailableCommands ias (buildfield linktoquartal unlinkfromquartal apply reset startpos setprops dismissunit recruitunit recruitedunit rejectedunit battlecost fullback surrender exit);
			AttackModeAvailableCommands ias (attack setupattackers startpos setprops movetopos go stop select getpos kill battlestatus unitkilled battlecost surrender fullback exit);
			NoneModeAvailableCommands ias (setmode exit);
			// battle's participants
			Participants ias (
				NumOfAttackers ias 0;
				NumOfDefenders ias 0;
				Units ias ();
				Init ias (
					(NumOfAttackers 0)^
					(NumOfDefenders 0)^
					(Units ())^
				);
			);
			Init ias (
				(BattleStatus none)^
				(Mode none)^
				(PlayerAttackerId nil)^
				Participants.Init~ Exe
#if (verbose)
				((The battlefield for player PlayerId~ linked to quartal Quartal~ has id Id~ battle status BattleStatus~) Ew.Output~) Do
#endif
			);
			Done ias (
				ReturnAllRemainedUnits~ Exe
				NotifyAboutReturnedUnits~ Exe
				((Player PlayerId~)->(Resources)->SelectedBattle nil)^
				((BattleField Id~)) Release
			);
			SetModeAndBattleStatus ias (
				(RMode false) ias doNothing;
				(RMode true) ias (
					(Mode Destination~)^
					(BattleStatus Mode~)^
#if (release)
					(((battle setmode ok Mode~) (player PlayerId~)) Ew.OutChannel~) Do
#endif
#if (verbose)
					((The battlefield Id~ of player PlayerId~ changed mode to Mode~) Ew.Output~) Do
#endif
				);
				(RMode (Modes~ Destination~) In)~ Exe
			);
			// Unit is added to battlefield
			AddUnitToBattleField ias (
				(added attacker) ias (Participants.NumOfAttackers ((Participants.NumOfAttackers~) Ew.IncValue~) Do)^;
				(added defender) ias (Participants.NumOfDefenders ((Participants.NumOfDefenders~) Ew.IncValue~) Do)^;
				((Characters)->(Unit Destination~)->(AssociateWithBattleField)->BFId Id~)^
				(Characters)->(Unit Destination~)->AssociateWithBattleField~ Exe
				(Participants.Units (Participants.Units~ (Destination~)) Join)^
				(added (Characters)->(Unit Destination~)->Type~)~ Exe
#if (verbose)
				((The battlefield Id~ has attackers Participants.NumOfAttackers~ and defenders Participants.NumOfDefenders~) Ew.Output~) Do
#endif
			);
			// unit is removed from battlefield when mode 'builddefence' is active
			RemoveFromBattleField ias (
				U ias nil;
				PId ias nil;
				RetAs ias nil;
				(CorrectUnit false) ias (
#if (verbose)
					((The unit U~ was not recruited to battlefield Id~ of player PId~) Ew.Output~) Do
#endif
				);
				(CorrectUnit true) ias (
					(removed attacker) ias (Participants.NumOfAttackers ((Participants.NumOfAttackers~) Ew.DecValue~) Do)^;
					(removed defender) ias (Participants.NumOfDefenders ((Participants.NumOfDefenders~) Ew.DecValue~) Do)^;
					((Characters)->(Unit U~)->(AssociateWithBattleField)->BFId nil)^
					(Characters)->(Unit U~)->AssociateWithBattleField~ Exe
					(removed (Characters)->(Unit U~)->Type~)~ Exe
#if (verbose)
					((Unit U~ of player PId~ returned to resource manager as RetAs~) Ew.Output~) Do
#endi
					((Player PId~) Tx (ModeInfo.ModeResourceMgr~ RetAs~ PId~ (U~))) Gate
#if (verbose)
					((The battlefield Id~ has attackers Participants.NumOfAttackers~ and defenders Participants.NumOfDefenders~) Ew.Output~) Do
#endif
				);
				(CorrectUnit (Participants.Units~ U~) In)~ Exe
			);
			// Add set of units (attackers in our case)
			AddSetOfUnitsToBattleField ias (
				AddUnit ias (
					(Destination $~)^
					AddUnitToBattleField~ Exe
				);
				(CommandParams~ AddUnit~) ForEach
#if (release)
				(((attack setupattackers ok) CommandParams~ (player PlayerId~)) Ew.OutChannel~) Do
#endif
			);
			// checks battle's status
			CheckBattleStatus ias (
				(AttackersDestroyed true) ias (
					(BattleStatus stopped)^
					((Player PlayerId~) Tx (ModeInfo.ModeBattle~ battlestatus Id~ (won defender))) Gate
					((Player PlayerAttackerId~) Tx (ModeInfo.ModeAttack~ battlestatus Id~ (loose attacker PlayerId~ BattleCost~))) Gate
				);
				(AttackersDestroyed false) ias doNothing;
				(DefendersDestroyed true) ias (
					(BattleStatus stopped)^
					((Player PlayerId~) Tx (ModeInfo.ModeBattle~ battlestatus Id~ (loose defender))) Gate
					((Player PlayerAttackerId~) Tx (ModeInfo.ModeAttack~ battlestatus Id~ (won attacker PlayerId~ BattleCost~))) Gate
				);
				(DefendersDestroyed false) ias doNothing;
				(AttackersDestroyed (Participants.NumOfAttackers~ 0) Ident)~ Exe
				(DefendersDestroyed (Participants.NumOfDefenders~ 0) Ident)~ Exe
			);
			// sends command to concrete unit
			PropagateCommandToUnit ias (
				(BattleStarted true) ias ((Characters)->(Unit Destination~) Tx (Quartal~ CommandId~ Destination~ CommandParams~)) Gate;
				(BattleStarted false) ias (
#if (verbose)
					((The command can not be propagated to unit Destination~ player PlayerId~ since battle was not started) Ew.Output~) Do
#endif
				);
				(BattleStarted (BattleStatus~ attaked) Ident)~ Exe
			);
			// all units which belong to given user are returned to player's pool
			ReturnUnit ias (
				U ias nil;
				PId ias nil;
				(PId (Characters)->(Unit U~)->PlayerId~)^
				(RemoveFromBattleField.U U~)^
				(RemoveFromBattleField.PId PId~)^
				(RemoveFromBattleField.RetAs returnunit)^
				RemoveFromBattleField~ Exe
#if (verbose)
				((The unit U~ removed from battlefield Id~ to player PId~ from player PlayerId~ units Participants.Units~) Ew.Output~) Do
#endif
			);
			ReturnAllRemainedUnits ias (
				ReturnUnits ias (
					(ReturnUnit.U $~)^
					ReturnUnit~ Exe
				);
				(Participants.Units~ ReturnUnits~) ForEach
				(Participants.Units ())^
				(Participants.NumOfDefenders 0)^
				(Participants.NumOfAttackers 0)^
			);
			// notifies player about returning all units
			NotifyAboutReturnedUnits ias ((Player PlayerId~) Tx (ModeInfo.ModeMain~ partclosed Id~ (battlefield))) Gate;
			// battlefield's cost is passed to player who won battle
			SetBattleCost ias (
				(BattleCost (CommandParams~ 0) Get)^
#if (verbose)
				((The battlefield Id~ of player PlayerId~ has cost BattleCost~) Ew.Output~) Do
#endif
			);
			// initial mode
			(Mode none) ias (
				(RightCommand false) ias doNothing;
				(RightCommand true) ias (
					// setting mode is allowed for all modes
					(Handle setmode) ias SetModeAndBattleStatus~ Exe;
					(Handle exit) ias Done~ Exe;
					(Handle CommandId~)~ Exe
				);
				(RightCommand (NoneModeAvailableCommands~ CommandId~) In)~ Exe
			);
			// building defence
			(Mode builddefence) ias (
				(RightCommand false) ias doNothing;
				(RightCommand true) ias (
					// builds battlefield's map
					(Handle buildfield) ias (
#if (verbose)
						((The battlefield for player PlayerId~ is being built) Ew.Output~) Do
#endif
						Fields.Build~ Exe
					);
					// links battlefield with quartal
					(Handle linktoquartal) ias (Quartal Destination~)^;
					// unlinks battlefield from the quartal and sets mode to 'none'
					(Handle unlinkfromquartal) ias (
						(Quartal nil)^
						(Mode none)^
					);
					// battlefield is published to all players and marked as ready for attack by other players
					(Handle apply) ias (
						(NotReady true) ias (
#if (verbose)
							((The battlefield Id~ must recruit units before) Ew.Output~) Do
#endif
#if (release)
							(((battle apply failed noUnits) (player PlayerId~)) Ew.OutChannel~) Do
#endif
						);
						(NotReady false) ias (
							(BattleStatus readyforattack)^
							(Mode none)^
#if (verbose)
							((The battlefield Id~ for player PlayerId~ is ready for attack) Ew.Output~) Do
#endif
#if (release)
							(((battle apply ok) (player PlayerId~)) Ew.OutChannel~) Do
#endif
						);
						(NotReady (Participants.Units~ ()) Ident)~ Exe
					);
					// battlefield is became invisible for players and can't be attacked by other players
					(Handle reset),(Handle fullback),(Handle surrender) ias (
						ReturnAllRemainedUnits~ Exe
						(BattleStatus none)^
						(Mode none)^
#if (verbose)
						((The battlefield Id~ for player PlayerId~ was resetted to initial state) Ew.Output~) Do
#endif
#if (release)
						(((battle CommandId~ ok) (player PlayerId~)) Ew.OutChannel~) Do
#endif
					);
					// player requests unit from resource manager to participate in battle, for defence.
					// the unit is identified by Destination~
					(Handle recruitunit) ias (
						((Player PlayerId~) Tx (ModeInfo.ModeResourceMgr~ takeunit PlayerId~ (Destination~ defender battle))) Gate
					);
					// positive response on request unit
					(Handle recruitedunit) ias (
						AddUnitToBattleField~ Exe
#if (verbose)
						((The unit Destination~ recruited to battlefield Id~ player PlayerId~ units Participants.Units~) Ew.Output~) Do
#endif
#if (release)
						(((battle recruitunit ok defence) (player PlayerId~) (unit Destination~)) Ew.OutChannel~) Do
#endif
					);
					// negative response on requestunit
					(Handle rejectedunit) ias (
#if (verbose)
						((The unit Destination~ can not be recruited to battlefield Id~ player PlayerId~) Ew.Output~) Do
#endif
#if (release)
						(((battle recruitunit failed (notHired defence)) (player PlayerId~) (unit Destination~)) Ew.OutChannel~) Do
#endif
					);
					// returned - returned to player's resources
					(Handle dismissunit) ias (
						(ReturnUnit.U Destination~)^
						ReturnUnit~ Exe	
						(Participants.Units (Participants.Units~ (Destination~)) Substruct)^
#if (release)
						(((battle dismissunit ok defence) (player PlayerId~) (unit Destination~)) Ew.OutChannel~) Do						
#endif
					);
					(Handle battlecost) ias SetBattleCost~ Exe;
					// setups units on battlefield's map (attackers and defenders)
					(Handle startpos),(Handle setprops) ias PropagateCommandToUnit~ Exe;
					(Handle exit) ias Done~ Exe;
					(Handle CommandId~)~ Exe
				);
				(RightCommand (BuildDefenceModeAvailableCommands~ CommandId~) In)~ Exe
			);
			// attack
			(Mode attack) ias (
				(RightCommand false) ias doNothing;
				(RightCommand true) ias (
					(Handle movetopos),(Handle go),(Handle stop),(Handle select),(Handle setprops),(Handle getpos),(Handle kill) ias PropagateCommandToUnit~ Exe;
					(Handle setupattackers) ias AddSetOfUnitsToBattleField~ Exe;
					(Handle battlecost) ias SetBattleCost~ Exe;
					(Handle attack) ias (
						(BattleStatus attacked)^
#if (verbose)
						((The battle between players PlayerId~ and PlayerAttackerId~ started) Ew.Output~) Do
#endif
#if (release)
						(((attack prepareinvasion ok) (player PlayerId~) (player PlayerAttackerId~)) Ew.OutChannel~) Do
#endif
					);
					(Handle unitkilled) ias (
						// unit is removed from player's resources
						(PId (Characters)->(Unit Destination~)->PlayerId~)^
						(RemoveFromBattleField.U Destination~)^
						(RemoveFromBattleField.PId PId~)^
						(RemoveFromBattleField.RetAs releaseunit)^
						RemoveFromBattleField~ Exe
						(Participants.Units (Participants.Units~ (Destination~)) Substruct)^
						CheckBattleStatus~ Exe
					);
					// sent from CheckBattleStatus when either attackers or defenders
					(Handle battlestatus) ias (
						(status won) ias (
							// perfroms resource's transfer
							((GlobalBanker GBankerId~) Tx (transfer starttransfer Id~ (PlayerAttackerId~ PlayerId~ BattleCost~))) Gate
							ReturnAllRemainedUnits~ Exe
							(Mode none)^
#if (release)
							(((battle attack ok finished) (player PlayerAttackerId~) (player PlayerId~)) Ew.OutChannel~) Do
#endif
						);
						(status loose) ias (
							// perfroms resource's transfer
							((GlobalBanker GBankerId~) Tx (transfer starttransfer Id~ (PlayerId~ PlayerAttackerId~ BattleCost~))) Gate
							ReturnAllRemainedUnits~ Exe
							(Mode none)^
#if (release)
							(((battle attack ok finished) (player PlayerId~) (player PlayerAttackerId~)) Ew.OutChannel~) Do
#endif
						);
						// status is sent by implementation of concrete battlefield
						(BattleStatus (CommandParams~ 0) Get)^
						(status BattleStatus~)~ Exe
						// performs resource transfer from loose side to winner side (PlayerId won)
#if (verbose)
						((The battle between players PlayerId~ and PlayerAttackerId~ finished with status BattleStatus~) Ew.Output~) Do
#endif
						(BattleStatus finished)^
					);
					// all units are returned to player's resource pool (attacker lost battle)
					(Handle surrender),(Handle fullback) ias (
						(Mode none)^
						(Participants.NumOfAttackers 0)^
						CheckBattleStatus~ Exe
#if (release)
						(((battle surrender ok) (player PlayerAttackerId~) (player PlayerId~)) Ew.OutChannel~) Do
#endif
					);
					(Handle exit) ias Done~ Exe;
					(Handle CommandId~)~ Exe
				);
				(RightCommand (AttackModeAvailableCommands~ CommandId~) In)~ Exe
			);
			(Mode Mode~)~ Exe
		);
	);
}