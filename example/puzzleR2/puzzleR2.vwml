options {
	language=__java__ {
  		package = "com.win.game.puzzle.model.vwml.puzzleR2"
  		path = "c:\var\vwml/puzzleR2/src"
  		author = "Groozman M.Z."
		project_name = "puzzleR2Proj"
  		description = "Antagonistic puzzle game"
		beyond {
			fringe communication ias (
				out ias "com.win.game.puzzleR2.model.fringe.gate.async.console.AsyncConsole"
				in ias "com.win.game.puzzleR2.model.fringe.gate.async.console.AsyncConsole"
			)
			fringe services ias (
				math ias "com.vw.lang.beyond.java.fringe.gate.math.Math"
			)
		}
		conflictring {
		}
	}
}

module puzzleR2 {
	/*
		EW represents external world data which can be loaded during game's 
		initialization phase
	*/
	Ew ias (
		FreePlaces ias (
			p1 p2 p3
			p4 p5 p6
			p7 p8 p9
		);
		RestPuzzles ias (
			(c1 counter1) (c2 counter2) (c3 counter3)
			(c4 counter4) (c5 counter5) (c6 counter6)
			(c7 counter7) (c8 counter8) (c9 counter9)
		);
		Correspondence ias (
			(p1 c1)(p2 c2)(p3 c3)
			(p4 c4)(p5 c5)(p6 c6)
			(p7 c7)(p8 c8)(p9 c9)
		);
		Input ias communication.in.read;
		Output ias communication.in.write;
		IncNumberOfPlayers ias services.math.inc;
		DecNumberOfPlayers ias services.math.dec;
		PlayersOnAuction ias 3;
		NoPlayers ias 0;
	);

	CroupierAssistant ias (
		/* available commands which can be received from external world  */
		PlayerCommands ias (leave join bet exit);
		/* read command */
		Command ias nil;
		CommandId ias nil;
		PlayerId ias nil;

		/* check if player made bet */
		FindPlayersBet ias (
			HasPlayerAlreadyMadeBet ias false;
			PlayerBet ias nil;
			CheckIfPlayerHasAlreadyMadeBet ias (
				B ias $;
				(PlayerMadeBet false) ias doNothing;
				(PlayerMadeBet true) ias (
					(HasPlayerAlreadyMadeBet true)^
					(PlayerBet B~~)^ 
					/* signals to 'ForEach' to stop iteration */
					($ doNothing)^
				);
				(PlayerMadeBet ((B~~)First PlayerId~) Ident)~ Exe
			);
			(Croupier.LastPlayersBets~ CheckIfPlayerHasAlreadyMadeBet~) ForEach
		);		

		CroupierAssistantLifeStep ias (
			/* Auction should be started */
			(StartAuction true false) ias (
				/* initializing puzzle */
				InitializePuzzleCounter ias (
					(($~)Last Auction.ActivePlayers~)^
				);
				(Ew.RestPuzzles~ InitializePuzzleCounter~) ForEach
				(Croupier.AuctionStarted true)^
				((Croupier starts auction) Ew.Output~) Do
			);
			/* do not do anything */
			(StartAuction true true) ias doNothing;
			(StartAuction false false) ias doNothing;
			/* Auction should be stopped */
			(StopAuction true true) ias (
				(Croupier.AuctionStarted false)^
				((Croupier stops auction) Ew.Output~) Do
			);
			/* do not do anything */
			(StopAuction false false) ias doNothing;
			(StopAuction false true) ias doNothing;	
			(StopAuction true false) ias doNothing;
			/* Assistant asks to stop auction immidiately */
			ForceToStopAuction ias (
				NotifyPlayersAuctionIsClosed ias (
					/* $ is synthetic entity which is interpreted as iterated component of complex entity on operation ForEach */
					Id ias $;
					/* notifies player identified by complex entity '(Player Id~~) */
					((Croupier closes auction and tells to player Id~~ to leave auction) Ew.Output~) Do
				);
				(Auction.Players~ NotifyPlayersAuctionIsClosed~) ForEach
				(Croupier.AuctionStarted false)^
				(Croupier.ForceToCloseAuction true)^ 
			);
			(AllPlayersLeft false) ias (
				/* player exist, try again */
				(PlayerExist false) ias (
					/* notify about invalid or unknown player */
					((Assistant reports that player PlayerId~ is unknown) Ew.Output~) Do
					/* waiting for correct player id */
					CroupierAssistantLifeStep~ Exe
				);
				/* player leaves */
				(PlayerExist true) ias (
					/* lookups for player which left and removes its bet, if exist */
					/* notifies rest players about left player */
					NotifyPlayersLeftPlayer ias (
						/* $ is synthetic entity which is interpreted as iterated component of complex entity on operation ForEach */
						Id ias $;
						/* notifies player identified by complex entity '(Player Id~~) */
						((Assistant notifies player Id~~ about left player PlayerId~) Ew.Output~) Do
					);
					((Assistant removes player PlayerId~ from auction) Ew.Output~) Do
					LeavePlayer [
						/* increments number of players */
						(Auction.ActivePlayers ((Auction.ActivePlayers~) Ew.DecNumberOfPlayers~) Do)^
						/* removes player from players group */
						(Auction.Players (Auction.Players~ (PlayerId~)) Substruct)^
						/* notifies all players in 'real world' about left player */
						(Auction.Players~ NotifyPlayersLeftPlayer~) ForEach
					LeavePlayer ]
					/* removes user's bet */
					RemoveBetWhenPlayerLeft [
						FindPlayersBet~ Exe
						(Croupier.LastPlayersBets (Croupier.LastPlayersBets~ (PlayerId~ FindPlayersBet.PlayerBet~))Substruct)^
					RemoveBetWhenPlayerLeft ]
					/* notify Croupier; may be auction should be started */
					(StopAuction (Auction.ActivePlayers~ Ew.NoPlayers~)Ident Croupier.AuctionStarted~)~ Exe
					/* next assistant's step */
					CroupierAssistantLifeStep~ Exe
				);
				/* checks if player with such id has already been registered */
				(PlayerExist (Auction.Players~ PlayerId~) In)~ Exe
			);
			/* here are no any player 'on board' */
			(AllPlayersLeft true) ias (
				((Assistant reports that all Ew.PlayersOnAuction~ players have left) Ew.Output~) Do
				CroupierAssistantLifeStep~ Exe
			);
			/* waiting for required number of players */
			(AllPlayersJoined false) ias (
				/* player exist, try again */
				(PlayerExist true) ias (
					/* notify about duplicated id */
					((Assistant reports that player PlayerId~ has already been registered) Ew.Output~) Do
				);
				/* new player  */
				(PlayerExist false) ias (
					NotifyPlayersJoinPlayer ias (
						/* $ is synthetic entity which is interpreted as iterated component of complex entity on operation ForEach */
						Id ias $;
						/* notifies player identified by complex entity '(Player Id~~) */
						((Assistant notifies player Id~~ about joined player PlayerId~) Ew.Output~) Do
					);
					((Assistant joins player PlayerId~ to auction) Ew.Output~) Do
					JoinPlayer [
						/* increments number of players */
						(Auction.ActivePlayers ((Auction.ActivePlayers~) Ew.IncNumberOfPlayers~) Do)^
						/* notifies all players in 'real world' about new player */
						(Auction.Players~ NotifyPlayersJoinPlayer~) ForEach
						/* player joined to players group; now it should be cloned; cloned Player is run automaticaly */
					        (Player (Player PlayerId~)) Clone
						/* join player to players group */
						(Auction.Players (Auction.Players~ PlayerId~) Join)^
					JoinPlayer ]
					/* notify Croupier; may be auction should be started */
					(StartAuction (Auction.ActivePlayers~ Ew.PlayersOnAuction~)Ident Croupier.AuctionStarted~)~ Exe
				);
				/* checks if player with such id has already been registered */
				(PlayerExist (Auction.Players~ PlayerId~) In)~ Exe
				/* waiting for correct player id */
				CroupierAssistantLifeStep~ Exe
			);
			/* all players 'on board' */
			(AllPlayersJoined true) ias (
				((Assistant reports that all Ew.PlayersOnAuction~ players have been registered and player PlayerId~ rejected) Ew.Output~) Do
				CroupierAssistantLifeStep~ Exe
			);
			/* no available command; waiting */
			(WaitForCommand true) ias (
				CroupierAssistantLifeStep~ Exe
			);
			/* some command received and should be processed */
			(WaitForCommand false) ias (
				/* wrong command  */
				(ProcessCommand false) ias (
					((Assistant reports about unknown command) Ew.Output~) Do
					CroupierAssistantLifeStep~ Exe
				);
				/* command is going to be processed */
				(ProcessCommand true) ias (
					leave ias (
						(AllPlayersLeft (Auction.ActivePlayers~ Ew.NoPlayers~) Ident)~ Exe
					);
					join ias (
						(AllPlayersJoined (Auction.ActivePlayers~ Ew.PlayersOnAuction~) Ident)~ Exe
					);
					bet ias (
						BetReceivingPermitted ias false;
						ReportAboutNonPermittedBetOperation ias (
							((Assistant reports that player PlayerId~ can bet after auction has been started) Ew.Output~) Do
						);
						(BetPermitted true true) ias (
							(PlayerExist true) ias (
								(DoNotAcceptPlayerBet true) ias (
									((Assistant reports that player PlayerId~ has already made bet) Ew.Output~) Do
									(HasPlayerAlreadyMadeBet false)^
								);
								(DoNotAcceptPlayerBet false) ias (
									(AcceptedBet (Command~)Last)^
									AcceptBet [
                                                				(Croupier.LastPlayersBets (Croupier.LastPlayersBets~ (PlayerId~ AcceptedBet~))Join)^
									AcceptBet ]
									((Assistant reports that player PlayerId~ made bet AcceptedBet~) Ew.Output~) Do
								);
								/* checks if player has made bet */
								FindPlayersBet~ Exe
								(DoNotAcceptPlayerBet FindPlayersBet.HasPlayerAlreadyMadeBet~)~ Exe
							);
							(PlayerExist false) ias (
								((Assistant reports that player PlayerId~ can not bet since it was not participates in auction) Ew.Output~) Do
							);
							/* Check if player has joined to auction */
							(PlayerExist (Auction.Players~ PlayerId~)In)~ Exe
						);
						(BetPermitted false false) ias (
							ReportAboutNonPermittedBetOperation~ Exe
						);
						(BetPermitted false true) ias (
							ReportAboutNonPermittedBetOperation~ Exe
						);
						(BetPermitted true false) ias (
							ReportAboutNonPermittedBetOperation~ Exe
						);
						/* waiting for bet permission for bet processing (conflicts with BetPermitted) */
						WaitForBetPermition [
							(BetPermitted Croupier.PermitBet)^
						WaitForBetPermition ]
						/* Check if bet is allowed; bet is allowed in case if auction started */
						(BetPermitted Croupier.AuctionStarted~ BetPermitted~~)~ Exe
						/* waiting for correct player id */
						CroupierAssistantLifeStep~ Exe
					);
					exit ias (
						/* prompt before exiting... */
						((Assistant tells to Croupier to stop auction) Ew.Output~) Do
						ForceToStopAuction~ Exe
					);
					CommandId~~ Exe
				);
				/* decompose command by getting command id; command is complex entity (commandId commandData ...) */
				(CommandId (Command~) First)^
				(Command (Command~) Rest)^
				(PlayerId (Command~) First)^
				((Assistant reports about command CommandId~ for player PlayerId~) Ew.Output~) Do
				/* check if command is in list of available commands */
				(IsCommandInList (PlayerCommands~ CommandId~) In)^
				/* CommandId should be applied to appropriate context */
				(CommandId~ (ProcessCommand IsCommandInList~)) Context
				/* executes actions depend on commandId */
				(ProcessCommand IsCommandInList~)~ Exe
			);
			/* waiting for command, in non-blocking manner */ 
			(Command (() Ew.Input~) Do)^
			(WaitForCommand (Command~ nil) Ident)~ Exe
		);

		/* Croupier assistant lifeterm */
		source lifeterm = CroupierAssistantLifeStep~ Exe
	);

	Croupier ias (
		AuctionStarted ias false;
		ForceToCloseAuction ias false;
		PermitBet ias false;
		/* available commands */
		Commands ias (selectCard permitBetOperation waitForBetFromPlayers announceWinner);
		/* current command  */
		Command ias selectCard;
		/* Last players' bet */
		LastPlayersBets ias ();	
		/* Croupier selects puzzle's card which is put up to auction  */
		PuzzleCard ias nil;
		/* Croupier processes Assistant's commands */
		/* Croupier's life step */
		CroupierLifeStep ias (
			(ContinueAuction true) ias (
				(ActiveCommand selectCard) ias (
					PublishedCard ias nil;
					/* Croupier takes random puzzle's card */
					(PuzzleCardDescriptor Ew.RestPuzzles~ Random)^
					(PublishedCard (PuzzleCardDescriptor~)First)^
					((Croupier selected card PublishedCard~ and it available for (PuzzleCardDescriptor~)Last~ times) Ew.Output~) Do
					/* selected card should be published to all players */
					CardPublishing [
						NotifyPlayersCardSelected ias (
							Id ias $;
							/* notifies player identified by complex entity '(Player Id~~) */
							((Player Id~~)->Card PublishedCard~)^
						);
						(Auction.Players~ NotifyPlayersCardSelected~) ForEach
					CardPublishing ]
					/* card was published to all players and next command is wait for players' bet */
					(Command permitBetOperation)^
				);
				(ActiveCommand permitBetOperation) ias (
					/* conflicts with WaitForBetReceivingPermition */
					BetPermitted [
						(PermitBet true)^
					BetPermitted ]
					(Command waitForBetFromPlayers)^
				);
				(ActiveCommand waitForBetFromPlayers) ias (
					(AllBetsCollected false) ias doNothing;
					/* all bets collected, so winner should be announced */
				        (AllBetsCollected true) ias (
						(PermitBet false)^
						(Command announceWinner)^
					);
					CheckStatusOfBetCollection [
						(AllBetsCollected ((Croupier.LastPlayersBets~)Size Auction.ActivePlayers~) Ident)~ Exe
					CheckStatusOfBetCollection ]
				);
                              	(ActiveCommand announceWinner) ias (
					(RemoveCardFromStorage true) ias (
						(Ew.RestPuzzles (Ew.RestPuzzles~ (PuzzleCardDescriptor~))Substruct)^
					);
					(RemoveCardFromStorage false) ias doNothing;
					((Croupier announced winner for card PublishedCard~) Ew.Output~) Do
					/* decrease card's counter which defines how many the same cards are in the pool */
					(PublishedCardCounter (PuzzleCardDescriptor~)Last)^
					(PublishedCardCounter~ ((PublishedCardCounter~~) Ew.DecNumberOfPlayers~) Do)^
					/* checks whether card should be removed from storage or no */
					(RemoveCardFromStorage (PublishedCardCounter~~ 0) Ident)~ Exe
					(PublishedCard nil)^
					(Command selectCard)^
				);
				(ActiveCommand Command~)~ Exe
				(ContinueAuction AuctionStarted~)~ Exe
			);
			(ContinueAuction false) ias (
				(ForceToClose true) ias (
					(PermitBet false)^
				);
				(ForceToClose false) ias (
					CroupierLifeStep~ Exe
				);
				(ForceToClose ForceToCloseAuction~)~ Exe
			);
			(ContinueAuction AuctionStarted~)~ Exe
		);
		/* Croupier lifeterm */
		source lifeterm = CroupierLifeStep~ Exe
	);

	Auction ias (
		/* number of active players  */
		ActivePlayers ias 0;
		/* initial players' list  */
		Players ias ();
	);

	Player ias (
		Id ias nil;
		Card ias nil;
		Bet ias 0;
		/* Player knows about joined and left players */
		JoinedPlayer ias nil;
		LeftPlayer ias nil;

		PlayerLifeStep ias (
			(NonActive true) ias doNothing;
		        (NonActive false) ias (
				500 Relax
			);
			(NonActive (Id~ nil)Ident)~ Exe
			PlayerLifeStep~ Exe
		);

		source lifeterm = (	
					(Id CroupierAssistant.PlayerId~)^
					PlayerLifeStep~ Exe
				) Exe
	);
}
