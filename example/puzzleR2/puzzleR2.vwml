options {
	language=__java__ {
  		package = "com.win.game.puzzle.model.vwml.puzzleR2"
  		path = "c:\var\vwml/puzzleR2/src"
  		author = "Groozman M.Z."
		project_name = "puzzleR2Proj"
  		description = "Antagonistic puzzle game"
		beyond {
			fringe communication ias (
				out ias "com.win.game.puzzleR2.model.fringe.gate.async.console.AsyncConsole"
				in ias "com.win.game.puzzleR2.model.fringe.gate.async.console.AsyncConsole"
			)
			fringe services ias (
				math ias "com.vw.lang.beyond.java.fringe.gate.math.Math"
				config ias "com.win.game.puzzleR2.model.fringe.gate.config.ConfigurationLoader"
				winner ias "com.win.game.puzzleR2.model.fringe.gate.ext.strategy.WinnerSelector"
			)
		}
		conflictring {
			"{CroupierAssistant}.CroupierAssistantLifeStep.(AllPlayersLeft false).(PlayerExist true).LeavePlayer" conflicts (
				"{Croupier}.CroupierLifeStep.(ContinueAuction true).(ActiveCommand analyzeBets).CheckStatusOfBetCollection"
				"{Croupier}.CroupierLifeStep.(ContinueAuction true).(ActiveCommand announceWinner).SelectWinner"
			)
			"{CroupierAssistant}.CroupierAssistantLifeStep.(AllPlayersJoined false).(PlayerExist false).JoinPlayer" conflicts (
				"{Croupier}.CroupierLifeStep.(ContinueAuction true).(ActiveCommand analyzeBets).CheckStatusOfBetCollection"
				"{Croupier}.CroupierLifeStep.(ContinueAuction true).(ActiveCommand announceWinner).SelectWinner"
			)
			"{CroupierAssistant}.CroupierAssistantLifeStep.(WaitForCommand false).(ProcessCommand true).bet.(BetPermitted true true).(PlayerExist true).BetsAccepting" conflicts (
				"{Croupier}.CroupierLifeStep.(ContinueAuction true).(ActiveCommand waitForBetFromPlayers).LockBetsProgress"
			)
			"{Ew}.ProcessDPCommand.FetchDPCommand" conflicts (
				"{Player}.AskToLoadCorrespondenceMap.LoadCorrespondenceMap" "{Croupier}.LoadAdditionalPuzzleElements.LoadPuzzleElements"
			)
			"{Player}.AskToLoadCorrespondenceMap.LoadCorrespondenceMap" conflicts (
				"{Player}.AskToLoadCorrespondenceMap.LoadCorrespondenceMap"
			)
		}
	}
}

module puzzleR2 {
	/*
		EW represents external world data which can be loaded during game's 
		initialization phase
	*/
	Ew ias (
		FreePlaces ias ();
                RestPuzzles ias ();
                Correspondence ias ();
		Command ias wait;
		DPCommands ias ();
		DPCommand ias nil;
		PlayerId ias nil;
		ReadyToStart ias false;
		Input ias communication.in.read;
		Output ias communication.in.write;
		ConfigLoader ias services.config.load;
		IncNumberOfPlayers ias services.math.inc;
		DecNumberOfPlayers ias services.math.dec;
		Compare ias services.math.compare;
		DecreaseCredit ias services.math.substr;
		IncreaseCredit ias services.math.sum;
		WinnerSelector ias services.winner.select;
		PlayersOnAuction ias 2;
		NoPlayers ias 0;
		PlayerCredit ias 100;
		BetTimeout ias 10000;

		ProcessDPCommand ias (
			FetchDPCommand [
				(emptyQ false) ias (
					(DPCommand (DPCommands~) First)^
					((Processed DP command DPCommand~) Ew.Output~) Do
					(Command (DPCommand~) First)^
					(PlayerId (DPCommand~) Last)^
					(DPCommands (DPCommands~ (DPCommand~))Substruct)^
					((Available DP commands DPCommands~) Ew.Output~) Do
				);
				(emptyQ true) ias doNothing;
				(emptyQ (DPCommands~ ()) Ident)~ Exe 	
			FetchDPCommand ]
		);

		EwLifeStep ias (
			/* waiting for command from assistant or croupier */
			(ew wait) ias (
				ProcessDPCommand~ Exe
 				EwLifeStep~ Exe
			);
			/* load configuration */
			(ew loadConfig),(ew reloadConfig) ias (
				(FreePlaces ((freePlaces) ConfigLoader~) Do)^
				(RestPuzzles ((restPuzzles) ConfigLoader~) Do)^
				((Configuration loaded) Ew.Output~) Do
				(ReadyToStart true)^
				(Command wait)^
				EwLifeStep~ Exe
			);
			(ew loadCorrespondence) ias (
				(notForConcretePlayer true) ias doNothing;
				(notForConcretePlayer false) ias (
					((Player PlayerId~)->Correspondence ((correspondence) ConfigLoader~) Do)^
					((Player PlayerId~)->Command loadedCorrespondenceMap)^
				);
				(notForConcretePlayer (PlayerId~ nil)Ident)~ Exe
				(Command wait)^
				EwLifeStep~ Exe
			);
			(ew loadPuzzleElements) ias (
				(RestPuzzles ((restPuzzles) ConfigLoader~) Do)^
				((The same elements of the same puzzle have been reloaded) Ew.Output~) Do
				(Croupier.AuctionStarted true)^
				(Command wait)^
				EwLifeStep~ Exe
			);
			(ew exit) ias doNothing;
			(ew Command~)~ Exe
		);
		/* Croupier assistant lifeterm */
		source lifeterm = EwLifeStep~ Exe
	);

	CroupierAssistant ias (
		/* available commands which can be received from external world  */
		PlayerCommands ias (leave join bet bets credit exit);
		/* available commands which can be received from internal world */
		InternalCommands ias (startNewAuction);
		InternalCommand ias none;
		/* read command */
		Command ias nil;
		CommandId ias nil;
		PlayerId ias nil;

		/* Updates player's bet */
		UpdatePlayerBet ias (
			RollbackBet ias (
                        	(Croupier.LastPlayersBets (Croupier.LastPlayersBets~ CurrentPlayerBet~)Join)^
				((Player PlayerId~)->Credit (((Player PlayerId~)->Credit~ (CurrentPlayerBet~) Last) Ew.DecreaseCredit~) Do)^
			);
			UpdatedBet ias 0;
			CurrentPlayerBet ias nil;
			PlayerCurrentCredit ias 0;
			(Croupier.LastPlayersBets (Croupier.LastPlayersBets~ (CurrentPlayerBet~))Substruct)^
			((Player PlayerId~)->Credit (((Player PlayerId~)->Credit~ (CurrentPlayerBet~) Last) Ew.IncreaseCredit~) Do)^
			(AcceptPlayerBet.AcceptedBet UpdatedBet~)^
			(AcceptPlayerBet.RollbackBet RollbackBet~)^
			AcceptPlayerBet~ Exe
			(AcceptPlayerBet.RollbackBet nil)^
		);

		/* Accepts player's bet */
		AcceptPlayerBet ias (
			AcceptedBet ias 0;
			RollbackBet ias nil;
			PlayerCurrentCredit ias 0;
			/* checks player credit */
			(BetCheckingResult 0),(BetCheckingResult 1) ias (
                        	(Croupier.LastPlayersBets (Croupier.LastPlayersBets~ (PlayerId~  AcceptedBet~))Join)^
				((Player PlayerId~)->Credit ((PlayerCurrentCredit~ AcceptedBet~) Ew.DecreaseCredit~) Do)^
				(PlayerCurrentCredit (Player PlayerId~)->Credit~)^
				((Assistant reports that player PlayerId~ made bet AcceptedBet~ credit PlayerCurrentCredit~) Ew.Output~) Do
			);
			(BetCheckingResult -1) ias (
				(doNotDoRollback false) ias RollbackBet~ Exe;
				(doNotDoRollback true) ias doNothing;
				(doNotDoRollback (RollbackBet~ nil) Ident)~ Exe
				((Assistant reports that player PlayerId~ does not have enough credits) Ew.Output~) Do
			);
			/* checks whether player has enough resources (credit) */
			(PlayerCurrentCredit (Player PlayerId~)->Credit~)^
			(BetCheckingResult ((PlayerCurrentCredit~ AcceptedBet~) Ew.Compare~) Do)~ Exe
		);

		/* check if player made bet */
		FindPlayersBet ias (
			HasPlayerAlreadyMadeBet ias false;
			PlayerBet ias nil;
			CheckIfPlayerHasAlreadyMadeBet ias (
				B ias $;
				(PlayerMadeBet false) ias doNothing;
				(PlayerMadeBet true) ias (
					(HasPlayerAlreadyMadeBet true)^
					(PlayerBet B~~)^ 
					/* signals to 'ForEach' to stop iteration */
					($ doNothing)^
				);
				(PlayerMadeBet ((B~~)First PlayerId~) Ident)~ Exe
			);
			(HasPlayerAlreadyMadeBet false)^
			(Croupier.LastPlayersBets~ CheckIfPlayerHasAlreadyMadeBet~) ForEach
		);		
		/* Stops auction - all players should leave auction */
		StopAuctionButNotClose ias (
			NotifyPlayersAuctionIsClosed ias (
				/* $ is synthetic entity which is interpreted as iterated component of complex entity on operation ForEach */
				Id ias $;
				/* notifies player identified by complex entity '(Player Id~~) */
				((Player Id~~)->AuctionStoppedByAssistantCommand true)^
				((Croupier closes auction and tells to player Id~~ to leave auction) Ew.Output~) Do
			);
			(Auction.Players~ NotifyPlayersAuctionIsClosed~) ForEach
			/* master player, with id == null, (other players are cloned from it) should be stopped also */
			((Player)->AuctionStoppedByAssistantCommand true)^
			(Croupier.AuctionStarted false)^
		);
		/* Assistant asks to stop auction immidiately */
		ForceToStopAuction ias (
			StopAuctionButNotClose~ Exe
			(Croupier.ForceToCloseAuction true)^
			(WiseClock.Command stop)^
			WiseClock Interrupt
			(Ew.Command exit)^ 
		);
		/* Starts auction according to conditions */
		StartAuctionByCondition ias (
			DefaultActionOnAuctionNotStarted ias (
		        	((Assistant reports that auction can not be started due to lack of players) Ew.Output~) Do
			);
			DoNotDoAnyActionOnAuctionNotStarted ias doNothing;
			ActionOnAuctionNotStarted ias DoNotDoAnyActionOnAuctionNotStarted;
			/* Auction should be started */
			(StartAuction true false) ias (
				/* initializing puzzle */
				InitializePuzzleCounter ias (
					(($~)Last Auction.ActivePlayers~)^
				);
				(Ew.RestPuzzles~ InitializePuzzleCounter~) ForEach
				(Croupier.AuctionStarted true)^
				((Croupier starts auction) Ew.Output~) Do
			);
			/* do not do anything */
			(StartAuction true true),(StartAuction false false) ias ActionOnAuctionNotStarted~~ Exe;

			(StartAuction (Auction.ActivePlayers~ Ew.PlayersOnAuction~)Ident Croupier.AuctionStarted~)~ Exe
			(ActionOnAuctionNotStarted DoNotDoAnyActionOnAuctionNotStarted)^
		);
		/* Stops auction according to conditions */
		StopAuctionByCondition ias (
			/* Auction should be stopped */
			(StopAuction true true) ias (
				(Croupier.AuctionStarted false)^
				Croupier.ResetAuctionProps~ Exe
				((Croupier stops auction) Ew.Output~) Do
			);
			/* do not do anything */
			(StopAuction false false),(StopAuction false true),(StopAuction true false) ias doNothing;
			
			(StopAuction (Auction.ActivePlayers~ Ew.NoPlayers~)Ident Croupier.AuctionStarted~)~ Exe
		);
		CroupierAssistantLifeStep ias (
			(AllPlayersLeft false) ias (
				/* player exist, try again */
				(PlayerExist false) ias (
					/* notify about invalid or unknown player */
					((Assistant reports that player PlayerId~ is unknown) Ew.Output~) Do
					/* waiting for correct player id */
					CroupierAssistantLifeStep~ Exe
				);
				/* player leaves */
				(PlayerExist true) ias (
					/* lookups for player which left and removes its bet, if exist */
					/* notifies rest players about left player */
					NotifyPlayersLeftPlayer ias (
						/* $ is synthetic entity which is interpreted as iterated component of complex entity on operation ForEach */
						Id ias $;
						/* notifies player identified by complex entity '(Player Id~~) */
						((Assistant notifies player Id~~ about left player PlayerId~) Ew.Output~) Do
					);
					((Assistant removes player PlayerId~ from auction) Ew.Output~) Do
					LeavePlayer [
						/* decrements number of players */
						(Auction.ActivePlayers ((Auction.ActivePlayers~) Ew.DecNumberOfPlayers~) Do)^
						FindPlayersBet~ Exe
						(Croupier.LastPlayersBets (Croupier.LastPlayersBets~ (FindPlayersBet.PlayerBet~))Substruct)^
					LeavePlayer ]
					/* removes player from players group */
					(Auction.Players (Auction.Players~ (PlayerId~)) Substruct)^
					/* notifies all players in 'real world' about left player */
					(Auction.Players~ NotifyPlayersLeftPlayer~) ForEach
					((Player PlayerId~)->AuctionStoppedByAssistantCommand true)^
					/* notify Croupier; may be auction should be stopped */
					StopAuctionByCondition~ Exe
					/* next assistant's step */
					CroupierAssistantLifeStep~ Exe
				);
				/* checks if player with such id has already been registered */
				(PlayerExist (Auction.Players~ PlayerId~) In)~ Exe
			);
			/* here are no any player 'on board' */
			(AllPlayersLeft true) ias (
				((Assistant reports that all Ew.PlayersOnAuction~ players have left) Ew.Output~) Do
				CroupierAssistantLifeStep~ Exe
			);
			/* waiting for required number of players */
			(AllPlayersJoined false) ias (
				/* player exist, try again */
				(PlayerExist true) ias (
					/* notify about duplicated id */
					((Assistant reports that player PlayerId~ has already been registered) Ew.Output~) Do
				);
				/* new player  */
				(PlayerExist false) ias (
					NotifyPlayersJoinPlayer ias (
						/* $ is synthetic entity which is interpreted as iterated component of complex entity on operation ForEach */
						Id ias $;
						/* notifies player identified by complex entity '(Player Id~~) */
						((Assistant notifies player Id~~ about joined player PlayerId~) Ew.Output~) Do
					);
					((Assistant joins player PlayerId~ to auction) Ew.Output~) Do
					/* notifies all players in 'real world' about new player */
					(Auction.Players~ NotifyPlayersJoinPlayer~) ForEach
					/* player joined to players group; now it should be cloned; cloned Player is run automaticaly */
					(Player (Player PlayerId~)) Clone
					/* join player to players group */
					(Auction.Players (Auction.Players~ PlayerId~) Join)^
					JoinPlayer [
						/* increments number of players */
						(Auction.ActivePlayers ((Auction.ActivePlayers~) Ew.IncNumberOfPlayers~) Do)^
					JoinPlayer ]
					/* notify Croupier; may be auction should be started */
					StartAuctionByCondition~ Exe
				);
				/* checks if player with such id has already been registered */
				(PlayerExist (Auction.Players~ PlayerId~) In)~ Exe
				/* waiting for correct player id */
				CroupierAssistantLifeStep~ Exe
			);
			/* all players 'on board' */
			(AllPlayersJoined true) ias (
				((Assistant reports that all Ew.PlayersOnAuction~ players have been registered and player PlayerId~ rejected) Ew.Output~) Do
				CroupierAssistantLifeStep~ Exe
			);
			/* no available command; waiting */
			(WaitForCommand true) ias (
				CroupierAssistantLifeStep~ Exe
			);
			/* some command received and should be processed */
			(WaitForCommand false) ias (
				/* wrong command  */
				(ProcessCommand false) ias (
					((Assistant reports about unknown command) Ew.Output~) Do
					CroupierAssistantLifeStep~ Exe
				);
				/* command is going to be processed */
				(ProcessCommand true) ias (
					leave ias (
						(AllPlayersLeft (Auction.ActivePlayers~ Ew.NoPlayers~) Ident)~ Exe
					);
					join ias (
						(AllPlayersJoined (Auction.ActivePlayers~ Ew.PlayersOnAuction~) Ident)~ Exe
					);
					bet ias (
						BetReceivingPermitted ias false;
						ReportAboutNonPermittedBetOperation ias (
							((Assistant reports that player PlayerId~ can not bet after auction has been started) Ew.Output~) Do
						);
						(BetPermitted true true) ias (
							(PlayerExist true) ias (
								(UpdatePlayerBet true) ias (
									(UpdatePlayerBet.UpdatedBet (Command~)Last)^
									(UpdatePlayerBet.CurrentPlayerBet FindPlayersBet.PlayerBet~)^
									UpdatePlayerBet~ Exe
								);
								(UpdatePlayerBet false) ias (
									/* accepts player credit */
									(AcceptPlayerBet.AcceptedBet (Command~)Last)^
									AcceptPlayerBet~ Exe
								);
								/* checks if player has made bet */
								BetsAccepting [
									FindPlayersBet~ Exe
									(UpdatePlayerBet FindPlayersBet.HasPlayerAlreadyMadeBet~)~ Exe
								BetsAccepting ]
							);
							(PlayerExist false) ias (
								((Assistant reports that player PlayerId~ can not bet since it was not participates in auction) Ew.Output~) Do
							);
							/* Check if player has joined to auction */
							(PlayerExist (Auction.Players~ PlayerId~)In)~ Exe
						);
						(BetPermitted false false),(BetPermitted false true),(BetPermitted true false) ias (
							ReportAboutNonPermittedBetOperation~ Exe
						);
						/* waiting for bet permission for bet processing (conflicts with BetPermitted) */
						(BetPermitted Croupier.PermitBet~)^
						/* Check if bet is allowed; bet is allowed in case if auction started */
						(BetPermitted Croupier.AuctionStarted~ BetPermitted~)~ Exe
						/* waiting for correct player id */
						CroupierAssistantLifeStep~ Exe
					);
					bets ias (
						((Assistant bets Croupier.LastPlayersBets~) Ew.Output~) Do
						CroupierAssistantLifeStep~ Exe
					);
					credit ias (
						(PlayerExist true) ias (
							((Credit of player PlayerId~ is (Player PlayerId~)->Credit~) Ew.Output~) Do
						);
						(PlayerExist false) ias doNothing;
						(PlayerExist (Auction.Players~ PlayerId~) In)~ Exe
						CroupierAssistantLifeStep~ Exe
					);
					exit ias (
						/* prompt before exiting... */
						((Assistant tells to Croupier to stop auction) Ew.Output~) Do
						ForceToStopAuction~ Exe
					);
					CommandId~~ Exe
				);
				/* decompose command by getting command id; command is complex entity (commandId commandData ...) */
				(CommandId (Command~) First)^
				(Command (Command~) Rest)^
				(PlayerId (Command~) First)^
				((Assistant reports about command CommandId~ for player PlayerId~) Ew.Output~) Do
				/* check if command is in list of available commands */
				(IsCommandInList (PlayerCommands~ CommandId~) In)^
				/* CommandId should be applied to appropriate context */
				(CommandId~ (ProcessCommand IsCommandInList~)) Context
				/* executes actions depend on commandId */
				(ProcessCommand IsCommandInList~)~ Exe
			);
			(Ready true) ias (
				/* waiting for command, in non-blocking manner */ 
				(Command (() Ew.Input~) Do)^
				(WaitForCommand (Command~ nil) Ident)~ Exe
			);
			(Ready false) ias (
				((waiting for ready state) Ew.Output~) Do
				CroupierAssistantLifeStep~ Exe
			);
			/* internal commands initiated by croupier or other entity; should be processed before any other commands */
			(ProcessInternalCommand true) ias (
				(internal startNewAuction) ias (
					(StartAuctionByCondition.ActionOnAuctionNotStarted StartAuctionByCondition.DefaultActionOnAuctionNotStarted)^
					StartAuctionByCondition~ Exe
					(InternalCommand none)^
				); 
				(internal InternalCommand~)~ Exe
			);
			(ProcessInternalCommand false) ias doNothing;
			(ProcessInternalCommand (InternalCommands~ InternalCommand~) In)~ Exe
			(Ready Ew.ReadyToStart~)~ Exe // TODO: should be sync
		);

		/* Croupier assistant lifeterm */
		source lifeterm = (
			((Assistant loads configuration of the auction) Ew.Output~) Do
			(Ew.Command loadConfig)^
			CroupierAssistantLifeStep~ Exe
		) Exe
	);

	Croupier ias (
		AuctionStarted ias false;
		ForceToCloseAuction ias false;
		AskAssistantAboutContinueAuction ias false;
		PermitBet ias false;
		/* available commands */
		Commands ias (selectCard permitBetOperation waitForBetFromPlayers announceWinner);
		/* current command  */
		Command ias selectCard;
		/* Last players' bet */
		LastPlayersBets ias ();	
		/* Croupier selects puzzle's card which is put up to auction  */
		PuzzleCard ias nil;
		/* last published card and its descriptor */
		PuzzleCardDescriptor ias nil;
		PublishedCard ias nil;
		/* Winner (for current card) */
		Winner ias nil;
		/* Winner (auction) */
		AuctionWinner ias nil;

		/* Loads additional puzzle's elements */
		LoadAdditionalPuzzleElements ias (
			LoadPuzzleElements [
				((Croupier asks to load additional puzzle elements) Ew.Output~) Do
				(Ew.DPCommands (Ew.DPCommands~ (loadPuzzleElements Croupier))Join)^
			LoadPuzzleElements ]
		);
		/* Resets auction's properties */
		ResetAuctionProps ias (
			(PermitBet false)^
			(PublishedCard nil)^
			(LastPlayersBets ())^
			(Command selectCard)^
		);
		/* starts new session */
		CroupierStartsNewAuctionSession ias (
			/* All cards have been sold */
			/* All users' bets should be reset also */
			(Croupier.LastPlayersBets ())^
			(Croupier.AuctionWinner nil)^
			/* Auction stopped */
			(AuctionStarted false)^
			/* Croupier asks whether auction should be continued or no */
			((Croupier continues auction with new puzzle set) Ew.Output~) Do
			/* tells to Ew that configuration should be reloaded */
			(Ew.ReadyToStart false)^
			/* loads new puzzle */
			(Ew.Command reloadConfig)^
			NotifyPlayersReloadConfig ias (
				Id ias $;
				/* notifies player identified by complex entity '(Player Id~~) */
				((Player Id~~)->Command askToLoadCorrespondenceMap)^
			);
			(Auction.Players~ NotifyPlayersReloadConfig~) ForEach
			/* this action will be executed in case if auction can't be started by some causes */
			(CroupierAssistant.InternalCommand startNewAuction)^
		);
		/* selects and publishes card */
		CroupierSelectsCard ias (
			/* Croupier takes random puzzle's card */
			(PuzzleCardDescriptor Ew.RestPuzzles~ Random)^
			(PublishedCard (PuzzleCardDescriptor~)First)^
			((Croupier selected card PublishedCard~ and it available for (PuzzleCardDescriptor~)Last~ times) Ew.Output~) Do
			/* selected card should be published to all players */
			NotifyPlayersCardSelected ias (
				Id ias $;
				/* notifies player identified by complex entity '(Player Id~~) */
				((Player Id~~)->Card PublishedCard~)^
			);
			(Auction.Players~ NotifyPlayersCardSelected~) ForEach
		);
		/* Croupier processes Assistant's commands */
		/* Croupier's life step */
		CroupierLifeStep ias (
			(ContinueAuction true) ias (
				(ActiveCommand selectCard) ias (
					(WinnerIsNotAccepted false) ias (
						CroupierStartsNewAuctionSession~ Exe
						(Command selectCard)^
					);
					(WinnerIsNotAccepted true) ias (
						CroupierSelectsCard~ Exe
						/* card was published to all players and next command is wait for players' bet */
						(Command permitBetOperation)^
					);
					(WinnerIsNotAccepted (AuctionWinner~ nil) Ident)~ Exe
				);
				(ActiveCommand permitBetOperation) ias (
					/* activates wise clock */
					(WiseClock.Command betsTimeout)^
					(Command waitForBetFromPlayers)^
				);
				(ActiveCommand waitForBetFromPlayers) ias (
					(BetsInProgress true) ias doNothing;
					(BetsInProgress false) ias (
						((Croupier reports that bets have already been made) Ew.Output~) Do
						((Croupier reports that no bets allowed) Ew.Output~) Do
						(Command analyzeBets)^
					);
					LockBetsProgress [
						(BetsInProgress PermitBet~)~ Exe
					LockBetsProgress ]
				);
				(ActiveCommand analyzeBets) ias (
					/* bets collected, so winner should be announced */
					(NoBets false) ias (Command announceWinner)^;
				        (NoBets true) ias (
						((No bets for PublishedCard~ were made) Ew.Output~) Do
						ResetAuctionProps~ Exe
					);
					CheckStatusOfBetCollection [
						(NoBets ((LastPlayersBets~)Size 0) Ident)~ Exe
					CheckStatusOfBetCollection ]
				);
                              	(ActiveCommand announceWinner) ias (
					Winner ias nil;
					AnnounceWinner ias (
						((Croupier announced winner Winner~ for card PublishedCard~) Ew.Output~) Do
						((Player Winner~)->WonCard PublishedCard~)^
						((Player Winner~)->Command win)^
					);
					(RemoveCardFromStorage true) ias (
						(NoPuzzleElements false) ias (
							(Ew.RestPuzzles (Ew.RestPuzzles~ (PuzzleCardDescriptor~))Substruct)^
							((Card PuzzleCardDescriptor~ was removed from puzzle set) Ew.Output~) Do
						);
						(NoPuzzleElements true) ias (
							((Croupier reports that all puzzle elements were sold) Ew.Output~) Do
							(AuctionStarted false)^
							ResetAuctionProps~ Exe
							LoadAdditionalPuzzleElements~ Exe
						);
						(NoPuzzleElements (Ew.RestPuzzles~ ())Ident)~ Exe
					);
					(RemoveCardFromStorage false) ias (
						((PublishedCard~ will be available for PublishedCardCounter~~ times) Ew.Output~) Do
					);
					(OnePlayerStayesOnAuction true) ias (
						(Winner (Croupier.LastPlayersBets~)First First)^
						AnnounceWinner~ Exe
					);
					(OnePlayerStayesOnAuction false) ias (
						(Winner (Croupier.LastPlayersBets~ Ew.WinnerSelector~) Do First)^
						AnnounceWinner~ Exe
					);
					SelectWinner [
						((Bets for PublishedCard~ are Croupier.LastPlayersBets~) Ew.Output~) Do
						(OnePlayerStayesOnAuction ((Croupier.LastPlayersBets~)Size 1) Ident)~ Exe
					SelectWinner ]
					/* decrease card's counter which defines how many the cards, of this type, keep in the pool */
					(PublishedCardCounter (PuzzleCardDescriptor~)Last)^
					(PublishedCardCounter~ ((PublishedCardCounter~~) Ew.DecNumberOfPlayers~) Do)^
					/* checks whether card should be removed from storage or no */
					(RemoveCardFromStorage (PublishedCardCounter~~ 0) Ident)~ Exe
					ResetAuctionProps~ Exe
				);
				(ActiveCommand Command~)~ Exe
				(ContinueAuction AuctionStarted~)~ Exe
			);
			(ContinueAuction false) ias (
				(ForceToClose true) ias (
					ResetAuctionProps~ Exe
				);
				(ForceToClose false) ias (
					CroupierLifeStep~ Exe
				);
				(ForceToClose ForceToCloseAuction~)~ Exe
			);
			(ContinueAuction AuctionStarted~)~ Exe
		);
		/* Croupier lifeterm */
		source lifeterm = CroupierLifeStep~ Exe
	);

	WiseClock ias (
		Command ias none;

		WiseClockLifeStep ias (
			(clock none) ias (
				WiseClockLifeStep~ Exe
			);
			(clock stop) ias (
				((Wise clock deactivated) Ew.Output~) Do
			);
			(clock betsTimeout) ias (
				DeactivateClockOnInterruption ias (
					((Wise clock interrupted) Ew.Output~) Do
					(Command stop)^
				);
				DisableBetsRegistrationWhenTimerExpired ias (
					((Wise clock tells to croupier to stop make bets) Ew.Output~) Do
					(Croupier.PermitBet false)^
					(Command none)^
				);
				(Croupier.PermitBet true)^
				((Wise clock got signal to start bets timeout) Ew.Output~) Do
				(Ew.BetTimeout~ DisableBetsRegistrationWhenTimerExpired~ DeactivateClockOnInterruption~) Relax
				WiseClockLifeStep~ Exe
			);
			(clock Command~)~ Exe
		);
		/* WiseClock lifeterm */
		source lifeterm = (
					((Wise clock activated) Ew.Output~) Do
					WiseClockLifeStep~ Exe
				) Exe
	);

	Auction ias (
		/* number of active players  */
		ActivePlayers ias 0;
		/* initial players' list  */
		Players ias ();
	);

	Player ias (
		Id ias nil;
		Card ias nil;
		WonCard ias nil;
		Credit ias 0;
		Bet ias 0;
		/* Player knows about joined and left players */
		JoinedPlayer ias nil;
		LeftPlayer ias nil;
		/* sets by croupier assistant to 'true' when auction should be stopped */
		AuctionStoppedByAssistantCommand ias false;
		Command ias none;
		/* Puzzle's correspondence map  */
		Correspondence ias ();

		AskToLoadCorrespondenceMap ias (
			LoadCorrespondenceMap [
				((Player Id~ asks to load correspondence map) Ew.Output~) Do
				(Ew.DPCommands (Ew.DPCommands~ (loadCorrespondence Id~))Join)^
			LoadCorrespondenceMap ]
		);

		PlayerLifeStep ias (
			(NonActive true) ias (
				(AuctionStopped true) ias (
					((Player Id~ left auction) Ew.Output~) Do
				);
				(AuctionStopped false) ias (
					PlayerLifeStep~ Exe
				);
				(AuctionStopped AuctionStoppedByAssistantCommand~)~ Exe
			);
		        (NonActive false) ias (
				(AuctionStopped true) ias (
					((Player Id~ left auction) Ew.Output~) Do
				);
				(AuctionStopped false) ias (
					(command none) ias doNothing;
					(command askToLoadCorrespondenceMap) ias (
						AskToLoadCorrespondenceMap~ Exe
						(Command none)^
					);
					(command loadedCorrespondenceMap) ias (
						((Player Id~ loaded correspondence map Correspondence~) Ew.Output~) Do
						(Command none)^
					);
					(command win) ias (
						Place ias nil;
						FindCardPlace ias (
							B ias $;
							(FoundPlace false) ias doNothing;
							(FoundPlace true) ias (
								(Place B~~)^
								(Correspondence (Correspondence~ (Place~))Substruct)^
								((Player Id~ puts card WonCard~ to place (Place~) Last and map is Correspondence~) Ew.Output~) Do
								($ doNothing)^
							);
							(Place nil)^
							(FoundPlace ((B~~)Last WonCard~)Ident)~ Exe
						);
						(AuctionWinner false) ias doNothing;
						(AuctionWinner true) ias (
							(Croupier.AuctionWinner Id~)^
						);
						(Correspondence~ FindCardPlace~) ForEach
						(AuctionWinner (Correspondence~ ())Ident)~ Exe
						(WonCard nil)^
						(Command none)^
					);
					(command Command~)~ Exe
					PlayerLifeStep~ Exe
				);
				(AuctionStopped AuctionStoppedByAssistantCommand~)~ Exe
			);
			(NonActive (Id~ nil)Ident)~ Exe
		);

		lifeterm = (
					(Id CroupierAssistant.PlayerId~)^
					(Credit Ew.PlayerCredit~)^
					(Command askToLoadCorrespondenceMap)^
					((Player Id~ activated and its credit is Credit~) Ew.Output~) Do	
					PlayerLifeStep~ Exe
				) Exe
	);
}
